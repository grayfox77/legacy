;
;                      Mask3VSB.SMM
;
;  "Mask III - Venom Strikes Back" Source Game Music Module
;
; Version Amalthea-1-1 (1st Ver., 1st Release, 1st Revis.)
;
;   This  MSX  Assembly  source file is part of "Yawara:
;   General Game  Music Player". Please, read the notice
;   and documentation file "Yawara.html". If you use the
;   program  in any way  you are  automatically agreeing
;   with  everything stated in  that  file, even if  you
;   haven't read it, so  I  recommend you  do  it. Thank
;   you for your cooperation, good night.
;
;    This file was created, edited and programmed from
;    1994  to 1998 by  "Cyberknight" Masao Kawata  for
;    "Unicorn Dreams Artwork Programs".  For  contact,
;    send an E-Mail to:
;
;                cyber_unicorn@hotmail.com
;
;    Check also the Home Page for new releases:
;
;            http://unicorndreams.home.ml.org
;

;
;   Compile using M80 and L80:
;
;   M80 =Mask3VSB.SMM
;   L80 Mask3VSB,Mask3VSB.GMM /n/e
;

;
;   To execute, run the program loader ("Yawara.Bin" or
;   "Yawara.Saf"). Please, read the  documentation file
;   ("Yawara.html").
;

;
;   "Mask III - Venom Strikes Back" game mapping:
;
;   0000                               FFFF
;      |M|?|?|M|?|?|?|?|?|?|?|?|?|?|?|?|
;
;   M = Game Music Routines and Data.
;


; MSX standard addresses and constants.

HKEYI  equ  0FD9AH
HTIMI  equ  0FD9FH

LINTTB equ  0FBB2H
SysDB  equ  LINTTB
SavHok equ  SysDB+010H

HOLD8  equ  0F806H
EBTb0  equ  HOLD8
Where  equ  EBTb0+039H

FNKSTR equ  0F87FH
EBDBnk equ  FNKSTR
SaveSP equ  EBDBnk+0000FH


       .Z80
       aseg

       org  00100H
       .Phase 00100H

GameDt:db   ' Mask III - Venom Strikes Back  '
GmBlPt:dw   GmBlNm-GameDt
StupRP:dw   SetupR-GameDt
SilnRP:dw   SilnRt-GameDt
StMsRP:dw   StMsRt-GameDt
PlayRP:dw   PlayRt-GameDt
GMPBAd:dw   0C000H
GMPNam:db   'Yawara  C00'
OpenMN:db   000H
StrtSE:db   000H
ChATDD:dw   0E011H
ChBTDD:dw   0E01CH
ChCTDD:dw   0E027H
BGMSPt:dw   MusicT-GameDt
SFXSPt:dw   EffctT-GameDt
OpnGFN:db   'Msk3VSB0Sc2'
OpnGFS:dw   04000H
MnuGFN:db   'Msk3VSB1Sc2'
MnuGFS:dw   04000H
OpnStC:dw   21*32+10
Resorc:db   00010010B

SetupR:jr   z,Start
       ld   de,07001H
       ld   a,(de)
       ld   (SavHok),a
       inc  de
       ld   a,(de)
       ld   (HTIMI),a
       ld   hl,08000H        ; Restore BIOS segment.
       ld   de,00000H
       ld   bc,04000H
       ldir
       pop  hl
       ld   sp,(SaveSP)
       jp   (hl)

Start: ld   a,(04000H)
       cp   0C5H
       jr   nz,SilnRt
       pop  ix
       ld   sp,08FFFH
       ld   hl,00000H        ; Save BIOS segment.
       ld   de,08000H
       ld   bc,04000H
       ldir
       ld   hl,04000H        ; Move game block.
       ld   de,00500H
       ld   bc,01800H
       ldir
       push ix
       ld   a,0C9H
       ld   hl,SavHok
       ld   de,07001H
       ldi
       dec  hl
       ld   (hl),a
       ld   hl,HTIMI
       ldi
       dec  hl
       ld   (hl),a
       call Where
Here:  ld   de,There-Here
       add  hl,de
       ld   de,00038H
       ld   bc,GmBlNm-There
       ldir
       ld   a,0BFH
       ld   (00EFEH),a
SilnRt:xor  a
StMsRt:ld   (07000H),a
       or   a
       jp   z,00EF7H
       dec  a
       jp   00600H

PlayRt:ld   a,(07000H)
       or   a
       ret  z
       jp   00688H

There: push hl
       push de
       push bc
       push af
       exx
       ex   af,af'
       push hl
       push de
       push bc
       push af
       push iy
       push ix
       call HKEYI

GmBlNm:db   'Mask3VSB000'
GmBlSz:dw   01800H
LoadAd:dw   04000H
       dw   0FFFFH
MusicT:db   001H,' Mask 3 - Opening Theme'
       db   003H,'   Venom Strikes Back  '
       db   002H,'    Mission Failure    '
       db   0FFH,0FFH
EffctT:db   000H,'       Silence...      '
       db   0FFH,0FFH

       end
