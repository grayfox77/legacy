;
;                      Nonamed.SMM
;
;           "Nonamed" Source Game Music Module
;
; Version Amalthea-1-0 (1st Ver., 1st Release, No Revis.)
;
;   This  MSX  Assembly  source file is part of "Yawara:
;   General Game  Music Player". Please, read the notice
;   and documentation file "Yawara.html". If you use the
;   program  in any way  you are  automatically agreeing
;   with  everything stated in  that  file, even if  you
;   haven't read it, so  I  recommend you  do  it. Thank
;   you for your cooperation, good night.
;
;    This file was created, edited and programmed from
;    1994  to 1998 by  "Cyberknight" Masao Kawata  for
;    "Unicorn Dreams Artwork Programs".  For  contact,
;    send an E-Mail to:
;
;                cyber_unicorn@hotmail.com
;
;    Check also the Home Page for new releases:
;
;            http://unicorndreams.home.ml.org
;

;
;   Compile using M80 and L80:
;
;   M80 =Nonamed.SMM
;   L80 Nonamed,Nonamed.GMM /n/e
;

;
;   To execute, run the program loader ("Yawara.Bin" or
;   "Yawara.Saf"). Please, read the  documentation file
;   ("Yawara.html").
;

;
;   "Nonamed" game mapping:
;
;                       E400 EBFF
;      |?|?|?|?|?|?|?|?|?|?|M|
;   4000                     EFFF
;
;   M = Game Music Routines and Data.
;


; Program addresses and constants.

HOLD8  equ  0F806H
EBTb0  equ  HOLD8
Where  equ  EBTb0+039H

LINTTB equ  0FBB2H
SysDB  equ  LINTTB
SavHok equ  SysDB+010H

FNKSTR equ  0F87FH
EBDBnk equ  FNKSTR
SaveSP equ  EBDBnk+0000FH

HKEYI  equ  0FD9AH
HTIMI  equ  0FD9FH


; MSX PSG constants.

PSGAdr equ  0A0H
PSGWrP equ  0A1H
PSGRdP equ  0A2H
PChnCt equ  007H
PAVol  equ  008H
PBVol  equ  009H
PCVol  equ  00AH


       .Z80
       aseg

       org  00100H
       .Phase 00100H

GameDt:db   '            Nonamed             '
GmBlPt:dw   GmBlNm-GameDt
StupRP:dw   SetupR-GameDt
SilnRP:dw   SilnRt-GameDt
StMsRP:dw   StMsRt-GameDt
PlayRP:dw   PlayRt-GameDt
GMPBAd:dw   06000H
GMPNam:db   'Yawara  600'
OpenMN:db   000H
StrtSE:db   000H
ChATDD:dw   0E011H
ChBTDD:dw   0E01CH
ChCTDD:dw   0E027H
BGMSPt:dw   MusicT-GameDt
SFXSPt:dw   EffctT-GameDt
OpnGFN:db   'Nonamed0Sc2'
OpnGFS:dw   04000H
MnuGFN:db   'Nonamed1Sc2'
MnuGFS:dw   04000H
OpnStC:dw   21*32+10
Resorc:db   00000010B

SetupR:jr   z,Start

       ld   de,08002H
       ld   a,(de)
       ld   (SavHok),a
       inc  de
       ld   a,(de)
       ld   (HTIMI),a

       ld   hl,04000H        ; Restore disk work area.
       ld   de,0E000H
       ld   bc,01000H
       ldir

       pop  hl               ; Restore Stack Pointer.
       ld   sp,(SaveSP)
       jp   (hl)

Start: ld   a,(08000H)
       or   a
       jr   z,SilnRt

       ld   hl,0E000H        ; Save disk work area.
       ld   de,04000H
       ld   bc,01000H
       ldir

       pop  ix               ; Set new Stack Pointer.
       ld   sp,0BFFFH

       ld   hl,08000H        ; Move game block.
       ld   de,0E400H
       ld   bc,00800H
       ldir
       push ix

       xor  a
       ld   c,0C9H
       ld   hl,08000H
       ld   (hl),a           ; Set main flag.
       inc  hl
       ld   (hl),a

       inc  hl               ; Save old and reset hooks.
       ex   de,hl
       ld   hl,SavHok
       ld   a,(hl)
       ld   (de),a
       ld   (hl),c
       inc  de
       ld   hl,HTIMI
       ld   a,(hl)
       ld   (de),a
       ld   (hl),c

       ld   hl,0E46BH
       ld   (hl),c
       ld   a,001H
       ld   (0E4D0H),a

       ld   a,0BFH           ; Fix very hazardous bug in game code.
       ld   (0EB13H),a
SilnRt:xor  a
StMsRt:ld   (08001H),a
       or   a
       jp   nz,0E44EH
       ld   a,PChnCt
       out  (PSGAdr),a
       ld   a,10111111B
       out  (PSGWrP),a
       ld   a,PAVol
       out  (PSGAdr),a
       xor  a
       out  (PSGWrP),a
       ld   a,PBVol
       out  (PSGAdr),a
       xor  a
       out  (PSGWrP),a
       ld   a,PCVol
       out  (PSGAdr),a
       xor  a
       out  (PSGWrP),a
       ret

PlayRt:ld   a,(08001H)
       or   a
       ret  z
       jp   0E4BFH

GmBlNm:db   'Nonamed 000'
GmBlSz:dw   00800H
LoadAd:dw   08000H
       dw   0FFFFH
MusicT:db   001H,'    Theme of Nonamed   '
       db   0FFH,0FFH
EffctT:db   000H,'       Silence...      '
       db   0FFH,0FFH

       end
