;
;                      Flintstn.SMM
;
;       "The Flintstones" Source Game Music Module
;
; Version Amalthea-1-0 (1st Ver., 1st Release, No Revis.)
;
;   This  MSX  Assembly  source file is part of "Yawara:
;   General Game  Music Player". Please, read the notice
;   and documentation file "Yawara.html". If you use the
;   program  in any way  you are  automatically agreeing
;   with  everything stated in  that  file, even if  you
;   haven't read it, so  I  recommend you  do  it. Thank
;   you for your cooperation, good night.
;
;    This file was created, edited and programmed from
;    1994  to 1998 by  "Cyberknight" Masao Kawata  for
;    "Unicorn Dreams Artwork Programs".  For  contact,
;    send an E-Mail to:
;
;                cyber_unicorn@hotmail.com
;
;    Check also the Home Page for new releases:
;
;            http://unicorndreams.home.ml.org
;

;
;   Compile using M80 and L80:
;
;   M80 =Flintstn.SMM
;   L80 Flintstn,Flintstn.GMM /n/e
;

;
;   To execute, run the program loader ("Yawara.Bin" or
;   "Yawara.Saf"). Please, read the  documentation file
;   ("Yawara.html").
;

;
;   "The Flintstones" game mapping:
;
;   0600   14A0
;      |M|M|?|?|?|?|?|?|?|?|?|?|?|?|?|?|
;   0000                               FFFF
;
;   M = Game Music Routines and Data.
;


; Program addresses and constants.

HOLD8  equ  0F806H
EBTb0  equ  HOLD8
Where  equ  EBTb0+039H

LINTTB equ  0FBB2H
SysDB  equ  LINTTB
SavHok equ  SysDB+010H

HKEYI  equ  0FD9AH
HTIMI  equ  0FD9FH


; MSX PSG constants.

PSGAdr equ  0A0H
PSGWrP equ  0A1H
PSGRdP equ  0A2H
PChnCt equ  007H
PAVol  equ  008H
PBVol  equ  009H
PCVol  equ  00AH


       .Z80
       aseg

       org  00100H
       .Phase 00100H

GameDt:db   '        The Flintstones         '
GmBlPt:dw   GmBlNm-GameDt
StupRP:dw   SetupR-GameDt
SilnRP:dw   SilnRt-GameDt
StMsRP:dw   StMsRt-GameDt
PlayRP:dw   PlayRt-GameDt
GMPBAd:dw   0C000H
GMPNam:db   'Yawara  C00'
OpenMN:db   000H
StrtSE:db   000H
ChATDD:dw   0E011H
ChBTDD:dw   0E01CH
ChCTDD:dw   0E027H
BGMSPt:dw   MusicT-GameDt
SFXSPt:dw   EffctT-GameDt
OpnGFN:db   'Flntstn0Sc2'
OpnGFS:dw   04000H
MnuGFN:db   'Flntstn1Sc2'
MnuGFS:dw   04000H
OpnStC:dw   21*32+10
Resorc:db   00000010B

SetupR:jr   z,Start

       ld   de,08002H
       ld   a,(de)
       ld   (SavHok),a
       inc  de
       ld   a,(de)
       ld   (HTIMI),a

       ld   hl,04000H        ; Restore BIOS.
       ld   de,00000H
       ld   bc,04000H
       ldir
       ret

Start: ld   a,(08000H)
       or   a
       jr   z,SilnRt
       ld   hl,00000H        ; Save BIOS.
       ld   de,04000H
       ld   bc,04000H
       ldir

       ld   hl,08000H        ; Move game block.
       ld   de,00600H
       ld   bc,00EA1H
       ldir

       xor  a
       ld   c,0C9H
       ld   hl,08000H
       ld   (hl),a           ; Set main flag.
       inc  hl
       ld   (hl),a

       ex   de,hl            ; Save old and reset hooks.
       inc  de
       ld   hl,SavHok
       ld   a,(hl)
       ld   (de),a
       ld   (hl),c
       inc  de
       ld   hl,HTIMI
       ld   a,(hl)
       ld   (de),a
       ld   (hl),c

       call Where            ; Set new interruption handler.
Here:  ld   de,There-Here
       add  hl,de
       ld   de,00038H
       ld   bc,GmBlNm-There
       ldir

SilnRt:xor  a
StMsRt:ld   (08001H),a
       or   a
       jp   z,00CC9H
       dec  a
       jp   00CC9H

PlayRt:ld   a,(08001H)
       or   a
       ret  z
       ld   a,(00606H)
       or   a
       jr   z,SilnRt
       jp   00718H

There: push hl
       push de
       push bc
       push af
       exx
       ex   af,af'
       push hl
       push de
       push bc
       push af
       push iy
       push ix
       call HKEYI

GmBlNm:db   'Flintstn000'
GmBlSz:dw   00EA1H
LoadAd:dw   08000H
       dw   0FFFFH
MusicT:db   001H,' The Flintstones Theme '
       db   002H,'Pebbles, Rocks & Stones'
       db   0FFH,0FFH
EffctT:db   000H,'       Silence...      '
       db   0FFH,0FFH

       end
