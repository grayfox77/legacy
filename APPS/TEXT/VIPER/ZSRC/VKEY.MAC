;VKEY
.Z80
CSEG

MACLIB B:\ZERO\MAC\VWORK.INC
MACLIB B:\ZERO\MAC\VBIOS.INC
MACLIB B:\ZERO\MAC\VSCREEN.INC
MACLIB B:\ZERO\MAC\VMEMORY.INC
MACLIB B:\ZERO\MAC\VVIEW.INC
MACLIB B:\ZERO\MAC\VSYS.INC
MACLIB B:\ZERO\MAC\VINPBOX.INC

PUBLIC EFRONT,TFRONT,FRONT,WAIT0,WAIT1,WAIT2,WAIT3,WAIT4,WAIT5,KEYRST
PUBLIC S_TRAP,ONTRP,OFFTRP,CBEEP,SNSSTP,SCNCUT,SCNSTP,PUTFEP,JE_FIRST,BUFIN
PUBLIC WBUF,RBUF,REPTIM,SCNTIM
PUBLIC TIMION,TIMIOF,TIMISV

FRJUNK: DW 0000H
REPTIM:DB 20
SCNTIM:DB 2
KREPT:DB 0 
KSCAN:DB 1 
KMAT:DS 11

 DB 8,0,0,0
CV0BUF::
 DS 8
 DB 4,0,0,0
CVPBUF::
 DS 4

BITDTA:: 
DB  8, 0, 1, 1, 2, 2, 18, 18
DB  3, 3, 19, 19, 35, 35, 35, 35
DB  4, 4, 20, 20, 36, 36, 36, 36
DB  52, 52, 52, 52, 52, 52, 52, 52
DB  5, 5, 21, 21, 37, 37, 37, 37
DB  53, 53, 53, 53, 53, 53, 53, 53
DB  69, 69, 69, 69, 69, 69, 69, 69
DB  69, 69, 69, 69, 69, 69, 69, 69
DB  6, 6, 22, 22, 38, 38, 38, 38
DB  54, 54, 54, 54, 54, 54, 54, 54
DB  70, 70, 70, 70, 70, 70, 70, 70
DB  70, 70, 70, 70, 70, 70, 70, 70
DB  86, 86, 86, 86, 86, 86, 86, 86
DB  86, 86, 86, 86, 86, 86, 86, 86
DB  86, 86, 86, 86, 86, 86, 86, 86
DB  86, 86, 86, 86, 86, 86, 86, 86
DB  7, 7, 23, 23, 39, 39, 39, 39
DB  55, 55, 55, 55, 55, 55, 55, 55
DB  71, 71, 71, 71, 71, 71, 71, 71
DB  71, 71, 71, 71, 71, 71, 71, 71
DB  87, 87, 87, 87, 87, 87, 87, 87
DB  87, 87, 87, 87, 87, 87, 87, 87
DB  87, 87, 87, 87, 87, 87, 87, 87
DB  87, 87, 87, 87, 87, 87, 87, 87
DB  103, 103, 103, 103, 103, 103, 103, 103
DB  103, 103, 103, 103, 103, 103, 103, 103
DB  103, 103, 103, 103, 103, 103, 103, 103
DB  103, 103, 103, 103, 103, 103, 103, 103
DB  103, 103, 103, 103, 103, 103, 103, 103
DB  103, 103, 103, 103, 103, 103, 103, 103
DB  103, 103, 103, 103, 103, 103, 103, 103
DB  103, 103, 103, 103, 103, 103, 103, 103

CVPDTA::
 DB 255,255,255,30,28,0,27,255
 DB 255,29,255,255,255,31,1,2
 DB 3,4,5,6,7,8,9,10
 DB 11,12,13,14,15,16,17,18
 DB 19,20,21,22,23,24,25,26

TFRONT:
 LD A,(FEPMD)
 PUSH AF
 XOR A
 LD (FEPMD),A
 CALL EFRONT
 POP AF
 LD (FEPMD),A
 RET

EFRONT:
 LD A,(FEPMD)
 AND A
 JP NZ,XFRONT
 CALL PUTFEP0
 JP AFRONT

XFRONT:
 LD A,(CNTHKF)
 AND A
 JP NZ,JE_TRAP
 CALL PUTFEP1
XFRJ0:
 CALL AFRONT
 CALL FNKCHK
 JP Z,XFRJ0

 LD A,(COM)
 AND A
 RET NZ
 LD A,(FEPBUF+1)
 CP 32
 JP Z,SPCKEY

 JP JE_TRAP

FNKCHK:
 LD A,(COM)
 CP 123
 RET NZ
 LD A,(CNVMD)
 XOR 1
 LD (CNVMD),A
 CALL PUTFEP2
 JP RTRUE

GETKEY:
 LD A,0FFH
 LD (COM),A
 CALL GETCH
 AND A
 JP Z,FGJ1
 LD (FRJUNK),A
 CP 8
 JP Z,FGJBS
 CP 9
 JP Z,FGJTAB
 CP 11
 JP Z,FGJHOME
 CP 12
 JP Z,FGJCLS
 CP 13
 JP Z,FGJRET
 CP 18
 JP Z,FGJINS
 CP 28
 JP Z,FGJCSR_R
 CP 29
 JP Z,FGJCSR_L
 CP 30
 JP Z,FGJCSR_U
 CP 31
 JP Z,FGJCSR_D


 CP 32
 RET C;RFALSE<-----ERROR COMMAND!

 CP 127
 JP Z,FGJDEL



;-------------------------- ANK KEY
 XOR A
 LD (COM),A
 LD A,1
 LD (FEPBUF),A
 LD A,(FRJUNK)
 LD (FEPBUF+1),A
 RET

SUTERU_CVP:;YOMISUTE CVP
 CALL GETCVP
 RET Z
 LD A,255
 LD (COM),A
 RET

;-------------------------- CNV KEY
FGJ1:
 CALL GETCNV
 RET Z
 LD H,0
 LD L,A
 LD DE,0B800H;1800H
 ADD HL,DE
 CALL NRV1
 LD (COM),A
 CP 255
 JP Z,SUTERU_CVP
 CP 238
 JP C,SUTERU_CVP


 CP 247
 JP C,FGJ20
;------------------------- SAMETIME HIT
 LD HL,0B900H;1900H
 LD DE,20H
 LD B,8
 LD C,247
FGJ11:
 CP C
 JP Z,FGXJ10
 ADD HL,DE
 INC C
 DJNZ FGJ11
FGXJ10:
 PUSH HL
 CALL GETCVP
 POP HL
 RET Z
 LD E,A
 LD D,0
 ADD HL,DE
 CALL NRV1
 LD (COM),A
 CP 238
 RET C
 CP 246
 RET NC
;----------------------------- PREFIX KEY
FGJ20:
 PUSH AF
 CALL SUTERU_CVP
 POP AF
 LD B,A
 LD A,(EDFLG)
 AND 00101000B
 LD A,B
 JP Z,FGJ25
 CP 238;ESC CHECK
 JP NZ,FGJ25
 LD A,29
 LD (COM),A
 RET
FGJ25:
 LD HL,0BA00H
 LD DE,20H
 LD B,8
 LD C,238
FGJ31:
 CP C
 JP Z,FGXJ30
 ADD HL,DE
 INC C
 DJNZ FGJ31
;------------------- KEY WAIT
FGXJ30:
 LD (FRJUNK),HL

 LD DE,SVDTA0
 CALL SAVSCT
 LD HL,(BASE5)
 CALL RSTMSC
 LD DE,MSD
 CALL PUTMSD

 CALL WAIT4
 JP NZ,FGJ24
 LD A,B
;-------------------- GET COM
FGJ21:
 LD HL,(FRJUNK)
 LD E,A
 LD D,0
 ADD HL,DE
 CALL NRV1
 LD (COM),A
 LD HL,SVDTA0
 CALL LODSCT
 RET
FGJ24:
 LD A,255
 LD (COM),A
 LD HL,SVDTA0
 CALL LODSCT
 RET
;------------------------------ CONTOROLKEY
FGJRET:
 LD A,4
 LD (COM),A
 RET

FGJTAB:
 LD A,21
 LD (COM),A
 RET

FGJBS:
 LD A,14
 LD (COM),A
 RET
FGJDEL:
 LD A,15
 LD (COM),A
 RET
FGJINS:
 LD A,13
 LD (COM),A
 RET
FGJCSR_L:
 LD A,7
 LD (COM),A
 RET
FGJCSR_R:
 LD A,8
 LD (COM),A
 RET
FGJCSR_U:
 LD A,5
 LD (COM),A
 RET
FGJCSR_D:
 LD A,6
 LD (COM),A
 RET


FGJHOME:
 LD A,98
 LD (COM),A
 RET
FGJCLS:
 LD A,102
 LD (COM),A
 RET
FGJA:
 LD (FEPBUF),A
 LD (FEPBUF+1),HL
 XOR A
 LD (COM),A
 RET



XFCSR:DW 0000H
PFCSR:DW 0000H
PFATR:DB 00H
ODCSR:DW 0000H
CNTHKF:DB 00H
EMSTPF:DB 00H
FCSRP:DW 0000H

RSM:
 LD A,(CNTHKF)
 PUSH AF
 XOR A
 LD (CNTHKF),A
 POP AF
 AND A
 JP Z,RFALSE
 JP RTRUE

S_FCSRP:
 LD A,(JESMD)
 AND A
 JP NZ,SFCPJ0
 LD DE,(CSRP)
 LD (FCSRP),DE
 RET
SFCPJ0:
 LD DE,(BASE5)
 LD (FCSRP),DE
 RET

JE_TRAP:
 CALL OFFTRP
 CALL S_FCSRP
 CALL SVCHEK
 CALL RSM
 JP Z,JTRJ0
 CALL S_WKS
 CALL FEPBUF_TO_KEYBUF
 CALL JE_FIRST
JTRJ0:
 CALL JE_MAIN
 CALL JE_END
 CALL SET_KEYMAP
 CALL ONTRP
 XOR A
 LD (COM),A
 RET

INT_JESHT:
 LD B,A
 LD A,(JESHT)
 AND 1
 JP NZ,INJHJ0
 LD A,B
 RET
INJHJ0:
 LD A,B
 AND 2
 LD A,B
 RET Z
 LD A,(JESMD)
 AND 00000001B
 LD A,B
 RET NZ
 POP HL
 PUSH BC
 CALL LDCHEK3
 POP BC
 JP B_KAKUTEI
 
JE_MAIN:
 EI
 LD A,06H
 CALL MJEENT;DISPATCH
 CALL INT_JESHT
 PUSH AF
 AND 1
 CALL NZ,P_STB
 EI
 POP AF
 LD B,A
 LD HL,(PFCSR)
 LD DE,(FCSRP)
 AND A
 SBC HL,DE
; JP Z,PS_BREAK

 LD A,B
 AND 2
 JP NZ,B_KAKUTEI
 LD A,B
 AND 4
 JP NZ,J_BREAK
 JP JE_MAIN

PS_BREAK:
 AND 2
 JP NZ,B_KAKUTEI
J_BREAK:
 XOR A
 LD (FEPBUF),A
 RET

B_KAKUTEI:
 LD A,B
 PUSH AF
 LD A,07H
 CALL MJEENT;GET RESULT
 CALL BUFIN
 POP AF
 AND 4
 RET NZ
 LD A,0FFH
 LD (CNTHKF),A
 RET

 
WKJUNK:DW 0000H

S_WKS:
 LD HL,(BUFSIZ)
 LD (WKJUNK),HL
 LD HL,1800H
 LD (BUFSIZ),HL
 RET

L_WKS:
 LD HL,(WKJUNK)
 LD (BUFSIZ),HL
 RET

BUFIN:
 LD B,0
 LD DE,FEPBUF+1
BFJ1:
 LD A,(HL)
 AND A
 JP Z,BFJ0
 LD (DE),A
 INC B
 INC DE
 INC HL
 JP BFJ1
BFJ0:
 LD A,B
 LD (FEPBUF),A
 RET

JE_FIRST:
 LD A,01H
 CALL MJEENT;INQUIRY
 EX DE,HL;<-MAX MEM
 LD A,02H
 CALL MJEENT;INVOKE
 CALL JFSTJ0
 LD A,09H
 CALL MJEENT;INQUIRE WINDOW SIZE
 CALL RSTPRT
 LD A,04H
 CALL MJEENT;CLEAR
 RET

JE_END:
 LD A,(CNTHKF)
 AND A
 RET NZ
 LD A,03H
 CALL MJEENT;RELEASE
 CALL L_WKS
 RET

CHCHK:
 CP 20H
 JP C,RFALSE
 CP 7FH
 JP Z,RFALSE
 CP 0FFH
 JP Z,RFALSE
 JP RTRUE

CHSNS:
 XOR A
 LD DE,(0F3FAH)
 LD HL,(0F3F8H)
 AND A
 SBC HL,DE
 RET Z
 LD A,(DE)
 AND A
 RET

FEPBUF_TO_KEYBUF:
 DI
 LD HL,EXCDTA
 LD BC,0
 CALL CHKCNV
 LD A,(FEPBUF+1)
FTBJ0:
 CALL WXBUF
 PUSH BC
 PUSH HL
 CALL GETCH
 POP HL
 POP BC
 AND A
 JP NZ,FTBJ0
 CALL WKBUF
 EI
 RET

CHKCNV:
 LD A,(CNVMD)
 LD D,A
 LD A,(DFVMD)
 CP D
 RET Z
 LD A,01H
 CALL WXBUF
 LD A,31H
 CALL WXBUF
 RET
 
WXBUF:
 LD (HL),A
 INC HL
 INC BC
 RET

WKBUF:
 LD A,B
 OR C
 RET Z
 DI
 LD HL,0FBF0H
 LD (0F3FAH),HL
 ADD HL,BC
 LD (0F3F8H),HL
 LD HL,EXCDTA
 LD DE,0FBF0H
 LDIR
 EI
 RET

SCNSTP:
 LD A,1
 LD (0F3F6H),A
 LD A,20
 LD (0F3F7H),A
 RET

SCNCUT:
 LD HL,0FBF0H
 LD (0F3FAH),HL
 LD (0F3F8H),HL
 RET

PCJUNK:DW 0000H

P_STB:
 LD (PCJUNK),HL
 CALL E_CSR
PSTJ0:
 EI
 LD HL,PSTJ0
 PUSH HL
 LD HL,(PCJUNK)
 LD A,(HL)
 AND A
 JP Z,PSTJ1
 CP 32
 JP NC,PST_PUT
 CP 5
 JP Z,PST_ERS
 CP 8
 JP Z,PST_CSR_L
 CP 12
 JP Z,PST_CLS
 CP 18
 JP Z,PST_RVS
 CP 24
 JP Z,PST_CSR_X
 CP 26
 JP Z,PST_ATR


;NOT FONCTION
 CP 16
 CALL NC,PST_SKIP
 JP PST_SKIP
PSTJ1:
 POP HL
 CALL P_CSR
 RET

INCPCJ:
 LD HL,(PCJUNK)
 INC HL
 LD (PCJUNK),HL
 RET

PST_CSR_L:
 LD HL,(XFCSR)
 DEC HL
 LD (XFCSR),HL
 CALL PFRPUT
 CALL LDCHEK
 CALL INCPCJ
 RET

PST_ERS:
 CALL PFRPUT
 CALL LDCHEK
 CALL INCPCJ
 RET

PST_CLS:
 CALL LDCHEK2
 CALL RSTPRT
 CALL INCPCJ
 RET

RSTPRT:
 LD DE,(FCSRP)
 LD (PFCSR),DE
 LD (ODCSR),DE
 LD (MAXCSR),DE
 LD DE,EXCDTA+100H
 LD (XFCSR),DE
 LD A,11110000B
 LD (PFATR),A
 LD A,0FFH
 LD (DE),A
 RET

PCHECK:
 LD HL,(MAXCSR)
 AND A
 SBC HL,DE
 RET

P_CSR:
 LD DE,(PFCSR)
 CALL PCHECK
 LD HL,(PFCSR)
 LD (ODCSR),HL
 LD BC,1
 RET Z
 CALL MCOLOR
 RET

E_CSR:
 LD DE,(ODCSR)
 CALL PCHECK
 LD HL,(ODCSR)
 LD BC,1
 RET Z
 CALL MCOLOR
 RET

PST_CSR_X:
 CALL INCPCJ
 LD DE,(PCJUNK)
 LD A,(DE)
 AND A
 JP Z,INCPCJ
 DEC A
 LD C,A
 LD B,0
 LD HL,EXCDTA+100H
 ADD HL,BC
 LD (XFCSR),HL
 CALL PFRPUT
 CALL RSTMAX
 CALL INCPCJ
 RET

PST_ATR:
 CALL INCPCJ
 LD DE,(PCJUNK)
 LD A,(DE)
 LD (PFATR),A
 CALL INCPCJ
 RET

PST_RVS:
 CALL INCPCJ
 LD DE,(PCJUNK)
 LD A,(DE)
 LD B,0
 LD C,A
 LD HL,(PFCSR)
 CALL MCOLOR
 CALL INCPCJ
 RET 
 
PST_SKIP:
 CALL INCPCJ
 RET

PST_PUT:
 LD A,(HL)
 LD HL,TCAD
 LD D,0
 LD E,A
 ADD HL,DE
 LD A,(HL)
 JP Z,PSPJ0
 LD HL,(PCJUNK)
 LD BC,2
 CALL PFPUT
 CALL RSTMAX
 RET
PSPJ0:
 LD HL,(PCJUNK)
 LD BC,1
 CALL PFPUT
 CALL RSTMAX
 RET

PFRPUT:
 LD BC,0
 LD HL,(XFCSR)
 LD DE,EXCDTA+100H
 AND A
 SBC HL,DE
 JP Z,PFRJ0
 LD C,L
 LD B,H
 LD HL,EXCDTA+100H
 LD DE,(FCSRP)
 CALL M2VRKC
PFRJ0:
 LD HL,(FCSRP)
 ADD HL,BC
 LD (PFCSR),HL
 RET

PFPUT:
 PUSH BC

 PUSH HL
 PUSH BC
 LD DE,(XFCSR)
 LDIR
 POP BC
 LD HL,(XFCSR)
 ADD HL,BC
 LD (XFCSR),HL
 POP HL

 LD DE,(PFCSR)
 CALL M2VRK
 PUSH BC
 LD HL,(PFCSR)
 LD A,(PFATR)
 CALL KLCOLOR
 POP BC

 LD HL,(PFCSR)
 ADD HL,BC
 LD (PFCSR),HL

 POP BC
 LD HL,(PCJUNK)
 ADD HL,BC
 LD (PCJUNK),HL
 RET

KLCOLOR:
 LD D,A
 LD A,(JESHT)
 AND 2
 LD A,D
 JP Z,LCOLOR
 CP 15 * 16 + 5
 JP Z,MCOLOR
 JP MLINE

SVCHEK:
 LD HL,(FCSRP)
 CALL HL16M
 LD DE,1C00H
 LD BC,200H
 XOR A
 LD (PFCSR),A
 CALL VRM
 RET
 RET

MAKEBC:
 LD HL,(MAXCSR)
 LD DE,(FCSRP)
 AND A
 SBC HL,DE
 JP C,RFALSE
 LD C,L
 LD B,H
 JP RTRUE

LDCHEK3:
 CALL MAKEBC
 RET NZ
 JP LDC2J0

LDCHEK2:
 CALL MAKEBC
 RET NZ
 PUSH BC
 CALL BC16M
 CALL DE16M
 LD HL,1C00H
 XOR A
 CALL VRM
 POP BC
LDC2J0:
 LD A,(JESHT)
 AND 2
 RET NZ
 LD HL,(FCSRP)
 LD A,(BASECL)
 CALL LCOLOR
 RET

MAXCSR:DW 0000H

LDCHEK:
 LD HL,(MAXCSR)
 LD DE,(PFCSR)
 AND A
 SBC HL,DE
 JP C,LDCJ0
 PUSH HL
 PUSH HL
 LD HL,(PFCSR)
 LD DE,(FCSRP)
 AND A
 SBC HL,DE
 CALL HL16M
 LD DE,1C00H
 ADD HL,DE
 POP BC
 CALL BC16M
 LD DE,(PFCSR)
 CALL DE16M
 XOR A
 CALL VRM
 POP BC
 LD HL,(PFCSR)
 LD A,(BASECL)
 CALL LCOLOR
 LD HL,(PFCSR)
 LD DE,(FCSRP)
 AND A
 SBC HL,DE
 CALL Z,MVCSR
LDCJ0:
 LD DE,(PFCSR)
 LD (MAXCSR),DE
 RET

RSTMAX:
 LD HL,(MAXCSR)
 LD DE,(PFCSR)
 AND A
 SBC HL,DE
 RET NC
 LD (MAXCSR),DE
 RET

SPCKEY:
 LD A,2
 LD (FEPBUF),A
 LD HL,4081H; Z_SPACE CODE
 LD (FEPBUF+1),HL
 RET
 
CPD0:DB 45,92,0
CPD1:DB 0B0H,0B0H,0

PUTFEP:
PUTFEP1:
 LD HL,17E8H
 CALL NRV1
 LD B,A
 LD A,(PFJUNK)
 CP B
 JP Z,PFJ11
PFJ1:
 LD A,(SYS12)
 AND A
 RET NZ
 LD HL,(BASE5)
 CALL HL16M
 LD DE,6400H
 LD BC,200H
 XOR A
 CALL VRM
PUTFEP2:
 LD HL,(BASE5)
 CALL RSTMSC
 LD A,(CNVMD)
 LD B,A
 LD A,46
 SUB B
 CALL EPUTMS
 LD HL,17E8H
 CALL NRV1
 LD (PFJUNK),A
PFJ11:
 LD A,0FFH
 LD (PFJUNK+1),A
 RET

PUTFEP0:
 LD A,(PFJUNK+1)
 AND A
 RET Z
 LD HL,17E8H
 CALL NRV1
 LD B,A
 LD A,(PFJUNK)
 CP B
 JP NZ,PFJ01
 LD HL,6400H
 LD DE,(BASE5)
 CALL DE16M
 LD BC,200H
 XOR A
 CALL VRMW
PFJ01:
 XOR A
 LD (PFJUNK+1),A
 RET

PFJUNK:DB 10101010B;DUMMY RANDOM DATA
DS 3

SRC_KEYMAP:
 LD HL,KMAT
 LD B,11
SRCKJ0:
 LD A,(HL)
 XOR 0FFH
 JP NZ,RTRUE
 DJNZ SRCKJ0
 JP RFALSE

WAIT0:
 CALL WAIT1
W0J:
 CALL SRC_KEYMAP
 JP NZ,W0J
 RET

WAIT1:
 CALL TIMION
WAIT1J0:
 CALL SRC_KEYMAP
 JP Z,WAIT1J0
 CALL KEYRST
 CALL SCNCUT
 RET

WAIT2:
 CALL WAIT1
W2J0:
 CALL GETCNVE
 JP NZ,W2J7
 CALL GETCH
 AND A
 JP Z,W2J0
 PUSH AF
 CALL KEYRST
 POP AF
 LD B,A
 LD HL,W2J1
 CALL TXMACH
 JP NZ,W2J4
 LD A,B
 LD HL,W2J2
 CALL TXMACH
 JP NZ,W2J5
 LD A,B
 LD HL,W2J3
 CALL TXMACH
 JP NZ,W2J6
 JP W2J0
W2J4:
 JP RTRUE
W2J5:
 JP RFALSE
W2J6:
 LD A,01111111B
 AND A
 RET
W2J7:
 CALL SNSSTP
 JP NZ,W2J0
 LD A,10111111B
 AND A
 RET

W2J1:DB "Yy",13,0
W2J2:DB "Nn ",0
W2J3:DB "Aa",0

WAIT3:
 CALL WAIT1
W3J0:
 CALL GETCNVE
 JP NZ,W2J5
 CALL GETCH
 AND A
 JP Z,W3J0
 PUSH AF
 CALL KEYRST
 POP AF
 LD HL,W3J1
 CALL TXMACH
 JP Z,W2J5
 DEC A
 SRL A
 INC A
 LD B,A
 JP W2J4

W3J1:
 DB "AaBbCcDdEeFfGgHh",0

WAIT4:
 CALL GETCH
 AND A
 JP Z,W4J0
 CP 64
 JP C,RFALSE;EXIT
 CP 128
 JP NC,RFALSE;EXIT
 AND 31
 LD B,A
 JP RTRUE
W4J0:
 CALL GETCNVE
 JP Z,WAIT4
 LD L,A
 LD H,0
 LD DE,0B800H
 ADD HL,DE
 CALL NRV1
 CP 246
 JP C,RFALSE;EXIT
 CALL GETCVP
 JP Z,WAIT4;REPEAT
 LD B,A
 JP RTRUE

WAIT5:
 CALL WAIT1
W5J0:
 CALL GETCNVE
 JP NZ,W5J7
 CALL GETCH
 AND A
 JP Z,W5J0
 PUSH AF
 CALL KEYRST
 POP AF
 LD B,A
 LD HL,W5J1
 CALL TXMACH
 JP NZ,W5J4
 JP W5J0
W5J4:
 LD B,A
 DEC B
 JP RTRUE
W5J5:
 JP RFALSE
W5J7:
 CALL SNSSTP
 JP NZ,W5J0
 LD A,10111111B
 AND A
 RET

W5J1:DB "0123456789",0

SNSSTP:
 LD C,7
 CALL SNSMAT
 XOR 0FFH
 AND 00010100B
 JP NZ,RTRUE
 JP RFALSE


TSAVE:
 NOP
 NOP
 NOP
 NOP
 RET


KEYTRP:
 CALL CHECK_SCANTIME
 JP NZ,BIOSKEY_OFF2

 CALL SET_KEYMAP
 LD A,D
 AND 1
 JP Z,BIOSKEY_OFF
 CALL RST_REPTIM
 JP NZ,BIOSKEY_OFF
 PUSH DE
 CALL CHECK_CNV
 POP DE
 JP Z,BIOSKEY_ON
 CALL MAKE_CNV
 JP BIOSKEY_OFF

BIOSKEY_ON:
 LD HL,0101H
 LD (0F3F6H),HL
 JP TSAVE

BIOSKEY_OFF:
 CALL C_KMAT
BIOSKEY_OFF2:
 LD HL,0FFFFH
 LD (0F3F6H),HL
 JP TSAVE

C_KMAT:
 LD HL,KMAT
 LD DE,NEWKEY
 LD BC,11
 LDIR
 LD HL,KMAT
 LD DE,OLDKEY
 LD BC,11
 LDIR
 RET

CHECK_SCANTIME:
 LD HL,KSCAN
 DEC (HL)
 RET NZ
 LD A,(SCNTIM)
 LD (HL),A
 JP T_RTRUE

SET_KEYMAP:
 LD DE,0
 LD BC,0B00H
 LD HL,KMAT 
SKMJ0: 
 CALL SNSMAT
 LD E,(HL)
 LD (HL),A
 CP E
 CALL NZ,SKMJ2
 LD A,(HL)
 XOR 0FFH
 CALL NZ,SKMJ1
 INC C
 INC HL
 DJNZ SKMJ0
 RET
SKMJ1:
 LD A,1
 OR D
 LD D,A;KEY ON FLAG
 RET
SKMJ2:
 LD A,2
 OR D
 LD D,A;HENKA
 RET

RST_REPTIM:
 LD A,D
 AND 2
 JP NZ,RSRJ0
 LD HL,KREPT
 DEC (HL)
 RET NZ
 LD A,1
 LD (HL),A
 XOR A
 RET
RSRJ0:
 LD A,(REPTIM)
 LD (KREPT),A
 XOR A
 RET


CHECK_CNV:
 LD IX,KMAT+6
 LD A,(IX)
 AND 11100110B
 LD L,A
 LD A,(IX+1)
 AND 01100100B
 SRL A
 SRL A
 OR L
 XOR 0FFH
 LD L,A

 LD A,(IX+2)
 AND 11111100B
 LD H,A
 LD A,(IX+1)
 AND 00000011B
 OR H
 XOR 0FFH
 LD H,A
 OR L
 RET

SNSFEP:
 LD A,(KMAT+6)
 AND 00000010B
 RET NZ
 LD A,(KMAT+8)
 AND 00000001B
 RET

MAKE_CNV:
 CALL SNSFEP
 JP Z,MCJ0X
 LD C,L
 LD B,0
 LD IX,BITDTA
 ADD IX,BC 
 LD C,H
 LD B,0
 LD IY,BITDTA
 ADD IY,BC

 LD A,(IX)
 CP 8
 JP Z,MCJ0
 CP 16
 JP NC,SET_CNV
;SINGLE
 LD D,A
 LD A,(IY)
 CP 8
 JP Z,MCJ1 
 AND 00001111B
 OR  00001000B
 SLA A
 SLA A
 SLA A
 SLA A
 OR D
 JP SET_CNV
MCJ1:
 LD A,D
 JP SET_CNV

MCJ0:
 LD A,(IY)
 CP 8
 RET Z
 CP 16
 JP NC,MCJ2
 OR 00001000B
 JP SET_CNV
MCJ2:
 OR 10001000B
 JP SET_CNV

MCJ0X:;CTRL+SPACE -> CTRL+SELECT
 LD A,16 + 4
 JP SET_CNV

SET_CNV:
 LD DE,CV0BUF
 CALL WBUF
 CALL GAZKEY
 JP Z,SCVP
 LD B,255
SCVP:
 LD A,B
 LD DE,CVPBUF
 CALL WBUF
 RET

GAZKEY:
 LD B,5
 LD DE,KMAT+1
 LD L,0
GAZJ0:
 LD A,(DE)
 XOR 0FFH
 JP NZ,GAZJ1
 INC DE
 LD A,L
 ADD A,8
 LD L,A
 DJNZ GAZJ0
 JP RFALSE
GAZJ1:
 LD IX,BITDTA
 LD D,0
 LD E,A
 ADD IX,DE
 LD A,(IX)
 AND 00001111B
 ADD A,L
 LD L,A
 LD H,0
 LD DE,CVPDTA
 ADD HL,DE
 LD B,(HL)
 JP RTRUE
 
SNSMAT:
 IN A,(0AAH)
 AND 0F0H
 ADD A,C
 OUT(0AAH),A
 IN A,(0A9H)
 RET

CBEEP:
 PUSH AF
 LD A,(CLICKF)
 AND A
 JP Z,CLBJ1
 DI
 LD A,0FH
 OUT(0ABH),A
 LD A,0AH
CLBJ0:
 DEC A
 JR NZ,CLBJ0
 LD A,0EH
 OUT(0ABH),A
 EI
CLBJ1:
 POP AF
 RET

GETCNVE:
 CALL GETCVP
 CALL GETCNV
 RET

GETCNV:
 LD DE,CV0BUF
 CALL RBUF
 RET

GETCVP:
 LD DE,CVPBUF
 CALL RBUF
 RET Z
 CP 255
 RET 

GETCH:
 LD C,06H
 LD E,0FFH
 CALL 0005H
; CP 32
; JP C,GCJ0
 CP 128
; JP Z,GCJ1
 RET C
 CP 160
 JP C,GCJ2
 CP 224
 RET C
 SUB 32
 RET
GCJ2:
 ADD A,32
 RET
GCJ0:
 CP 9
 RET Z
 CP 11
 RET Z
 CP 12
 RET Z
 CP 13
 RET Z
GCJ1:
 XOR A
 RET

KEYRST:
 LD HL,0
 LD (CV0BUF-3),HL
 LD (CVPBUF-3),HL
 XOR A
 LD (CV0BUF-1),A
 LD (CVPBUF-1),A
 RET

TIMISV:
 LD HL,TIMI
 LD DE,CUTJ
 LD BC,3
 LDIR
 RET

CUTJ:
 DS 5

TIMIOF:
 DI
 LD A,0C9H
 LD (TIMI),A
 RET

TIMION:
 DI
 LD HL,CUTJ
 LD DE,TIMI
 LD BC,3
 LDIR
 EI
 RET

S_TRAP:
 DI
 LD HL,H.TIMI
 LD DE,TSAVE
 LD BC,5
 LDIR
 EI
 RET

ONTRP:
 LD A,(0F341H)
 LD (TRDTA+1),A
 LD HL,KEYTRP
 LD (TRDTA+2),HL

 DI
 LD HL,TRDTA
 LD DE,H.TIMI
 LD BC,5
 LDIR
 EI
 RET

TRDTA:
 RST 30H
 DB 00H
 DW 0000H
 RET

OFFTRP:
 LD A,2
 LD (0F3F6H),A
 LD A,50
 LD (0F3F7H),A
 DI
 LD HL,TSAVE
 LD DE,H.TIMI
 LD BC,5
 LDIR
 EI
 RET

JFSTJ0:;JE WINDOW SIZ
 LD HL,(BASE6)
 LD DE,(FCSRP)
 AND A
 SBC HL,DE
 LD DE,64
 AND A
 SBC HL,DE
 JP C,JFSTJ1
 EX DE,HL
JFSTJ1:
 LD E,L
 LD B,1
 LD C,L
 RET


FRONT:
AFRONT:
 CALL TIMION
 CALL GETKEY
 LD A,(COM)
 CP 238
 JP NC,AFRONT
; CALL KEYRST
 CALL CBEEP
 RET

WBUF:
 PUSH IX
 PUSH DE
 POP IX
 LD L,(IX-1)
 LD H,0
 ADD HL,DE
 LD (HL),A
 CALL INCW
 CALL CHKSIZW
 POP IX
 RET

RBUF:
 PUSH IX
 PUSH DE
 POP IX
 CALL CHKSIZR
 JP Z,RBFJ0

 LD L,(IX-2)
 LD H,0
 ADD HL,DE
 LD A,(HL)
 LD L,A
 CALL INCR
 LD A,0FFH
 AND A
 LD A,L
RBFJ0:
 POP IX
 RET

INCR:
 INC (IX-2)
 LD A,(IX-2)
 LD B,(IX-4)
 CP B
 RET C
 XOR A
 LD (IX-2),A
 RET

INCW:
 INC (IX-1)
 LD A,(IX-1)
 LD B,(IX-4)
 CP B
 RET C
 XOR A
 LD (IX-1),A
 RET

CHKSIZR:
 LD A,(IX-1)
 LD B,(IX-2)
 CP B
 RET

CHKSIZW:
 LD A,(IX-1)
 LD B,(IX-2)
 CP B
 RET NZ
 CALL INCR;READ CURSOR RESET
 RET

T_RFALSE:
 LD A,0FFH
 AND A
 RET

T_RTRUE:
 XOR A
 RET

END
