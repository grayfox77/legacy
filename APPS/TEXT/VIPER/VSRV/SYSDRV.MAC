;----------------------------------
;            MSX2 SYSDRV
;----------------------------------
        CSEG
;        .Z80

	INCLUDE RABELS.INC
	INCLUDE MATHDRV.INC
	INCLUDE VWORK.INC

	PUBLIC GOBIOS,GOBIO2,PTMS,PTHL
	PUBLIC MPJUMP,MPCALL
	PUBLIC TXBIG,TXSMALL,TX2HL,HL2TX,HL2TXS,HL2TXR,TX2FCB,FCB2TX,TXCOMP
	PUBLIC TX2MFCB,DHL2TX,DH2TXS,DH2TXR
	PUBLIC TXMACH,TXTYPE
	PUBLIC RSFT1,RSFT2
	PUBLIC RTRUE,RFALSE

RTRUE:
 XOR A
 RET

RFALSE:
 LD A,0FFH
 AND A
 RET

GOBIOS:
; IX<ADRESS
 LD IY,(EXPTBL-1)
 JP CALSLT

GOBIO2:
; IX<ADRESS
 EXX
 EX AF,AF'
 LD HL,EXTROM
 PUSH HL
 LD HL,0C300H
 PUSH HL
 PUSH IX
 LD HL,021DDH
 PUSH HL
 LD HL,03333H
 PUSH HL
 LD HL,0
 ADD HL,SP
 LD A,0C3H
 LD (H.NMI),A
 LD (H.NMI+1),HL
 EX AF,AF'
 EXX
 LD IX,NMI
 LD IY,(EXPTBL-1)
 CALL CALSLT
 EI
 LD HL,10
 ADD HL,SP
 LD SP,HL
 RET

PTMS:
 LD C,09H
 CALL 0005H
 RET

PTHL:
 PUSH DE
 CALL HL2TX
 LD L,A
 LD H,0
 POP DE
 ADD HL,DE
 LD A,"$"
 LD (HL),A
 CALL PTMS
 RET

MPJUNK:
 RST 30H
 DB 00H
 DW 0000H
 RET

MPCALL:
 PUSH IX
MPJUMP:
 EX (SP),HL
 PUSH AF
 PUSH DE
 ADD A,A
 LD D,0
 LD E,A
 ADD IY,DE
 LD E,(IY)
 LD D,(IY+1)
 EX DE,HL
 POP DE
 POP AF
 EX (SP),HL
 RET

TX2HL:
 PUSH IX
 PUSH IY
 LD D,0
 LD E,A
 DEC E
 ADD HL,DE
 PUSH HL
 POP IX
 LD B,A
TX2DJ0:
 LD A,(IX)
 CP 48
 JP C,TX2DJ1
 CP 58 
 JP NC,TX2DJ1
 JP TX2DJ2
TX2DJ1:
 DEC IX
 DJNZ TX2DJ0
 JP TX2DJ5

TX2DJ2:
 LD IY,HEAD0
 LD HL,0
TX2DJ3:
 LD A,(IX)
 SUB 48
 JP C,TX2DJ4
 CP 10
 JP NC,TX2DJ4
 PUSH HL
 LD E,(IY)
 LD D,(IY+1)
 LD L,A
 LD H,0
 CALL IMULT   
 EX DE,HL
 POP HL
 ADD HL,DE
 JP C,TX2DJ5
 INC IY
 INC IY
 DEC IX 
 DJNZ TX2DJ3
TX2DJ4:
 XOR A
 POP IY
 POP IX
 RET
TX2DJ5:
 LD A,0FFH
 POP IY
 POP IX
 RET

HL2TXS:
 PUSH AF
 PUSH DE
 CALL HL2TX
 POP HL
 LD B,A
 POP AF
 CALL RSFT1
 RET

HL2TXR:
 PUSH AF
 PUSH DE
 CALL HL2TX
 POP HL
 LD B,A
 POP AF
 CALL RSFT2
 RET

DH2TXS:
 PUSH AF
 PUSH DE
 CALL DHL2TX
 POP HL
 LD B,A
 POP AF
 CALL RSFT1
 RET

DH2TXR:
 PUSH AF
 PUSH DE
 CALL DHL2TX
 POP HL
 LD B,A
 POP AF
 CALL RSFT2
 RET

DHL2TX:
 PUSH DE
 EX DE,HL
 LD HL,10000
 CALL DIMOD
 LD A,E
 OR D
 JP Z,DH2TJ0
 POP BC

 PUSH HL

 PUSH BC
 EX DE,HL
 POP DE

 PUSH DE
 CALL HL2TX
 POP DE

 LD L,A
 LD H,0
 ADD HL,DE
 EX DE,HL

 POP HL
 PUSH AF
 LD A,4
 CALL HL2TXR
 POP AF
 ADD A,4
 RET
DH2TJ0:
 POP DE
 JP HL2TX

HL2TX:
 PUSH IX
 PUSH IY
 LD B,5
 LD C,0
 PUSH DE
 POP IY
 LD IX,HEAD0+8
 EX DE,HL
H2TJ5:
 CALL H2TJ0
 JP NZ,H2TJ7
 CALL H2TJ1
 DJNZ H2TJ5
 LD A,"0"
 LD (IY),A
 LD A,1
 POP IY
 POP IX
 RET

H2TJ6:
 CALL H2TJ0
H2TJ7:
 CALL H2TJ3
 CALL H2TJ1
 DJNZ H2TJ6
 LD A,C
 POP IY
 POP IX
 RET

H2TJ0:
 LD L,(IX)
 LD H,(IX+1)
 PUSH BC
 CALL IMOD
 POP BC
 LD A,E
 AND A
 RET

H2TJ3:
 LD A,48
 ADD A,E
 LD (IY),A
 INC IY
 INC C
 RET

H2TJ1:
 EX DE,HL
 DEC IX
 DEC IX
 RET

HEAD0:
 DW 1
 DW 10
 DW 100
 DW 1000
 DW 10000
 DW 0FFFFH 

TX2FCB:
 PUSH DE
 PUSH HL
 PUSH AF
 CALL TXBIG
 LD DE,DTA
 CALL P_SPACE
 POP AF
 POP HL
 LD C,A
;*MAKEDRIVE
T2F2J4:
 LD A,(HL)
 CP " "
 JP NZ,T2F2J3
 INC HL
 DEC C
 JP Z,T2FJ4X
 JP T2F2J4
T2F2J3:
 LD E,L
 LD D,H
 INC DE
 LD A,(DE)
 CP ":"
 JP NZ,T2FJ2
 CALL M_DRV
 JP NZ,T2FJ1

;*MAKENAME
T2FJ2:
 LD B,8
 LD DE,DTA+1
 CALL M_NAME
 JP NZ,T2FJ1

;MAKEEXT
 LD B,3
 LD DE,DTA+9
 CALL M_NAME
 JP NZ,T2FJ1
T2FJ0:
 POP DE
 LD HL,DTA
 LD BC,12
 LDIR
 JP RTRUE

T2FJ4X:
 POP HL;DUMMY
 JP RFALSE

T2FJ1:
 POP DE
 JP RFALSE

M_DRV:
 PUSH HL
 LD A,(HL)
 CALL TXS2B
 LD HL,HEAD1
 CALL TXMACH
 POP HL
 AND A
 JP Z,RFALSE
 LD (DTA),A
 INC HL
 INC HL
 DEC C
 DEC C
 JP RTRUE

HEAD1:DB "ABCDEFGH",0

M_NAME:
 LD A,(HL)
 CP "."
 JP NZ,MNJ0
 INC HL
MNJ0:
 LD A,B
 AND A
 JP Z,RTRUE
 LD A,C
 AND A
 JP Z,RTRUE
 LD A,(HL)
 CALL CHECK0
 JP NZ,RFALSE
 LD A,(HL)
 INC HL
 DEC C
 CP " "
 JP Z,MNJ0
 CP "*"
 JP Z,P_WILD
 CP "."
 JP Z,RTRUE
 LD (DE),A
 INC DE
 DEC B
 JP MNJ0

P_WILD:
 LD A,"?"
MFJ3:
 LD (DE),A
 INC DE
 DJNZ MFJ3
 JP RTRUE

CHECK0:
 PUSH HL
 LD HL,HEAD2
 CALL TXMACH
 AND A
 POP HL
 RET

HEAD2:
 DB 34,"+,/:;=[]",0

P_SPACE:
 LD HL,(DEFDRV)
 LD H," "
 LD (DTA),HL
 LD HL,DTA+1
 LD DE,DTA+2
 LD BC,11
 LDIR
 RET 

FCB2TX:
 PUSH IX
 PUSH DE
 POP IX
 LD C,0
 LD A,(HL)
 AND A
 JP NZ,F2TJ10
 LD A,(DEFDRV)
F2TJ10:
 ADD A,"A"-1
 LD (IX),A
 INC IX
 LD A,":"
 LD (IX),A
 INC IX
 INC HL
 INC C
 INC C
 LD B,8
 PUSH HL
F2TJ0:
 LD A,(HL)
 CP 33
 JP C,F2TJ1
 LD (IX),A
 INC HL
 INC IX
 INC C
 DJNZ F2TJ0
F2TJ1:
 POP HL
 LD DE,8
 ADD HL,DE
 LD A,(HL)
 CP 33
 JP C,F2TJ3
 LD A,"."
 LD (IX),A
 INC IX
 INC C
 LD B,3
F2TJ2:
 LD A,(HL)
 CP 33
 JP C,F2TJ3
 LD (IX),A
 INC HL
 INC IX
 INC C
 DJNZ F2TJ2
F2TJ3:
 LD A,C
 POP IX
 RET

TXS2B:
 CP "a"
 RET C
 CP "z"+1
 RET NC
 SUB 32
 RET

TXB2S:
 CALL JISCHK
 CP "A"
 RET C
 CP "Z"+1
 RET NC
 ADD A,32
 RET

JISCHK:
 CP 129
 JP C,RFALSE
 CP 160
 JP C,RTRUE
 CP 223
 JP C,RFALSE
 CP 254
 JP C,RTRUE
 JP RFALSE

TXBIG:
 LD B,A
TXBJ0:
 LD A,(HL)
 CALL TXS2B
 LD (HL),A
 INC HL
 CALL JISCHK
 JP Z,TXBJ1
 DJNZ TXBJ0
 RET
TXBJ1:;JIS JUMP
 DEC B
 RET Z
 INC HL
 JP TXBJ0

TXSMALL:
 LD B,A
TXSMJ0:
 LD A,(HL)
 CALL TXB2S
 LD (HL),A
 INC HL
 CALL JISCHK
 JP Z,TXSMJ1
 DJNZ TXSMJ0
 RET
TXSMJ1:;JIS JUMP
 DEC B
 RET Z
 INC HL
 JP TXSMJ0

TXTYPE:
 CP " "+1
 JP C,TXTPJ4
 CP "/"+1
 JP C,TXTPJ3
 CP "9"+1
 JP C,TXTPJ2
 CP "A"
 JP C,TXTPJ3
 CP "Z"+1
 JP C,TXTPJ1
 CP "a"
 JP C,TXTPJ3
 CP "z"+1
 JP C,TXTPJ1
 CP 127
 JP C,TXTPJ0
TXTPJ0:;KANA
 XOR A
 RET
TXTPJ1:;ALPHA
 LD A,1
 RET
TXTPJ2:;SUJI
 LD A,2
 RET
TXTPJ3:;KIGOH
 LD HL,TXTPJ5
 CALL TXMACH
 JP NZ,TXTPJ0
 LD A,3
 RET
TXTPJ4:;CONTOROL
 LD A,4
 RET
TXTPJ5:
 DB "!#$%_",0

TXMACH:
 PUSH BC
 PUSH HL
 LD C,1
 LD B,A
TXMCJ0:
 LD A,(HL)
 AND A
 JP Z,TXMCJ1
 CP B
 JP Z,TXMCJ2
 INC HL
 INC C
 JP TXMCJ0
TXMCJ1:
 POP HL
 POP BC
 XOR A
 RET
TXMCJ2:
 LD A,C
 AND A
 POP HL
 POP BC
 RET

TXCOMP:
 LD B,A
TXCPJ0:
 LD A,(DE)
 XOR (HL)
 JP NZ,RFALSE
 INC DE
 INC HL
 DJNZ TXCPJ0
 JP RTRUE

RSFT1:
 CALL RSFT
 SUB B
 RET Z
 LD B,A
 LD A," "
RS1J0:
 LD (HL),A
 INC HL
 DJNZ RS1J0
 RET

RSFT2:
 CALL RSFT
 SUB B
 RET Z
 LD B,A
 LD A,"0"
RS2J0:
 LD (HL),A
 INC HL
 DJNZ RS2J0
 RET
 
RSFT:
 PUSH HL
 PUSH DE
 PUSH BC
 PUSH AF
 PUSH HL
 LD E,A
 LD D,0
 ADD HL,DE
 DEC HL
 EX DE,HL
 POP HL
 LD C,B
 LD B,0
 ADD HL,BC
 DEC HL
 LDDR
 POP AF
 POP BC
 POP DE
 POP HL
 RET

TJUNK0: DB 00H
TJUNK1: DW 0000H
TJUNK2: DW 0000H
TJUNK3: DW 0000H
TJUNK4: DW 0000H

TX2MFCB:
 LD (TJUNK0),A
 AND A
 RET Z
 LD (TJUNK1),HL
 XOR A
 LD (DE),A
 LD (TJUNK4),DE
 INC DE
 LD (TJUNK2),DE
 LD (TJUNK3),BC
 LD A,B
 OR C
 JP Z,RFALSE
T2MFJ1:
 CALL TRANS
 JP NZ,T2MFJ0
 LD HL,(TJUNK4)
 INC (HL)
 LD HL,TJUNK0
 DEC (HL)
 JP Z,RTRUE
T2MFJ0:
 LD HL,(TJUNK3)
 LD A,H
 OR L
 JP Z,RTRUE
 JP T2MFJ1

TRANS:
 LD HL,(TJUNK1)
 PUSH HL
 CALL GETTKN
 LD A,E
 AND A
 JP Z,RFALSE
 POP HL
 LD DE,(TJUNK2)
 CALL TX2FCB
 JP NZ,RFALSE
 LD HL,(TJUNK2)
 LD DE,12
 ADD HL,DE
 LD (TJUNK2),HL
 JP RTRUE
 
GETTKN:
 LD HL,(TJUNK1)
 LD BC,(TJUNK3)
 LD DE,0
GTKJ0:
 LD A,(HL)
 INC HL
 LD (TJUNK1),HL
 INC DE
 DEC BC
 LD (TJUNK3),BC
 CP " "
 RET Z
 LD A,C
 OR B
 RET Z
 JP GTKJ0

	END
