;VKEY
	;.Z80
	CSEG

	INCLUDE	VWORK.INC
	INCLUDE	RABELS.INC
	INCLUDE	SYSDRV.INC
	INCLUDE	MATHDRV.INC
	INCLUDE	VDPDRV.INC
	INCLUDE	FILEDRV.INC
	INCLUDE	VCSR.INC
	INCLUDE	VVIEW.INC
	INCLUDE	VPUT.INC
	INCLUDE	VSTACK.INC
	INCLUDE	VTEXT.INC
	INCLUDE	VLH.INC

	PUBLIC	FRONT,WAIT0,WAIT1,WAIT2,WAIT3,WAIT4,KEYRST
	PUBLIC	S_TRAP,ONTRP,OFFTRP,CBEEP,SNSSTP,SCNCUT
	PUBLIC	WBUF,RBUF,REPTIM,SCNTIM
	PUBLIC	TIMION,TIMIOF,TIMISV

;ORG 800H

FRJUNK:	DW	0000H
REPTIM:	DB	20
SCNTIM:	DB	2
KREPT:	DB	0
KSCAN:	DB	1
KMAT:	DS	11

	DB	4,0,0,0
CV0BUF::
	DS	4
	DB	4,0,0,0
CVPBUF::
	DS	4

BITDTA::
	DB	8,0,1,1,2,2,18,18
	DB	3,3,19,19,35,35,35,35
	DB	4,4,20,20,36,36,36,36
	DB	52,52,52,52,52,52,52,52
	DB	5,5,21,21,37,37,37,37
	DB	53,53,53,53,53,53,53,53
	DB	69,69,69,69,69,69,69,69
	DB	69,69,69,69,69,69,69,69
	DB	6,6,22,22,38,38,38,38
	DB	54,54,54,54,54,54,54,54
	DB	70,70,70,70,70,70,70,70
	DB	70,70,70,70,70,70,70,70
	DB	86,86,86,86,86,86,86,86
	DB	86,86,86,86,86,86,86,86
	DB	86,86,86,86,86,86,86,86
	DB	86,86,86,86,86,86,86,86
	DB	7,7,23,23,39,39,39,39
	DB	55,55,55,55,55,55,55,55
	DB	71,71,71,71,71,71,71,71
	DB	71,71,71,71,71,71,71,71
	DB	87,87,87,87,87,87,87,87
	DB	87,87,87,87,87,87,87,87
	DB	87,87,87,87,87,87,87,87
	DB	87,87,87,87,87,87,87,87
	DB	103,103,103,103,103,103,103,103
	DB	103,103,103,103,103,103,103,103
	DB	103,103,103,103,103,103,103,103
	DB	103,103,103,103,103,103,103,103
	DB	103,103,103,103,103,103,103,103
	DB	103,103,103,103,103,103,103,103
	DB	103,103,103,103,103,103,103,103
	DB	103,103,103,103,103,103,103,103

CVPDTA::
	DB	255,255,255,30,28,0,27,255
	DB	255,29,255,255,255,31,1,2
	DB	3,4,5,6,7,8,9,10
	DB	11,12,13,14,15,16,17,18
	DB	19,20,21,22,23,24,25,26

FRONT:
	CALL	TIMION
	CALL	GETKEY
	LD	A,(COM)
	CP	238
	JP	NC,FRONT
; CALL KEYRST
	CALL	CBEEP
	RET

SUTERU_CVP:;YOMISUTE CVP
	CALL	GETCVP
	RET	Z
	LD	A,255
	LD	(COM),A
	RET


GETKEY:
	LD	A,0FFH
	LD	(COM),A
	CALL	GETCH
	AND	A
	JP	Z,FGJ1
	LD	(FRJUNK),A
	CP	9
	JP	Z,FGJTAB
	CP	11
	JP	Z,FGJHOME
	CP	12
	JP	Z,FGJCLS
	CP	13
	JP	Z,FGJRET
;-------------------------- ANK KEY
	XOR	A
	LD	(COM),A
	LD	A,1
	LD	(FEPBUF),A
	LD	A,(FRJUNK)
	LD	(FEPBUF+1),A
	RET

;-------------------------- CNV KEY
FGJ1:
	CALL	GETCNV
	RET	Z
	LD	H,0
	LD	L,A
	LD	DE,1800H
	ADD	HL,DE
	CALL	NRV1
	LD	(COM),A
	CP	255
	JP	Z,SUTERU_CVP
	CP	238
	JP	C,SUTERU_CVP
	CP	247
	JP	C,FGJ20
;------------------------- SAMETIME HIT
	LD	HL,01900H
	LD	DE,20H
	LD	B,8
	LD	C,247
FGJ11:
	CP	C
	JP	Z,FGXJ10
	ADD	HL,DE
	INC	C
	DJNZ	FGJ11
FGXJ10:
	PUSH	HL
	CALL	GETCVP
	POP	HL
	RET	Z
	LD	E,A
	LD	D,0
	ADD	HL,DE
	CALL	NRV1
	LD	(COM),A
	CP	238
	RET	C
	CP	246
	RET	NC
;----------------------------- PREFIX KEY
FGJ20:
	PUSH	AF
	CALL	SUTERU_CVP
	POP	AF
	LD	B,A
	LD	A,(EDFLG)
	AND	00101000B
	LD	A,B
	JP	Z,FGJ25
	CP	238;ESC CHECK
	JP	NZ,FGJ25
	LD	A,29
	LD	(COM),A
	RET
FGJ25:
	LD	HL,01A00H
	LD	DE,20H
	LD	B,8
	LD	C,238
FGJ31:
	CP	C
	JP	Z,FGXJ30
	ADD	HL,DE
	INC	C
	DJNZ	FGJ31
;------------------- KEY WAIT
FGXJ30:
	LD	(FRJUNK),HL

	LD	DE,SVDTA0
	CALL	SAVSCT
	LD	HL,(BASE5)
	CALL	RSTMSC
	LD	DE,MSD
	CALL	PUTMSD

	CALL	WAIT4
	JP	NZ,FGJ24
	LD	A,B
;-------------------- GET COM
FGJ21:
	LD	HL,(FRJUNK)
	LD	E,A
	LD	D,0
	ADD	HL,DE
	CALL	NRV1
	LD	(COM),A
	LD	HL,SVDTA0
	CALL	LODSCT
	RET
FGJ24:
	LD	A,255
	LD	(COM),A
	LD	HL,SVDTA0
	CALL	LODSCT
	RET
;------------------------------ CONTOROLKEY
FGJRET:
	LD	A,4
	LD	(COM),A
	RET

FGJTAB:
	LD	A,21
	LD	(COM),A
	RET

FGJHOME:
	LD	A,98
	LD	(COM),A
	RET
FGJCLS:
	LD	A,102
	LD	(COM),A
	RET
FGJA:
	LD	(FEPBUF),A
	LD	(FEPBUF+1),HL
	XOR	A
	LD	(COM),A
	RET

SRC_KEYMAP:
	LD	HL,KMAT
	LD	B,11
SRCKJ0:
	LD	A,(HL)
	XOR	0FFH
	JP	NZ,RTRUE
	INC	HL
	DJNZ	SRCKJ0
	JP	RFALSE

WAIT0:
	CALL	WAIT1
W0J:
	CALL	SRC_KEYMAP
	JP	NZ,W0J
	RET

WAIT1:
	CALL	TIMION
	CALL	SRC_KEYMAP
	JP	Z,WAIT1
	CALL	KEYRST
	CALL	SCNCUT
	RET

WAIT2:
	CALL	WAIT1
W2J0:
	CALL	GETCNVE
	JP	NZ,W2J7
	CALL	GETCH
	AND	A
	JP	Z,W2J0
	PUSH	AF
	PUSH	AF
	CALL	KEYRST
	POP	AF
	LD	HL,W2J1
	CALL	TXMACH
	POP	BC
	JP	NZ,W2J4
	PUSH	BC
	LD	A,B
	LD	HL,W2J2
	CALL	TXMACH
	POP	BC
	JP	NZ,W2J5
	LD	A,B
	LD	HL,W2J3
	CALL	TXMACH
	JP	Z,W2J0
	JP	W2J6
W2J4:
	JP	RTRUE
W2J5:
	JP	RFALSE
W2J6:
	LD	A,01111111B
	AND	A
	RET
W2J7:
	CALL	SNSSTP
	JP	NZ,W2J0
	LD	A,10111111B
	AND	A
	RET

W2J1:	DB	"Yy",13,0
W2J2:	DB	"Nn ",0
W2J3:	DB	"Aa",0

WAIT3:
	CALL	WAIT1
W3J0:
	CALL	GETCNVE
	JP	NZ,W2J5
	CALL	GETCH
	AND	A
	JP	Z,W3J0
	PUSH	AF
	CALL	KEYRST
	POP	AF
	LD	HL,W3J1
	CALL	TXMACH
	JP	Z,W2J5
	DEC	A
	SRL	A
	INC	A
	LD	B,A
	JP	W2J4

W3J1:
	DB	"AaBbCcDdEeFfGgHh",0

WAIT4:
	CALL	GETCH
	AND	A
	JP	Z,W4J0
	CP	64
	JP	C,RFALSE;EXIT
	CP	128
	JP	NC,RFALSE;EXIT
	AND	31
	LD	B,A
	JP	RTRUE
W4J0:
	CALL	GETCNVE
	JP	Z,WAIT4
	LD	L,A
	LD	H,0
	LD	DE,1800H
	ADD	HL,DE
	CALL	NRV1
	CP	246
	JP	C,RFALSE;EXIT
	CALL	GETCVP
	JP	Z,WAIT4;REPEAT
	LD	B,A
	JP	RTRUE

SNSSTP:
	LD	C,7
	CALL	SNSMAT
	XOR	0FFH
	AND	00010100B
	JP	NZ,T_RTRUE
	JP	T_RFALSE

TIMISV:
	LD	HL,TIMI
	LD	DE,CUTJ
	LD	BC,3
	LDIR
	RET

CUTJ:
	DS	5

TIMIOF:
	DI
	LD	A,0C9H
	LD	(TIMI),A
	RET

TIMION:
	DI
	LD	HL,CUTJ
	LD	DE,TIMI
	LD	BC,3
	LDIR
	EI
	RET

TSAVE:
	NOP
	NOP
	NOP
	NOP
	NOP
	RET

S_TRAP:
	DI
	LD	HL,H.TIMI
	LD	DE,TSAVE
	LD	BC,5
	LDIR
	EI
	RET

ONTRP:
	LD	A,(0F341H)
	LD	(TRDTA+1),A
	LD	HL,KEYTRP
	LD	(TRDTA+2),HL

	DI
	LD	HL,TRDTA
	LD	DE,H.TIMI
	LD	BC,5
	LDIR
	EI
	RET

TRDTA:
	RST	30H
	DB	00H
	DW	0000H
	RET

OFFTRP:
	LD	A,2
	LD	(0F3F6H),A
	LD	A,50
	LD	(0F3F7H),A
	DI
	LD	HL,TSAVE
	LD	DE,H.TIMI
	LD	BC,5
	LDIR
	EI
	RET

KEYTRP:
	CALL	CHECK_SCANTIME
	JP	NZ,BIOSKEY_OFF2

	CALL	SET_KEYMAP
	LD	A,D
	AND	1
	JP	Z,BIOSKEY_OFF
	CALL	RST_REPTIM
	JP	NZ,BIOSKEY_OFF
	PUSH	DE
	CALL	CHECK_CNV
	POP	DE
	JP	Z,BIOSKEY_ON
	CALL	MAKE_CNV
	JP	BIOSKEY_OFF

BIOSKEY_ON:
	LD	HL,0101H
	LD	(0F3F6H),HL
	JP	TSAVE

BIOSKEY_OFF:
	CALL	C_KMAT
BIOSKEY_OFF2:
	LD	HL,0FFFFH
	LD	(0F3F6H),HL
	JP	TSAVE

C_KMAT:
	LD	HL,KMAT
	LD	DE,NEWKEY
	LD	BC,11
	LDIR
	LD	HL,KMAT
	LD	DE,OLDKEY
	LD	BC,11
	LDIR
	RET

CHECK_SCANTIME:
	LD	HL,KSCAN
	DEC	(HL)
	RET	NZ
	LD	A,(SCNTIM)
	LD	(HL),A
	JP	T_RTRUE

SET_KEYMAP:
	LD	DE,0
	LD	BC,0B00H
	LD	HL,KMAT
SKMJ0:
	CALL	SNSMAT
	LD	E,(HL)
	LD	(HL),A
	CP	E
	CALL	NZ,SKMJ2
	LD	A,(HL)
	XOR	0FFH
	CALL	NZ,SKMJ1
	INC	C
	INC	HL
	DJNZ	SKMJ0
	RET
SKMJ1:
	LD	A,1
	OR	D
	LD	D,A;KEY ON FLAG
	RET
SKMJ2:
	LD	A,2
	OR	D
	LD	D,A;HENKA
	RET

RST_REPTIM:
	LD	A,D
	AND	2
	JP	NZ,RSRJ0
	LD	HL,KREPT
	DEC	(HL)
	RET	NZ
	LD	A,1
	LD	(HL),A
	XOR	A
	RET
RSRJ0:
	LD	A,(REPTIM)
	LD	(KREPT),A
	XOR	A
	RET

CHECK_CNV:
	LD	IX,KMAT+6
	LD	A,(IX)
	AND	11100110B
	LD	L,A
	LD	A,(IX+1)
	AND	01100100B
	SRL	A
	SRL	A
	OR	L
	XOR	0FFH
	LD	L,A

	LD	A,(IX+2)
	AND	11111100B
	LD	H,A
	LD	A,(IX+1)
	AND	00000011B
	OR	H
	XOR	0FFH
	LD	H,A
	OR	L
	RET

SNSFEP:
	LD	A,(KMAT+6)
	AND	00000010B
	RET	NZ
	LD	A,(KMAT+8)
	AND	00000001B
	RET

MAKE_CNV:
	CALL	SNSFEP
	JP	Z,MCJ0X
	LD	C,L
	LD	B,0
	LD	IX,BITDTA
	ADD	IX,BC
	LD	C,H
	LD	B,0
	LD	IY,BITDTA
	ADD	IY,BC

	LD	A,(IX)
	CP	8
	JP	Z,MCJ0
	CP	16
	JP	NC,SET_CNV
;SINGLE
	LD	D,A
	LD	A,(IY)
	CP	8
	JP	Z,MCJ1
	AND	00001111B
	OR	00001000B
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	OR	D
	JP	SET_CNV
MCJ1:
	LD	A,D
	JP	SET_CNV

MCJ0:
	LD	A,(IY)
	CP	8
	RET	Z
	CP	16
	JP	NC,MCJ2
	OR	00001000B
	JP	SET_CNV
MCJ2:
	OR	10001000B
	JP	SET_CNV

MCJ0X:;CTRL+SPACE -> CTRL+SELECT
	LD	A,16+4
	JP	SET_CNV


SET_CNV:
	LD	DE,CV0BUF
	CALL	WBUF
	CALL	GAZKEY
	JP	Z,SCVP
	LD	B,255
SCVP:
	LD	A,B
	LD	DE,CVPBUF
	CALL	WBUF
	RET

GAZKEY:
	LD	B,5
	LD	DE,KMAT+1
	LD	L,0
GAZJ0:
	LD	A,(DE)
	XOR	0FFH
	JP	NZ,GAZJ1
	INC	DE
	LD	A,L
	ADD	A,8
	LD	L,A
	DJNZ	GAZJ0
	JP	T_RFALSE
GAZJ1:
	LD	IX,BITDTA
	LD	D,0
	LD	E,A
	ADD	IX,DE
	LD	A,(IX)
	AND	00001111B
	ADD	A,L
	LD	L,A
	LD	H,0
	LD	DE,CVPDTA
	ADD	HL,DE
	LD	B,(HL)
	JP	T_RTRUE

GETCNVE:
	CALL	GETCVP
	CALL	GETCNV
	RET

GETCNV:
	LD	DE,CV0BUF
	CALL	RBUF
	RET

GETCVP:
	LD	DE,CVPBUF
	CALL	RBUF
	RET	Z
	CP	255
	RET

GETCH:
	LD	C,06H
	LD	E,0FFH
	CALL	0005H
	CP	32
	JP	C,GCJ0
	CP	127
	JP	Z,GCJ1
	RET	C
; CP 160
; JP C,GCJ2
; CP 224
; RET C
; SUB 32
	RET
;GCJ2:
; ADD A,32
; RET
GCJ0:
	CP	9
	RET	Z
	CP	11
	RET	Z
	CP	12
	RET	Z
	CP	13
	RET	Z
GCJ1:
	XOR	A
	RET

SNSMAT:
	IN	A,(0AAH)
	AND	0F0H
	ADD	A,C
	OUT(0AAH),A
	IN	A,(0A9H)
	RET

CBEEP:
	PUSH	AF
	LD	A,(CLICKF)
	AND	A
	JP	Z,CLBJ1
	DI
	LD	A,0FH
	OUT(0ABH),A
	LD	A,0AH
CLBJ0:
	DEC	A
	JR	NZ,CLBJ0
	LD	A,0EH
	OUT(0ABH),A
	EI
CLBJ1:
	POP	AF
	RET

KEYRST:
	LD	HL,0
	LD	(CV0BUF-3),HL
	LD	(CVPBUF-3),HL
	XOR	A
	LD	(CV0BUF-1),A
	LD	(CVPBUF-1),A
	RET

SCNCUT:
	LD	HL,0FBF0H
	LD	(0F3FAH),HL
	LD	(0F3F8H),HL
	RET

WBUF:
	PUSH	IX
	PUSH	DE
	POP	IX
	LD	L,(IX-1)
	LD	H,0
	ADD	HL,DE
	LD	(HL),A
	CALL	INCW
	CALL	CHKSIZW
	POP	IX
	RET

RBUF:
	PUSH	IX
	PUSH	DE
	POP	IX
	CALL	CHKSIZR
	JP	Z,RBFJ0

	LD	L,(IX-2)
	LD	H,0
	ADD	HL,DE
	LD	A,(HL)
	LD	L,A
	CALL	INCR
	LD	A,0FFH
	AND	A
	LD	A,L
RBFJ0:
	POP	IX
	RET

INCR:
	INC	(IX-2)
	LD	A,(IX-2)
	LD	B,(IX-4)
	CP	B
	RET	C
	XOR	A
	LD	(IX-2),A
	RET

INCW:
	INC	(IX-1)
	LD	A,(IX-1)
	LD	B,(IX-4)
	CP	B
	RET	C
	XOR	A
	LD	(IX-1),A
	RET

CHKSIZR:
	LD	A,(IX-1)
	LD	B,(IX-2)
	CP	B
	RET

CHKSIZW:
	LD	A,(IX-1)
	LD	B,(IX-2)
	CP	B
	RET	NZ
	CALL	INCR;READ CURSOR RESET
	RET

T_RFALSE:
	LD	A,0FFH
	AND	A
	RET

T_RTRUE:
	XOR	A
	RET

	END
