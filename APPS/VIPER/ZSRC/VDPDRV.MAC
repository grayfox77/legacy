;----------------------------------
;            MSX2 VDPDRV
;----------------------------------
	CSEG
	.Z80

MACLIB B:\ZERO\MAC\RABELS.INC
MACLIB B:\ZERO\MAC\SYSDRV.INC
MACLIB B:\ZERO\MAC\MATHDRV.INC
MACLIB B:\ZERO\MAC\VWORK.INC
EXTRN TCAD,CMPD0,CMPD1,ZFONT0,ZFONT1,ZFONT2

PUBLIC NSTR1,NSTW1,NSTR2,NSTW2
PUBLIC NSTR3,NSTW3,NSTR4,NSTW4
PUBLIC NRV1,NWV1,NRV2,NWV2
PUBLIC WSYNC,WVDP,SVDP,VWAIT
PUBLIC VRM,VRMW,VRMK,VRMX,VFIRST,RPT,WPT
PUBLIC VR2M,M2VR,M2V,V2M,ZM2V,ZV2M,M2VRK,M2VRK2,M2VRKX,M2VRKC
PUBLIC VM2V,VV2M,EM2V,EV2M,EZM2V,EZV2M
PUBLIC SVVDPW,LDVDPW,SVPLT,LDPLT

BJUNK0: DW 0000H
VSWT:   DB 00H
VPTR:   DW 0000H

RPT: DB 00H
WPT: DB 00H
BVR: DB 00H

VFIRST:
 LD A,(EXPTBL)
 LD HL,0006H
 CALL RDSLT
 LD (RPT),A
 LD A,(EXPTBL)
 LD HL,0007H
 CALL RDSLT
 LD (WPT),A
 LD A,(EXPTBL)
 LD HL,002DH
 CALL RDSLT
 LD (BVR),A
 RET

LDVDPW:
 LD DE,RG0SAV
 LD BC,8
 LDIR
 LD DE,RG8SAV
 LD BC,10
 LDIR

;JUMP R#18
 INC HL
 INC DE

 LD BC,5
 LDIR
 LD DE,RG25SA
 LD BC,3
 LDIR
 RET

SVVDPW:
 LD HL,RG0SAV
 LD BC,8
 LDIR
 LD HL,RG8SAV
 LD BC,16
 LDIR
 LD HL,RG25SA
 LD BC,3
 LDIR
 RET

LDPLT:
 LD B,16
 LD A,0
LDPJ0:
 PUSH AF
 PUSH BC
 PUSH DE
 LD IX,0149H
 CALL GOBIO2
 POP DE
 LD H,B
 SRL H
 SRL H
 SRL H
 SRL H
 LD A,H
 AND 00000111B
 LD (DE),A
 INC DE
 LD A,C
 AND 00000111B
 LD (DE),A
 INC DE
 LD A,B
 AND 00000111B
 LD (DE),A
 INC DE
 POP BC
 POP AF
 INC A
 DJNZ LDPJ0
 RET

SVPLT:
 LD B,16
 LD D,0
SVPJ0:
 LD A,(HL)
 AND 00000111B
 INC HL
 ADD A,A
 ADD A,A
 ADD A,A
 ADD A,A
 
 PUSH AF
 LD A,(HL)
 AND 00000111B
 INC HL
 LD E,A
 LD A,(HL)
 INC HL
 AND 00000111B
 LD C,A
 POP AF
 OR C
 PUSH HL
 LD IX,014DH
 CALL GOBIO2
 POP HL
 INC D
 DJNZ SVPJ0
 RET

YJUNKZ:DB 00H
YJUNK0:DW 0000H
YJUNK1:DW 0000H

VRMY1:
 LD A,(YJUNKZ)
 AND A
 RET Z

 LD HL,(YJUNK0)
 LD DE,(YJUNK1)

 XOR A
 LD (YJUNKZ),A
 LD L,192
 JP YMMM


YMMM:

; L= SOURCE Y
; E= DISTNATION Y
; D= LONG
; H= DIX+DIY

 DI
 CALL VWAIT
 
 LD A,(WPT)
 LD C,A
 INC C
 LD A,34
 OUT(C),A
 LD A,17+80H
 OUT(C),A
 INC C
 INC C
 XOR A
 OUT(C),L
 OUT(C),A

 OUT(C),A
 OUT(C),A
 OUT(C),E
 OUT(C),A
 OUT(C),A
 OUT(C),A
 OUT(C),D
 OUT(C),A
 OUT(C),A
 OUT(C),H
 LD A,11100000B
 OUT(C),A
 RET
 
GET.STATUS:
 PUSH BC
 LD BC,(RPT)
 INC C
 OUT(C),A
 LD A,8FH
 OUT(C),A
 IN A,(C)
 POP BC
 RET
 
VWAIT:
 LD A,2
 CALL GET.STATUS
 AND 1
 JP NZ,VWAIT
 XOR A
 CALL GET.STATUS
 RET

VRMX:
 LD A,(WIDTH)
 CP 32
 JP NZ,VRMK
 LD A,(BVR)
 CP 02H
 JP C,VRMK
 LD A,C
 AND 000001111B
 JP NZ,VRMK
VRMY0:
 SRL H
 RR L
 SRL H
 RR L
 SRL H
 RR L
 SRL H
 RR L

 SRL D
 RR E
 SRL D
 RR E
 SRL D
 RR E
 SRL D
 RR E
 
 SRL B
 RR C
 SRL B
 RR C
 SRL B
 RR C
 SRL B
 RR C

 LD A,C
 AND A
 RET Z
 LD A,0FFH
 LD (YJUNKZ),A

; L= SOURCE Y
; E= DISTNATION Y
; D= LONG
; H= DIX+DIY
 LD H,0
 LD D,C
 LD (YJUNK0),HL
 LD (YJUNK1),DE
 
 LD E,192
 JP YMMM

VRMK:
 CALL HL16M
 CALL DE16M
 CALL BC16M
 XOR A
VRMW:
 PUSH AF
 PUSH BC
 PUSH DE
 PUSH HL
 LD (BJUNK0),A
 LD A,B
 OR C
 JP Z,VWJ0
 DI
 PUSH BC
 PUSH DE
 CALL ANSTR1
 PUSH BC
 POP DE
 LD HL,(BUFPTR) 
 CALL VV2M
 CALL WSYNC
 POP HL
 CALL ANSTW2
 POP DE
 LD HL,(BUFPTR)
 CALL VM2V
VWJ0:
 POP HL
 POP DE
 POP BC
 POP AF
 RET

VRM:
 PUSH AF
 PUSH BC
 PUSH DE
 PUSH HL
 PUSH HL
 DI
 LD (BJUNK0),A
 AND A
 SBC HL,DE
 POP HL
 JP NC,VJ0
 CALL VRMA1
 JP VJ1
VJ0:
 CALL VRMA2
VJ1:
 POP HL
 POP DE
 POP BC
 POP AF
 RET

VR2M:
 PUSH AF
 PUSH BC
 PUSH DE
 PUSH HL
 DI
 CALL NSTR3
 EX DE,HL
 PUSH BC
 POP DE
 CALL V2M
;EI
 POP HL
 POP DE
 POP BC
 POP AF
 RET

M2VR:
 PUSH AF
 PUSH BC
 PUSH DE
 PUSH HL
 DI
 EX DE,HL
 CALL NSTW3
 EX DE,HL
 PUSH BC
 POP DE
 CALL M2V
;EI
 POP HL
 POP DE
 POP BC
 POP AF
 RET

VIJ:
 LD HL,(BUFPTR)
 LD A,(RPT)
 LD C,A
 LD B,0
 CALL VINIR
 CALL VINIR
 CALL VINIR
 CALL VINIR

 CALL VINIR
 CALL VINIR
 CALL VINIR
 CALL VINIR
 RET

VOJ:
 LD HL,(BUFPTR)
 LD A,(WPT)
 LD C,A
 LD B,0
 CALL VOTIR
 CALL VOTIR
 CALL VOTIR
 CALL VOTIR

 CALL VOTIR
 CALL VOTIR
 CALL VOTIR
 CALL VOTIR
 RET

VRMA1:
 LD A,B
 AND 11111000B
 JP Z,VRM1
 PUSH HL
 PUSH DE
 PUSH BC
 ADD HL,BC
 EX DE,HL
 ADD HL,BC
 EX DE,HL
 SRA B
 SRA B
 SRA B
W1:
 PUSH BC
 LD A,D
 SUB 8
 LD D,A
 LD A,H
 SUB 8
 LD H,A
 CALL ANSTR1
 PUSH HL
 CALL VIJ
 EX DE,HL
 CALL ANSTW2
 EX DE,HL
 CALL VOJ
 POP HL
 POP BC
 DJNZ W1
 POP BC
 LD A,B
 AND 00000111B
 LD B,A
 POP DE
 POP HL
 JP VRM1

VRMA2:
 LD A,B
 AND 11111000B
 JP Z,VRM1
 PUSH BC
 SRA B
 SRA B
 SRA B
W2:
 PUSH BC
 CALL ANSTR1
 PUSH HL
 CALL VIJ
 EX DE,HL
 CALL ANSTW2
 EX DE,HL
 CALL VOJ
 POP HL
 LD A,H
 ADD A,8
 LD H,A
 LD A,D
 ADD A,8
 LD D,A
 POP BC
 DJNZ W2
 POP BC
 LD A,B
 AND 00000111B
 LD B,A

VRM1:
 LD A,B
 OR C
 RET Z
 PUSH BC
 PUSH DE
 CALL ANSTR1
 PUSH BC
 POP DE
 LD HL,(BUFPTR) 
 CALL V2M
 POP HL
 CALL ANSTW2
 POP DE
 LD HL,(BUFPTR)
 CALL M2V
 RET

RESVR:
 LD A,D
 AND 00111111B
 RET NZ
 LD A,(VSWT)
 EX DE,HL
 CALL NSTR3
 EX DE,HL
 RET

RESVW:
 LD A,D
 AND 00111111B
 RET NZ
 LD A,(VSWT)
 EX DE,HL
 CALL NSTW3
 EX DE,HL
 RET

IOTAIL:
 LD (VPTR),DE
 POP DE
 RET

VINIR:
 PUSH DE
 LD DE,(VPTR)
VIRJ0:  
 INI
 JP Z,VIRJ1
 INC E
 JP NZ,VIRJ0
 INC D
 CALL RESVR
 JP VIRJ0
VIRJ1:
 INC E
 JP NZ,IOTAIL
 INC D
 CALL RESVR
 JP IOTAIL

VOTIR:
 PUSH DE
 LD DE,(VPTR)
VORJ0:  
 OUTI
 JP Z,VORJ1
 INC E
 JP NZ,VORJ0
 INC D
 CALL RESVW
 JP VORJ0
VORJ1:
 INC E
 JP NZ,IOTAIL
 INC D
 CALL RESVW
 JP IOTAIL

VINDR:
 PUSH DE
 LD DE,(VPTR)
VIDRJ0:  
 IND
 JP Z,VIDRJ1
 INC E
 JP NZ,VIDRJ0
 INC D
 CALL RESVR
 JP VIDRJ0
VIDRJ1:
 INC E
 JP NZ,IOTAIL
 INC D
 CALL RESVR
 JP IOTAIL

VOTDR:
 PUSH DE
 LD DE,(VPTR)
VODRJ0:  
 OUTD
 JP Z,VODRJ1
 INC E
 JP NZ,VODRJ0
 INC D
 CALL RESVW
 JP VODRJ0
VODRJ1:
 INC E
 JP NZ,IOTAIL
 INC D
 CALL RESVW
 JP IOTAIL

V2M:
 LD A,(RPT)
 LD C,A

 LD A,D
 AND A
 JP Z,V16
 LD B,0
V14:
 CALL VINIR
 DEC D
 JP NZ,V14
V16:
 LD A,E
 AND A
 RET Z
 LD B,E
 CALL VINIR
 RET

EM2V:
 CALL NSTW4
 JP M2V

EV2M:
 CALL NSTR4
 JP V2M

EZM2V:
 CALL NSTW4
 JP ZM2V

EZV2M:
 CALL NSTR4
 JP ZV2M

M2V:
 LD A,(WPT)
 LD C,A

 LD A,D
 AND A
 JP Z,V26
 LD B,0
V24:
 CALL VOTIR
 DEC D
 JP NZ,V24
V26:
 LD A,E
 AND A
 RET Z
 LD B,E
 CALL VOTIR
 RET

ZV2M:
 LD A,(RPT)
 LD C,A

 LD A,D
 AND A
 JP Z,V116
 LD B,0
V114:
 CALL VINDR
 DEC D
 JP NZ,V114
V116:
 LD A,E
 AND A
 RET Z
 LD B,E
 CALL VINDR
 RET

ZM2V:
 LD A,(WPT)
 LD C,A

 LD A,D
 AND A
 JP Z,V126
 LD B,0
V124:
 CALL VOTDR
 DEC D
 JP NZ,V124
V126:
 LD A,E
 AND A
 RET Z
 LD B,E
 CALL VOTDR
 RET

VV2M:
 LD A,(RPT)
 LD C,A

 LD A,D
 AND A
 JP Z,V36
 LD B,0
V34:
 INIR
 DEC D
 JP NZ,V34
V36:
 LD A,E
 AND A
 RET Z
 LD B,E
 INIR
 RET

VM2V:
 LD A,(WPT)
 LD C,A

 LD A,D
 AND A
 JP Z,V46
 LD B,0
V44:
 OTIR
 DEC D
 JP NZ,V44
V46:
 LD A,E
 AND A
 RET Z
 LD B,E
 OTIR
 RET

NST:
 LD (VPTR),HL
 LD A,(WPT)
 LD C,A
 INC C
 XOR A
 OUT(C),A
 LD A,173
 OUT(C),A
 LD A,H
 RLCA
 RLCA
 AND 3
 RET

NST1:
 XOR A
 LD (VSWT),A
 CALL NST
 JP NJ0 

NST2:
 LD A,0FFH
 LD (VSWT),A
 CALL NST
 OR 4
NJ0:
 OUT(C),A
 LD A,142
 OUT(C),A
 OUT(C),L
 LD A,H
 AND 63
 RET

NSTR1:
 DI
 PUSH BC
 CALL NST1
 OUT(C),A
; EI
 POP BC
 RET

NSTR2:
 DI
 PUSH BC
 CALL NST2
 OUT(C),A
; EI
 POP BC
 RET

NSTW1:
 DI
 PUSH BC
 CALL NST1
 OR 64
 OUT(C),A
; EI
 POP BC
 RET

NSTW2:
 DI
 PUSH BC
 CALL NST2
 OR 64
 OUT(C),A
; EI
 POP BC
 RET

NSTR3:
 PUSH BC
 AND A
 JP NZ,N0
 CALL NST1
 OUT(C),A
 POP BC
 RET
N0:
 CALL NST2
 OUT(C),A
 POP BC
 RET

NSTW3:
 PUSH BC
 AND A
 JP NZ,N1
 CALL NST1
 OR 64
 OUT(C),A
 POP BC
 RET
N1:
 CALL NST2
 OR 64
 OUT(C),A
 POP BC
 RET

NSTR4:
 PUSH AF
 PUSH HL
 LD HL,(VPTR)
 LD A,(VSWT)
 CALL NSTR3
 POP HL
 POP AF
 RET

NSTW4:
 PUSH AF
 PUSH HL
 LD HL,(VPTR)
 LD A,(VSWT)
 CALL NSTW3
 POP HL
 POP AF
 RET

ANSTR1:
 LD A,(BJUNK0)
 AND 1
 JP NSTR3

ANSTW1:
 LD A,(BJUNK0)
 AND 1
 JP NSTW3

ANSTR2:
 LD A,(BJUNK0)
 AND 2
 JP NSTR3

ANSTW2:
 LD A,(BJUNK0)
 AND 2
 JP NSTW3

NRV1:
 PUSH BC
 CALL NSTR1
 LD A,(RPT)
 LD C,A
 IN A,(C)
 POP BC
 RET

NWV1:
 PUSH BC
 PUSH AF
 CALL NSTW1
 LD A,(WPT)
 LD C,A
 POP AF
 OUT(C),A
 POP BC
 RET

NRV2:
 PUSH BC
 CALL NSTR2
 LD A,(RPT)
 LD C,A
 IN A,(C)
 POP BC
 RET

NWV2:
 PUSH BC
 PUSH AF
 CALL NSTW2
 LD A,(WPT)
 LD C,A
 POP AF
 OUT(C),A
 POP BC
 RET

WSYNC:
 PUSH AF
 PUSH BC
 DI
 LD A,(RPT)
 LD C,A
 INC C
WSJ0:
 LD A,2
 OUT(C),A
 LD A,15+80H
 OUT(C),A
 IN A,(C)
 AND 64
 JP Z,WSJ0

 XOR A
 OUT(C),A
 LD A,15+80H
 OUT(C),A
 POP BC
 POP AF
 RET

WVDP:
 PUSH AF
 PUSH HL
 LD A,B
 LD B,0
 LD HL,RG0SAV
 ADD HL,BC
 LD (HL),A
 LD B,A
 POP HL
 POP AF
 RET

SVDP:
 PUSH AF
 PUSH BC
 PUSH HL
 CALL WSYNC
 LD HL,0
 CALL NRV1;DUMMY READ
 LD A,(WPT)
 LD C,A
 INC C
 XOR A
 OUT(C),A
 LD A,17+80H
 OUT(C),A
 INC C
 INC C
 LD HL,RG0SAV
 LD B,8
 OTIR
 LD HL,RG8SAV
 LD B,16
 OTIR
 LD HL,RG25SA
 LD B,3
 OTIR
 POP HL
 POP BC
 POP AF
 EI
 RET

VJUNKP: DB 00H
VJUNK0: DW 0000H
VJUNK1: DW 0000H

M2VRKC:
 LD A,(PTFLG1)
 LD (VJUNKP),A
 LD A,B
 OR C
 RET Z
 PUSH HL
 ADD HL,BC
 CALL INEG
 LD (VJUNK0),HL
 POP DE
 LD BC,0
VRMCJ0:
 LD HL,(VJUNK0)
 ADD HL,DE
 RET C
 CALL IFONT
 JP VRMCJ0


IFONT:
 LD A,(DE)
 INC DE
 LD L,A
 LD H,HIGH TCAD
 LD A,(HL)
 CP 6
 LD A,L
 JP NZ,IFONT0
 LD H,A
 LD A,(DE)
 INC DE
 LD L,A
 LD A,(VJUNKP)
 AND 00100000B
 JP NZ,IFONT1
 JP IFONT2

IFONT0:
 INC BC
 RET
IFONT1:
IFONT2:
 INC BC
 RET

M2VRK:
 LD A,(PTFLG1)
 JP M2VRKX
M2VRK2:
 LD A,(PTFLG2)
M2VRKX:
 LD (VJUNKP),A
 LD A,B
 OR C
 RET Z
 PUSH HL
 ADD HL,BC
 CALL INEG
 LD (VJUNK0),HL
 EX DE,HL
 CALL HL16M
 CALL NSTW1
 CALL VRMY1
 POP DE
 LD BC,0
VRMZJ0:
 LD HL,(VJUNK0)
 ADD HL,DE
 RET C
 CALL PFONT
 JP VRMZJ0

PFONT:
 LD A,(DE)
 INC DE
 LD L,A
 LD H,HIGH TCAD
 LD A,(HL)
 CP 6
 LD A,L
 JP NZ,FONT0
 LD H,A
 LD A,(DE)
 INC DE
 LD L,A
 LD A,(VJUNKP)
 AND 00100000B
 JP NZ,FONT1
 JP FONT2

FONT0:;ANK PUT
 INC BC
 PUSH BC
 PUSH DE
 LD D,20H
 LD E,A
 CP 32
 JP C,FNT0J1
 CP 0A0H
 CALL NC,FNT0J0
 LD HL,0D9D8H
 LD (KROMPT),HL
 CALL JISROM
 CALL TRS16
 POP DE
 POP BC
 RET
FNT0J0:;ANK KANA
 LD HL,920H - 0A0H
 ADD HL,DE
 EX DE,HL
 RET

FNT0J1:;SUPER CONTOROL
 CP 13
 JP NC,FNT0J2
 ADD A,A
 LD E,A
 LD D,0
 LD HL,FNTXD
 ADD HL,DE
 LD E,(HL)
 INC HL
 LD D,(HL)
 EX DE,HL
 JP FNTX

FNT0J2:
 LD HL,ZFONT1
 JP FNTX
 
FNTXD:
 DW ZFONT1
 DW ZFONT0+(0BH * 16)
 DW ZFONT0+(0DH * 16)
 DW ZFONT0+(0AH * 16)

 DW ZFONT0+(0CH * 16)
 DW ZFONT0+(2CH * 16)
 DW ZFONT0+(2DH * 16)
 DW ZFONT0+(2EH * 16)

 DW ZFONT0+(2FH * 16)
 DW ZFONT0+(4EH * 16)
 DW ZFONT0+(4FH * 16)
 DW ZFONT0+(0EH * 16)
 
 DW ZFONT0+(0FH * 16)


FNTX:
 CALL M2V16
 POP DE
 POP BC
 RET

FONT2:;KANJI PUT 2
 INC BC
 INC BC
 PUSH BC
 PUSH DE
 CALL SJIS2J
 CALL JISROM
 CALL TRS32
 POP DE
 POP BC
 RET

FONT1:;KANJI PUT 1
 INC BC
 PUSH BC
 PUSH DE
 CALL SJIS2J
 LD A,D
 CP 26H
 JP C,FNT1J0
FNT1J1:
 CALL JISROM
 CALL TRS32X
 POP DE
 POP BC
 RET
FNT1J0:
 CP 23H
 JP C,FNT1J1
 JP Z,FNT1J2
 CP 24H
 JP Z,FNT1J3
 JP FNT1J4
FNT1J2:;ALPHA
 LD D,0
 EX DE,HL
 CALL HL16M
 LD DE,ZFONT0 - (30H*16)
 ADD HL,DE
 LD DE,16
 CALL M2V16K
 POP DE
 POP BC
 RET
FNT1J3:;HIRAGANA
 LD D,0
 EX DE,HL
 CALL HL16M
 LD DE,ZFONT1 - (20H*16)
 
 ADD HL,DE
 LD DE,16
 CALL M2V16K
 POP DE
 POP BC
 RET

FNT1J4:;KATAKANA
 LD D,0
 EX DE,HL
 CALL HL16M
 LD DE,ZFONT2 - (20H*16)
 ADD HL,DE
 LD DE,16
 CALL M2V16K
 POP DE
 POP BC
 RET

M2V16X:
 LD A,(WPT)
 LD C,A
 LD B,16
M2V16XJ0:
 LD A,(HL)
 XOR 0FFH
 OUT (C),A
 INC HL
 DJNZ M2V16XJ0
 RET

M2V16K:
 LD A,(VJUNKP)
 AND 00010000B
 JP NZ,M2V16X
M2V16:
 LD A,(WPT)
 LD C,A
 OUTI
 OUTI
 OUTI
 OUTI

 OUTI
 OUTI
 OUTI
 OUTI

 OUTI
 OUTI
 OUTI
 OUTI

 OUTI
 OUTI
 OUTI
 OUTI
 RET

TRS16:
 LD HL,DTA+2700H
 LD C,0D9H
 LD A,(VJUNKP)
 AND 00000010B
 JP Z,FT0J1

 LD B,8
FT0J5:
 IN A,(0D9H)
 DJNZ FT0J5

FT0J1:
 LD B,8
 INIR
 LD B,8
FT0J0:
 IN A,(0D9H)
 DJNZ FT0J0

 LD B,8
 INIR
 LD HL,DTA+2700H
 LD A,(VJUNKP)
 AND 1
 JP NZ,M2V16X
 JP M2V16

TRS32X:
 LD DE,DTA+2700H
 LD H,HIGH CMPD0
 LD BC,(KROMPT+1)
 LD B,8
FTX1J0:
 IN A,(C)
 LD L,A
 LD A,(HL)
 LD (DE),A
 INC DE
 DJNZ FTX1J0

 LD BC,(KROMPT+1)
 LD B,8
 LD H,HIGH CMPD1
 LD IX,DTA+2700H
FTX1J1:
 IN A,(C)
 LD L,A
 LD E,(HL)
 LD A,(IX)
 OR E
 LD (IX),A
 INC IX
 DJNZ FTX1J1

 LD BC,(KROMPT+1)
 LD B,8
 LD H,HIGH CMPD0
 LD DE,DTA+2700H+8
FTX1J2:
 IN A,(C)
 LD L,A
 LD A,(HL)
 LD (DE),A
 INC DE
 DJNZ FTX1J2
 
 LD BC,(KROMPT+1)
 LD B,8
 LD H,HIGH CMPD1
 LD IX,DTA+2700H+8
FTX1J3:
 IN A,(C)
 LD L,A
 LD E,(HL)
 LD A,(IX)
 OR E
 LD (IX),A
 INC IX
 DJNZ FTX1J3

 LD HL,DTA+2700H
 CALL M2V16K
 RET

TRS32:
 LD HL,DTA+2700H
 LD BC,(KROMPT+1)
 LD B,8
 INIR
 LD B,8
 LD HL,DTA+2700H+16
 INIR
 LD B,8
 LD HL,DTA+2700H+8
 INIR
 LD B,8
 LD HL,DTA+2700H+24
 INIR
 
 LD HL,DTA+2700H
 CALL M2V16K
 CALL M2V16K
 RET
 
KROMPT: DW 0D9D8H

JISROM:
 ;DE<-JIS
 LD A,E
 SUB 20H
 LD E,A
 LD A,D
 CP 50H
 JP NC,JSRM2
 LD HL,0D9D8H
 LD (KROMPT),HL
 
 SUB 20H
 LD D,A
 CALL H96M
 LD A,D
 CP 16
 CALL NC,SUB512
 LD C,E
 LD B,0
 ADD HL,BC
TRSJRM:
 LD BC,(KROMPT)
 LD A,L
 AND 00111111B
 OUT(C),A
 INC C
 ADD HL,HL
 ADD HL,HL
 OUT(C),H
 RET
JSRM2:
 SUB 80
 LD D,A
 CALL H96M
 LD C,E
 LD B,0
 ADD HL,BC
 LD DE,0DBDAH
 LD (KROMPT),DE
 JP TRSJRM
 
H96M:
 LD C,D
 LD B,0
 LD HL,0
 ADD HL,BC
 ADD HL,BC
 ADD HL,BC
 ADD HL,HL
 ADD HL,HL
 ADD HL,HL
 ADD HL,HL
 ADD HL,HL
 RET

SUB512:
 LD BC,512
 AND A
 SBC HL,BC
 RET

SJIS2J:
 ;HL<-SJIS JIS->DE
 LD A,H
 CP 09FH
 JP C,SJ2JJ0
 SUB 40H
SJ2JJ0:
 SUB 70H
 ADD A,A
 LD C,A

 LD A,L
 CP 80H
 ADC A,0
 CP 9FH
 JP NC,SJ2JJ1
 DEC C
 ADD A,5EH
SJ2JJ1:
 SUB 7EH
 LD D,C
 LD E,A
 RET

END

