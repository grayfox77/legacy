;----------------------------------
;            MSX2 FILEDRV
;----------------------------------
	CSEG
	.Z80

MACLIB B:\mark\MAC\RABELS.INC
MACLIB B:\mark\MAC\VWORK.INC
MACLIB B:\mark\MAC\SYSDRV.INC
MACLIB B:\mark\MAC\MATHDRV.INC
MACLIB B:\mark\MAC\VDPDRV.INC
MACLIB B:\mark\MAC\VSYS.INC

EXTRN DEFDRV


PUBLIC INIDRV,INIDR2,BDSTAT,STPBAT,SDMA,FCLOSE,FSRCH,FDEL,FINFO,FNAME
PUBLIC CLGDRV,FXDRV
PUBLIC ROPEN,WOPEN,WOPEN2,AOPEN
PUBLIC RFILE,WFILE,RFILEV,WFILEV
PUBLIC ZWFILEV,ZRFILEV,ZOPEN,ZSET
PUBLIC RSEC,WSEC,RSECV,WSECV,SFAT,RFAT,WFAT

PUBLIC SECSIZ,CLSNUM,CLSUUN,DPBADR,FATADR,FRESIZ,DENTNM,DSCSIZ

PUBLIC FCOPY,ZFCOPY
JUNK0:  DW 0000H
JUNK1:  DW 0000H
SAV0:   DW 0000H
	DW 0000H

AJUNK0: DW 0000H
BJUNK0: DW 0000H
BJUNK1: DW 0000H
FJUNK0: DW 0000H
	DW 0000H
FJUNK1: DW 0000H
	DW 0000H
FJUNK2: DW 0000H
	DW 0000H
PPROM: DS 2
SPROM: DS 3
SDRSL: DS 1
SWARM: DS 2
ERRV: DW ERROR
SPSAV: DS 2
VESAV: DS 2
BDSTAT: DS 1
SECICL: DW 0000H;SECTER IN CLASTER
SECSIZ: DW  512;SECTOR SIZE
CLSNUM: DW 0000H;ALL CLASTER NUMBER
CLSUUN: DW 0000H;UNUSE CLASTER NUMBER
DPBADR: DW 0000H;DPB ADRESS
FATADR: DW 0000H;FAT ADRESS
FRESIZ: DW 0000H;FREE DISK AREA SIZE
        DW 0000H
DENTNM: DW  112;DIRECTRY ENTRY NUMBER
DSCSIZ: DW 0000H;DIRECTRY SECTOR SIZE

RGSAV1:DS 12
RGSAV2:DS 12

SFAT:
 LD DE,(FATADR)
 CALL SDMA
 LD IX,(DPBADR)
 LD E,(IX+8);FATHEAD SECTOR
 LD D,(IX+9)
 LD H,1
 LD A,(DEFDRV)
 DEC A
 LD L,A
 CALL WSEC
 RET

RFAT:
 CALL AFAT
 JP NZ,RFALSE
 LD A,B
 AND A
 JP NZ,RFATJ0
 LD A,(DE)
 LD L,A
 INC DE
 LD A,(DE)
 AND 00001111B
 LD H,A
 JP RTRUE
RFATJ0:
 LD A,(DE)
 LD L,A
 INC DE
 LD A,(DE)
 LD H,A
 SRL H
 RR L
 SRL H
 RR L
 SRL H
 RR L
 SRL H
 RR L
 JP RTRUE

WFAT:
 LD (AJUNK0),DE
 CALL AFAT
 JP NZ,RFALSE
 LD A,B
 AND A
 JP NZ,WFATJ0
 LD A,(AJUNK0)
 LD (DE),A
 INC DE
 LD A,(AJUNK0+1)
 AND 00001111B
 LD B,A
 LD A,(DE)
 AND 11110000B
 OR B
 LD (DE),A
 JP RTRUE
WFATJ0:
 LD BC,(AJUNK0)
 XOR A
 SRL B
 RR C
 RR A
 SRL B
 RR C
 RR A
 SRL B
 RR C
 RR A
 SRL B
 RR C
 RR A
 LD B,A
 LD A,(DE)
 AND 00001111B
 OR B
 LD (DE),A
 INC DE
 LD A,C
 LD (DE),A
 JP RTRUE

AFAT:
 LD DE,(CLSNUM)
 CALL ICOMP0
 JP Z,RFALSE
 PUSH HL
 LD DE,3
 CALL IMULT
 EX DE,HL
 LD HL,2
 CALL IMOD
 LD B,L
 LD HL,(FATADR)
 ADD HL,DE
 EX DE,HL
 POP HL
 JP RTRUE

FXDRV:
 LD A,(DEFDRV)
 LD E,A
 LD C,1BH
 CALL EBDOS
 LD (SECICL),A
 LD (SECSIZ),BC
 LD (CLSNUM),DE
 LD (CLSUUN),HL
 LD (DPBADR),IX
 LD (FATADR),IY
 XOR A
 LD (SECICL+1),A
 LD HL,(SECICL)
 LD DE,(SECSIZ)
 CALL IMULT
 EX DE,HL
 LD HL,256
 CALL IMOD
 LD HL,(CLSUUN)
 CALL IMULT
 LD DE,4
 EX DE,HL
 CALL IMOD
 LD (FRESIZ),DE
 LD HL,(DENTNM)
 LD DE,32
 CALL IMULT
 EX DE,HL
 LD HL,(SECSIZ)
 CALL IMOD
 LD (DSCSIZ),DE
 RET

RG2B1:
 LD (RGSAV1),A
 LD (RGSAV1+2),BC
 LD (RGSAV1+4),DE
 LD (RGSAV1+6),HL
 LD (RGSAV1+8),IX
 LD (RGSAV1+10),IY
 RET

B2RG1:
 LD A,(RGSAV1)
 LD BC,(RGSAV1+2)
 LD DE,(RGSAV1+4)
 LD HL,(RGSAV1+6)
 LD IX,(RGSAV1+8)
 LD IY,(RGSAV1+10)
 RET

CLGDRV:
 PUSH AF
 LD C,18H
 CALL EBDOS
 POP AF
 DEC A
 LD B,A
 LD A,L
 JP Z,CLGDJ1
CLGDJ0:
 RR A
 DJNZ CLGDJ0
CLGDJ1:
 AND 1
 JP NZ,RTRUE
 JP Z,RFALSE

EBDOS:
 JP SYSENT

; CALL RG2B1
; PUSH HL
; LD HL,(DISKVE)
; LD (VESAV),HL
; LD HL,ERRV
; LD (DISKVE),HL
; LD HL,BDSTAT
; LD (HL),0
; POP HL
; LD (SPSAV),SP
; CALL SYSENT
;GOOD:
; PUSH HL
; LD HL,(VESAV)
; LD (DISKVE),HL
; POP HL
; RET

ERROR:
; LD SP,(SPSAV)
; LD A,C
; LD (BDSTAT),A
; LD B,2
; CP 00000001B
; JP Z,ERJ0
; AND 11111110B
; LD B,3
; CP 00000010B
; JP Z,ERJ0
; LD B,4
;ERJ0:
; LD A,B
; CALL INPYN
; AND A
; JP NZ,GOOD
; CALL B2RG1
; JP EBDOS

INIDRV:
 PUSH HL
 LD HL,HPROM
 LD DE,SPROM
 LD BC,3
 LDIR

 LD A,0C3H
 LD (HPROM),A
 POP HL
 LD (HPROM+1),HL
 RET

INIDR2:
 LD HL,SPROM
 LD DE,HPROM
 LD BC,3
 LDIR
 RET

STPBAT:
 LD HL,(SYSENT+1)
 LD DE,19
 ADD HL,DE
 LD (HL),0
 RET

SYS:
 PUSH BC
 PUSH DE
 PUSH HL
 PUSH IX
 PUSH IY
 LD C,A
 CALL EBDOS
 AND A
 POP IY
 POP IX
 POP HL
 POP DE
 POP BC
 RET

FINFO:
 LD C,1BH
 CALL EBDOS
 RET

RFILE:
 PUSH BC
 PUSH DE
 PUSH IX
 PUSH IY
 LD C,27H
 CALL EBDOS
 AND A
 POP IY
 POP IX
 POP DE
 POP BC
 RET

WFILE:
 LD A,26H
 CALL SYS
 RET

RSEC:
 LD C,2FH
 CALL EBDOS
 AND A
 RET

WSEC:
 LD C,30H
 CALL EBDOS
 AND A
 RET

OPEN:
 LD A,0FH
 CALL SYS
 RET

FMAKE:
 LD A,16H
 CALL SYS
 RET

SDMA:
 LD A,1AH
 CALL SYS
 RET

FCLOSE:
 LD A,10H
 CALL SYS
 RET

FSRCH:
 LD A,11H
 CALL SYS
 RET

FDEL:
 LD A,13H
 CALL SYS
 RET

FNAME:
 LD A,17H
 CALL SYS
 RET

FFST:
 PUSH IX
 PUSH DE
 POP IX
 LD A,1
 LD (IX+14),A
 XOR A
 LD (IX+15),A
 LD (IX+33),A
 LD (IX+34),A
 LD (IX+35),A
 LD (IX+36),A 
 POP IX
 RET

ROPEN:
 CALL OPEN
 RET NZ
 CALL FFST
 XOR A
 RET

WOPEN:
WOPEN2:
 CALL FDEL
WOPJ0:
 CALL FMAKE
 CALL FFST
 XOR A
 RET

AOPEN:
 CALL FSRCH
 JP NZ,WOPJ0
 CALL OPEN
 RET NZ
 PUSH IX
 PUSH DE
 POP IX  
 LD A,1
 LD (IX+14),A
 XOR A
 LD (IX+15),A
 LD A,(IX+16)
 LD (IX+33),A
 LD A,(IX+17)
 LD (IX+34),A
 LD A,(IX+18)
 LD (IX+35),A
 LD A,(IX+19)
 LD (IX+36),A
 POP IX
 XOR A
 AND A
 RET 

ZOPEN:
 PUSH DE
 PUSH BC
 PUSH IX
 PUSH HL
 PUSH DE
 POP IX 
 LD A,1
 LD (IX+14),A
 XOR A
 LD (IX+15),A

 LD (JUNK0),HL
 LD HL,0
 LD (JUNK1),HL  
 LD L,(IX+33)
 LD H,(IX+34)
 LD (SAV0),HL
 LD HL,33
 ADD HL,DE
 PUSH HL
 POP BC
 LD DE,JUNK0
 CALL DBLSUB
 JP C,ZOPJ0
 LD A,(IX+33)
 OR (IX+34)
 OR (IX+35)
 OR (IX+36)
 JP Z,ZOPJ0
 CALL EXZO
 LD A,0FFH
 POP HL
 POP IX
 POP BC
 POP DE
 RET
ZOPJ0:
 POP HL
 LD HL,(SAV0)
 PUSH HL
 XOR A
 LD (IX+33),A
 LD (IX+34),A
 LD (IX+35),A
 LD (IX+36),A
 CALL EXZO
 XOR A
 POP HL
 POP IX
 POP BC
 POP DE
 RET

EXZO:
 LD A,(IX+33)
 LD (SAV0),A
 LD A,(IX+34)
 LD (SAV0+1),A
 LD A,(IX+35)
 LD (SAV0+2),A
 LD A,(IX+36)
 LD (SAV0+3),A 
 RET 

ZSET:
 PUSH IX
 PUSH DE
 POP IX
 LD A,(SAV0)
 LD (IX+33),A
 LD A,(SAV0+1)
 LD (IX+34),A
 LD A,(SAV0+2)
 LD (IX+35),A
 LD A,(SAV0+3)
 LD (IX+36),A
 POP IX
 RET

RFILEV:
 PUSH BC
 PUSH DE
 PUSH DE
 PUSH HL
 EX DE,HL
 LD HL,0
 LD (JUNK1),HL
 LD HL,(BUFSIZ)
 CALL IMOD
 LD B,E
 LD (JUNK0),HL
 POP HL
 POP DE
 LD A,B
 AND A
 JP Z,RFVJ1
RFVJ0:
 LD HL,(BUFSIZ)
 CALL RFILE
 JP NZ,RFVJ2
 PUSH DE
 PUSH BC
 LD HL,(BUFPTR)
 LD DE,(BUFSIZ) 
 CALL EM2V
 LD HL,(JUNK1)
 LD DE,(BUFSIZ)
 ADD HL,DE
 LD (JUNK1),HL
 POP BC
 POP DE 
 DJNZ RFVJ0
RFVJ1:
 LD HL,(JUNK0)
 LD A,H
 OR L
 JP Z,RFVJ3
 CALL RFILE
 JP NZ,RFVJ2
 LD DE,(JUNK0)
 LD HL,(BUFPTR)
 CALL EM2V
 LD HL,(JUNK1)
 LD DE,(JUNK0)
 ADD HL,DE
 LD (JUNK1),HL
RFVJ3:
 XOR A
 AND A
 LD HL,(JUNK1)
 POP DE
 POP BC
 RET 

RFVJ2:
 PUSH HL
 
 EX DE,HL
 LD HL,(BUFPTR)
 CALL EM2V
 POP DE
 LD HL,(JUNK0)
 ADD HL,DE
 LD A,0FFH
 AND A
 POP DE
 POP BC
 RET   
  
WFILEV:
 PUSH BC
 PUSH DE
 PUSH HL
 PUSH DE
 PUSH HL
 EX DE,HL
 LD HL,(BUFSIZ)
 CALL IMOD
 LD B,E
 LD (JUNK0),HL
 POP HL
 POP DE
 LD A,B
 AND A
 JP Z,WFVJ1
WFVJ0:
 PUSH DE
 PUSH BC
 LD HL,(BUFPTR)
 LD DE,(BUFSIZ)
 CALL EV2M
 POP BC
 POP DE
 LD HL,(BUFSIZ)
 CALL WFILE
 JP NZ,0000H;hWFVJ2
 DJNZ WFVJ0
WFVJ1:
 LD HL,(JUNK0)
 LD A,H
 OR L
 JP Z,WFVJ3
 PUSH BC
 PUSH DE
 LD HL,(BUFPTR)
 LD DE,(JUNK0)
 CALL EV2M
 POP DE
 POP BC
 LD HL,(JUNK0)
 CALL WFILE
 JP NZ,WFVJ2
WFVJ3:
 XOR A
 AND A
 POP HL
 POP DE
 POP BC
 RET 
WFVJ2:
 LD A,0FFH
 AND A
 POP HL
 POP DE
 POP BC
 RET   

ZRFILEV:
 PUSH BC
 PUSH DE
 PUSH DE
 PUSH HL
 EX DE,HL
 LD HL,0
 LD (JUNK1),HL
 LD HL,(BUFSIZ)
 CALL IMOD
 LD B,E
 LD (JUNK0),HL
 POP HL
 POP DE
 LD A,B
 AND A
 JP Z,ZRFVJ1
ZRFVJ0:
 LD HL,(BUFSIZ)
 CALL RFILE
 JP NZ,ZRFVJ2
 PUSH DE
 PUSH BC
 LD HL,(BUFPTR)
 LD DE,(BUFSIZ)
 ADD HL,DE
 DEC HL 
 CALL EZM2V
 LD HL,(JUNK1)
 LD DE,(BUFSIZ)
 ADD HL,DE
 LD (JUNK1),HL
 POP BC
 POP DE 
 DJNZ ZRFVJ0
ZRFVJ1:
 LD HL,(JUNK0)
 LD A,H
 OR L
 JP Z,ZRFVJ3
 CALL RFILE
 JP NZ,ZRFVJ2
 LD HL,(BUFPTR)
 LD DE,(JUNK0)
 ADD HL,DE
 DEC HL
 CALL EZM2V
 LD HL,(JUNK1)
 LD DE,(JUNK0)
 ADD HL,DE
 LD (JUNK1),HL
ZRFVJ3:
 XOR A
 AND A
 LD HL,(JUNK1)
 POP DE
 POP BC
 RET 

ZRFVJ2:
 PUSH HL
 EX DE,HL
 LD HL,(BUFPTR)
 ADD HL,DE
 DEC HL
 CALL EZM2V
 POP DE
 LD HL,(JUNK0)
 ADD HL,DE
 LD A,0FFH
 AND A
 POP DE
 POP BC
 RET   
  
ZWFILEV:
 PUSH BC
 PUSH DE
 PUSH HL
 PUSH DE
 PUSH HL
 EX DE,HL
 LD HL,(BUFSIZ)
 CALL IMOD
 LD B,E
 LD (JUNK0),HL
 POP HL
 POP DE
 LD A,B
 AND A
 JP Z,ZWFVJ1
ZWFVJ0:
 PUSH DE
 PUSH BC
 LD HL,(BUFPTR)
 LD DE,(BUFSIZ)
 ADD HL,DE
 DEC HL
 CALL EZV2M
 POP BC
 POP DE
 LD HL,(BUFSIZ)
 CALL WFILE
 JP NZ,ZWFVJ2
 DJNZ ZWFVJ0
ZWFVJ1:
 LD HL,(JUNK0)
 LD A,H
 OR L
 JP Z,ZWFVJ3
 PUSH BC
 PUSH DE
 LD HL,(BUFPTR)
 LD DE,(JUNK0)
 ADD HL,DE
 DEC HL
 CALL EZV2M
 POP DE
 POP BC
 LD HL,(JUNK0)
 CALL WFILE
 JP NZ,ZWFVJ2
ZWFVJ3:
 XOR A
 AND A
 POP HL
 POP DE
 POP BC
 RET 
ZWFVJ2:
 LD A,0FFH
 AND A
 POP HL
 POP DE
 POP BC
 RET   

RSECV:
 PUSH BC
 PUSH DE
 PUSH HL
 PUSH DE
 PUSH HL
 LD DE,(BUFSIZ)
 LD HL,(SECSIZ)
 CALL IMOD
 LD (JUNK0),DE
 EX DE,HL
 LD HL,(BUFSIZ)
 AND A
 SBC HL,DE
 LD (JUNK1),HL
 POP HL
 POP DE
RSCJ0:
 LD A,H
 LD BC,(JUNK0)
 CP C     
 JP C,RSCJ1
 PUSH HL
 PUSH DE
 LD H,C
 CALL RSEC
 LD DE,(JUNK1)
 LD HL,(BUFPTR)
 CALL EM2V
 POP DE
 LD HL,(JUNK0)
 ADD HL,DE
 EX DE,HL
 POP HL
 LD A,(JUNK0)
 LD B,A
 LD A,H
 SUB B
 LD H,A
 JP RSCJ0
RSCJ1:
 AND A
 JP Z,RSCJ2
 CALL RSEC
 LD L,H
 LD H,0
 LD DE,(SECSIZ)
 CALL IMULT
 EX DE,HL
 LD HL,(BUFPTR)
 CALL EM2V
RSCJ2:
 POP HL
 POP DE
 POP BC
 RET

WSECV:
 PUSH BC
 PUSH DE
 PUSH HL
 PUSH DE
 PUSH HL
 LD DE,(BUFSIZ)
 LD HL,(SECSIZ)
 CALL IMOD
 LD (JUNK0),DE
 EX DE,HL
 LD HL,(BUFSIZ)
 AND A
 SBC HL,DE
 LD (JUNK1),HL
 POP HL
 POP DE
 POP BC
WSCJ0:
 LD A,H
 LD BC,(JUNK0)
 CP C     
 JP C,WSCJ1
 PUSH HL
 PUSH HL
 PUSH DE
 LD DE,(JUNK1)
 LD HL,(BUFPTR)
 CALL EV2M
 POP DE
 POP HL
 LD A,(JUNK0)
 LD H,A
 CALL WSEC
 LD HL,(JUNK0)
 ADD HL,DE
 EX DE,HL
 POP HL
 LD A,(JUNK0)
 LD B,A
 LD A,H
 SUB B
 LD H,A
 JP WSCJ0
WSCJ1:
 AND A
 JP Z,WSCJ2
 PUSH HL
 PUSH DE
 LD L,H
 LD H,0
 LD DE,(SECSIZ)
 CALL IMULT
 EX DE,HL
 LD HL,(BUFPTR)
 CALL EV2M
 POP DE
 POP HL
 CALL WSEC
WSCJ2:
 POP HL
 POP DE
 POP BC
 RET



FCOPY:
 CALL COPYJUNK
FCPJ0:
 PUSH DE
 PUSH HL
 PUSH DE
 PUSH HL
 LD HL,(FJUNK0)
 LD DE,(FJUNK0+2)
 LD (FJUNK2),HL
 LD (FJUNK2+2),DE
 LD HL,FJUNK0
 LD DE,FJUNK1
 LD BC,FJUNK0
 CALL DBLSUB
 JP C,FCPJ1

 POP DE
 LD HL,BUFSIZ
 CALL RFILE
 POP DE
 LD HL,BUFSIZ
 CALL WFILE
 POP HL
 POP DE
 JP FCPJ0
FCPJ1:
 LD HL,(FJUNK2)
 LD DE,(FJUNK2+2)
 LD A,L
 OR H
 OR E
 OR D
 JP Z,FCPJ2

 POP DE
 LD HL,(FJUNK2)
 CALL RFILE
 POP DE
 LD HL,(FJUNK2)
 CALL WFILE
 POP HL
 POP DE
 RET
FCPJ2:
 POP HL
 POP HL
 POP HL
 POP DE
 RET

ZFCOPY:
 CALL COPYJUNK
ZFCPJ0:
 PUSH DE
 PUSH HL
 PUSH DE
 PUSH HL
 LD HL,(FJUNK0)
 LD DE,(FJUNK0+2)
 LD (FJUNK2),HL
 LD (FJUNK2+2),DE

 LD HL,FJUNK0
 LD DE,FJUNK1
 LD BC,FJUNK0
 CALL DBLSUB
 JP C,ZFCPJ1
 POP DE
 LD HL,BUFSIZ
 CALL ZOPEN
 CALL RFILE
 CALL ZSET
 LD HL,BUFSIZ
 CALL DTAZ
 POP DE
 LD HL,BUFSIZ
 CALL WFILE
 POP HL
 POP DE
 JP ZFCPJ0
ZFCPJ1:
 LD HL,(FJUNK2)
 LD DE,(FJUNK2+2)
 LD A,L
 OR H
 OR E
 OR D
 JP Z,ZFCPJ2
 POP DE
 LD HL,(FJUNK2)
 CALL ZOPEN
 CALL RFILE
 CALL ZSET
 LD HL,(FJUNK2)
 CALL DTAZ
 POP DE
 LD HL,(FJUNK2)
 CALL WFILE
 POP HL
 POP DE
 RET
ZFCPJ2:
 POP HL
 POP HL
 POP HL
 POP DE
 RET

DTAZ:
 PUSH IX
 EX DE,HL
 LD HL,(BUFPTR)
 ADD HL,DE
 DEC HL
 PUSH HL
 LD HL,2
 CALL IMOD
 POP HL
 LD IX,(BUFPTR)
DZJ0:
 LD B,(IX)
 LD A,(HL)
 LD (IX),A
 LD (HL),B
 INC IX
 DEC HL
 DEC DE
 LD A,D
 OR E
 JP NZ,DZJ0
 POP IX
 RET

COPYJUNK:
 PUSH  HL
 LD A,(BC)
 LD (FJUNK0),A
 INC BC
 LD A,(BC)
 LD (FJUNK0+1),A
 INC BC
 LD A,(BC)
 LD (FJUNK0+2),A
 INC BC
 LD A,(BC)
 LD (FJUNK0+3),A
 LD HL,BUFSIZ
 LD (FJUNK1),HL
 LD  HL,0
 LD (FJUNK1+2),HL
 POP HL
 RET


 END
