1 '      save"mapedit.bas
2 '      key2,"screen0"+chr$(13)
4 DEFINTA-Z
5 COLOR 15,1,1
6 '
7 '	JP: choix de fichier 
8 '
9 GOSUB50000
10 '
11 '	Initialisation
12 '
13 SCREEN5,0:OPEN"grp:"FOROUTPUTAS#1
14 '
15 '	Realisation sprites
16 '
17 FORJ=0TO3:RESTORE60000:SETPAGE0,J:CLS:FORI=&H7800TO&H7827:READA:VPOKEI,A:NEXT:NEXT
20 '
22 '	Chargement ecran "tiles"
23 '
24 SETPAGE2,2:BLOADB$,S
25 COLOR=RESTORE
26 SET PAGE 0,0:'COPY(0,0)-(256,16*(13-YA)-1),2TO(0,0)
47 '
48 '	JP: SI pas un nouveau fichier
49 '
50 IFD=0THEN600
51 '
52 ' Choix couleurs fond + grille + sprites
53 '
54 '
55 CF=0:CL=0:CS=8:PRESET(0,22):PRINT#1,"Fond Line Sprite"
56 S=STICK(0):X=X-1*(S=3ANDX<2)+1*(S=7ANDX>0):PUTSPRITE0,(X*40+12,44),CS,4:ONX+1GOSUB57,59,61:IFSTRIG(0)=0THEN56ELSE65
57 CF=CF+1*(S=5ANDCF>0)-1*(S=1ANDCF<15):PUTSPRITE1,(8,4),CF,4:RETURN:'colorcf:return
59 CL=CL+1*(S=5ANDCL>0)-1*(S=1ANDCL<15):PUTSPRITE2,(48,4),CL,1:RETURN
61 CS=CS+1*(S=5ANDCS>0)-1*(S=1ANDCS<15):PUTSPRITE3,(88,4),CS,2:RETURN
65 CLS:COLORCL,CF
66 'GOTO600
67 '
68 '	Definition taille tableau
69 '
70 SETPAGE3,3:X=1:Y=1
80 FORI=0TO256STEP16:C=CL+1*(IMOD64=0):LINE(I,0)-(I,192),C:NEXT
90 FORI=0TO192STEP16:C=CL+(IMOD64=0):LINE(0,I)-(255,I),C:NEXT
100 PRESET(0,200):PRINT#1,"taille du tableau:"
110 S=STICK(0)
120 X=X+1*(X>1ANDS=7)-1*(X<16ANDS=3)
130 Y=Y+1*(Y>1ANDS=1)-1*(Y<16ANDS=5)
135 H=-1*(S>0)
140 PUTSPRITE0,(0,-1),CS,0
150 PUTSPRITE1,(0,Y*16-8),CS,1
160 PUTSPRITE2,(X*16-8,-1),CS,3
170 PUTSPRITE3,(X*16-8,Y*16-8),CS,2
180 IFSTRIG(0) THEN200
190 GOTO 110
200 PRESET(160,200):PRINT#1,X;" x ";Y
210 IFX=XAANDY=YA THEN300
220 XA=X:YA=Y
250 GOTO 110
255 '
256 '	Definition taille "carte"
257 '
300 CLS:X=1:Y=1
310 FORI=0TO256STEPXA:C=15+5*(IMOD(XA*4)=0):LINE(I,0)-(I,192),C:NEXT
320 FORI=0TO192STEPYA:C=15+5*(IMOD(YA*4)=0):LINE(0,I)-(255,I),C:NEXT
330 PRESET(0,200):PRINT#1,"taille de la carte:"
340 S=STICK(0)
350 X=X+1*(X>1ANDS=7)-1*(X<INT(256/XA)ANDS=3)
360 Y=Y+1*(Y>1ANDS=1)-1*(Y<INT(192/YA)ANDS=5)
370 PUTSPRITE0,(0,-1),CS,0
380 PUTSPRITE1,(0,Y*YA-8),CS,1
390 PUTSPRITE2,(X*XA-8,-1),CS,3
400 PUTSPRITE3,(X*XA-8,Y*YA-8),CS,2
420 IFSTRIG(0) THEN450
430 GOTO 340
450 PRESET(160,200):PRINT#1,X;" x ";Y
460 IFX=XBANDY=YB THEN500
470 XB=X:YB=Y
480 GOTO 340
490 '
492 '	Sauvegarde fichier "carte"
494 '
500 LINE(XB*XA+1,0)-(256,192),1,BF
510 LINE(0,YB*YA+1)-(256,192),1,BF
515 PRESET(0,200):PRINT#1,"Modification Niveau:         "
520 BSAVEC$+".MAP",0,&H6A00,S
522 '
523 '	Sauvegarde fichier "donnees carte"
524 '
530 SETPAGE1,1:LINE(0,0)-(255,192),0,BF
540 VPOKE0,XA:VPOKE1,YA:VPOKE2,XB:VPOKE3,YB:VPOKE4,CF:VPOKE5,CL:VPOKE6,CS
550 BSAVEC$+".VPK",0,&H7FFF,S
590 GOTO 675
595 '
596 '	Chargement ecran "donnees carte" 
597 '
600 SETPAGE1,1:BLOADC$+".vpk",S
620 XA=VPEEK(0):YA=VPEEK(1):XB=VPEEK(2):YB=VPEEK(3):CF=VPEEK(4):CL=VPEEK(5):CS=VPEEK(6)
630 'VPOKE4,8:VPOKE5,15:VPOKE6,3
670 'GOTO670
675 '
676 '	Chargement ecran "carte" 
677 '
678 COPY(0,0)-(256,16*(13-YA)-1),2TO(0,0),0
680 SETPAGE3,3:BLOADC$+".map",S
690 '
692 '
695 '	Debut programme principal
697 '
700 B=4:W=0:Z=0:X=0:Y=0:E=1:F=2:Y2=0:Y1=13-YA
782 '
785 '	Boucle programme principal
787 '
792 '	
795 '	Choix du mode
797 '
800 OUT&HAA,0:U=INP(&HA9)
810 A=-1*(U=253)-2*(U=251)-3*(U=247)-4*(U=239)-5*(U=127)
815 IFA<1ORA>4THEN830
820 SETPAGE-(A-1)*(A>2),-(A-1)*(A>2):IFE=2THENSWAPE,F
830 B=-B*(A=0)-A*(A>0)
840 S=STICK(0):G=STRIG(0)
850 H=-H*(G<0)
855 '
857 '	Saut suivant mode 
859 '
860 ONB GOTO 1000,1000,2000,2500,3000,1230,1430,3610
990 GOTO 800
995 '
997 '	MODE: realisation tableau: choix "tiles" 
998 ' 
1000 OUT&HAA,5:U=INP(&HA9):IFU=254THEN1400ELSEIFU=253THEN1200ELSEIFU=251THEN3500
1002 ON BGOTO1005,1500
1005 ONEGOTO1010,1110
1010 X=X-1*(S>1ANDS<5ANDX<15)+1*(S<9ANDS>5ANDX>0)
1020 Y=Y-1*(S>3ANDS<7ANDY<Y1-1)-1*(S=1ORS=2ORS=8)*(Y>0)
1050 PUTSPRITE0,(X*16,Y*16-1),CS,0
1060 IFG=-1ANDH=0THENSWAPE,F:XC=X+1:YC=Y+1:H=1
1090 GOTO 800
1110 XC=XC-1*(S>1ANDS<5ANDXC<16)+1*(S<9ANDS>5ANDXC>X+1)
1120 YC=YC-1*(S>3ANDS<7ANDYC<Y1)-1*(S=1ORS=2ORS=8)*(YC>Y+1)
1150 PUTSPRITE1,(XC*16-7,YC*16-9),CS,2
1160 IFG=-1ANDH=0THEN SWAPE,F:B=2:H=1:DX=XC-X:DY=YC-Y
1190 GOTO 800
1195 '
1197 '  MODE: realisation tableau: mise en place Texte
1198 '
1200 COPY(0,192)-(256,209),2TO(0,0),0
1210 LINE(0,17)-(256,31),0,BF
1215 IFVPEEK(VN+186)>0THEN1360
1220 GOSUB1370:B=6:XT=0
1230 T$="":T=0:XT=XT-1*(S=3ANDXT<36)+1*(S=7ANDXT>0):IFXT=36THENGOSUB1370
1240 T$=INKEY$:IFT$=""THEN1300ELSET=ASC(T$):IFT>31ANDT<91THENT=T-32+14*(T>47)+6*(T>64)-22*(T=63)-33*(T=39)-29*(T=44)-28*(T=46):T=T*6ELSE1300
1260 COPY(T,9)-(T+5,17)TO(XT*7,20)
1270 VPOKE(VN+XT+186),T:XT=XT+1
1300 PUTSPRITE0,(XT*7,19),CS,0
1310 OUT&HAA,7:U=INP(&HA9):IFU=127THENA=1:GOTO2320ELSEIFU=223THEN1350
1320 GOTO 840
1350 FORXT=0TO35:VPOKE VN+XT+186,0:NEXT:LINE(0,16)-(256,31),0,BF:GOTO 840
1360 FORXT=0TO35:T=VPEEK(VN+XT+186):COPY(T,9)-(T+5,17)TO(XT*7,20):NEXT:GOTO1220
1370 T$=INKEY$:IFT$<>""THEN1220ELSE RETURN
1395 '
1397 '	MODE: realisation tableau: sprite 
1398 '
1400 COPY(0,176)-(256,191),2TO(0,0),0
1410 LINE(0,16)-(256,31),0,BF
1420 B=7:E=1:F=2
1430 OUT&HAA,5:U=INP(&HA9):IFU=254THENA=1:GOTO2320
1435 ONEGOTO1440,1480
1436 '
1437 '	MODE: realisation tableau: choix sprite 
1438 '
1440 X=X-1*(S>1ANDS<5ANDX<15)+1*(S<9ANDS>5ANDX>0)
1450 PUTSPRITE0,(X*16,-1),CS,0:PUTSPRITE1,(X*16+7,8),CS,2
1460 IFG=-1ANDH=0THENSWAPE,F:H=1
1470 GOTO840
1476 '
1477 '	MODE: realisation tableau: choix place sprite 
1478 '
1480 XD=XD-1*(S>1ANDS<5ANDXD<XA-DX-1)+1*(S<9ANDS>5ANDXD>0)
1485 YD=YD-1*(S>3ANDS<7ANDYD<(YA-DY-1))-1*(S=1ORS=2ORS=8)*(YD>0)
1490 PUTSPRITE2,(XD*16,(YD+Y1)*16-1),CS,0:PUTSPRITE3,(XD*16+7,(YD+Y1)*16+7),CS,2
1495 IFG=-1ANDH=0THENSWAPE,F:H=1:GOSUB1800
1496 GOTO 840
1497 '
1498 '	MODE: realisation tableau: choix place 'tiles' 
1499 '
1500 ONEGOTO1510,1610
1510 XD=XD-1*(S>1ANDS<5ANDXD<XA-DX)+1*(S<9ANDS>5ANDXD>0)
1520 YD=YD-1*(S>3ANDS<7ANDYD<(YA-DY))-1*(S=1ORS=2ORS=8)*(YD>0)
1550 PUTSPRITE2,(XD*16,(YD+Y1)*16-1),CS,0
1560 IFG=-1ANDH=0THENSWAPE,F:XE=XD+DX:YE=YD+DY:H=1
1590 GOTO 800
1610 XE=XE-DX*(S>1ANDS<5AND(XE+DX)<17)+DX*(S<9ANDS>5ANDXE>(XD+DX))
1620 YE=YE-DY*(S>3ANDS<7ANDYE<(12-DY))-DY*(S=1ORS=2ORS=8)*(YE>(YD+DY))
1650 PUTSPRITE3,(XE*16-8,(YE+Y1)*16-9),CS,2
1660 IFG=-1ANDH=0THEN SWAPE,F:H=1:GOSUB1700
1690 GOTO 800
1700 '
1705 '	MODE: realisation tableau: mise en place 'tiles'
1707 '
1710 FORI=X+1TOXC:FORJ=Y+1TOYC:FORK=XD+1TOXESTEPDX:FORL=YD+1TOYESTEPDY
1720 COPY((I-1)*16,(J-1+Y2)*16)-((I)*16-1,(J+Y2)*16-1),2TO((K-2+I-X)*16,(L+J-Y+Y1-2)*16),0
1730 VA=VN+I+K-X-2+(L+J-Y-2)*16
1740 VP=I-1+(J-1+Y2)*16
1750 VPOKE VA,VP
1770 NEXT:NEXT:NEXT:NEXT
1790 RETURN
1800 '
1805 '	MODE: realisation tableau: mise en place sprite
1807 '
1810 IFVPEEK(VN+178)=0THEN1900
1820 I=VPEEK(VN+179):J=VPEEK(VN+180):VA=VPEEK(VN+I+(J-Y1)*16):VY=(INT(VA/16))*16:VX=(VAMOD16)*16:COPY(VX,VY)-(15+VX,15+VY),2TO(I*16,J*16),0
1900 COPY(X*16,0)-(X*16+15,15)TO(XD*16,(YD+Y1)*16),0,TPSET:VPOKE(VN+178),X:VPOKE(VN+179),XD:VPOKE(VN+180),(YD+Y1)
1990 RETURN
1996 '
1997 '  MODE: choix bloc "tiles"
1998 '
2000 Y2=Y2-1*(S>3ANDS<7ANDY2<YA)-1*(S=1ORS=2ORS=8)*(Y2>0)
2010 PUTSPRITE0,(0,Y2*16),CS,0
2020 PUTSPRITE1,(248,(Y2+Y1)*16-8),CS,2
2100 IFG=-1ANDH=0THEN H=1:A=1:GOTO 2320
2190 GOTO 840
2300 '
2320 COPY(0,Y2*16)-(255,(Y2+Y1)*16-1),2TO(0,0),0
2490 GOTO 820
2500 '
2505 '	MODE: choix niveau 
2507 '
2510 W=W-1*(S>1ANDS<5ANDW<(XB-1))+1*(S<9ANDS>5ANDW>0)
2520 Z=Z-1*(S>3ANDS<7ANDZ<(YB-1))-1*(S=1ORS=2ORS=8)*(Z>0)
2550 PUTSPRITE0,(W*XA,Z*YA),CS,0
2552 PUTSPRITE1,(W*XA,Z*YA+YA-8),CS,1
2554 PUTSPRITE2,(W*XA+XA-8,Z*YA+YA-8),CS,2
2556 PUTSPRITE3,(W*XA+XA-8,Z*YA),CS,3
2560 N=W+Z*XB:VN=&H8300+N*256
2570 PRESET(160,200):PRINT#1,N
2580 IFG=-1THENB=1:GOTO 2650
2590 OUT&HAA,3:U=INP(&HA9):IFU=254THEN2800ELSEIFU=127THEN2810
2600 GOTO 840
2650 '
2660 '	MISE EN PLACE TABLEAU
2670 '
2690 SETPAGE0,0
2695 CALL TURBO ON(XA,Y1,YA,N,VN)
2700 FORI=0TOXA-1:FORJ=Y1TO12
2710 VA=VPEEK(VN+I+(J-Y1)*16)
2730 VY=(INT(VA/16))*16
2740 VX=(VAMOD16)*16
2750 COPY(VX,VY)-(15+VX,15+VY),2TO(I*16,J*16),0
2760 NEXTJ:NEXTI
2770 CALL TURBO OFF
2780 IFVPEEK(VN+178)=0THEN2788
2783 I=VPEEK(VN+179):J=VPEEK(VN+180):VA=VPEEK(VN+178)
2787 COPY(VA*16,176)-(VA*16+15,191),2TO(I*16,J*16),0,TPSET
2788 GOTO 800
2790 '
2793 '	MODE: choix niveau: copier tableau 
2796 '
2800 VC=VN:PUTSPRITE4,(W*XA,Z*YA),CS,4:GOTO800
2802 '
2805 '	MODE: choix niveau: coller tableau 
2807 '
2810 SETPAGE0,0
2812 CALL TURBO ON(XA,YA,Y1,VC,VN)
2815 FORI=0TOXA-1:FORJ=0TOYA
2820 VA=VPEEK(VC+I+J*16)
2830 VP=VN+I+J*16
2850 VPOKE VP,VA
2870 NEXTJ:NEXTI
2880 CALL TURBO OFF
2890 SETPAGE3,3:GOTO800
3000 '
3005 '	Sauvegarde fichiers
3007 '
3010 SETPAGE1,1:BSAVEC$+".vpk",0,&H8000,S
3030 SETPAGE3,3:BSAVEC$+".map",0,&H6A00,S
3040 SET PAGE0,0:B=1:A=1
3090 GOTO 800
3200 '
3205 '	? ? ? ? ? ?
3207 '
3500 'VA=VPEEK(VN+213):X=3:B=8
3510 'LINE(0,0)-(256,31),0,BF:COPY(48,160)-(159,175),2TO(48,0),0
3520 'IFVA=0THEN3610
3530 'VX=(VAMOD16)*16:COPY(VX,160)-(15+VX,175),2TO(0,16),0
3610 'X=X-1*(S>1ANDS<5ANDX<10)+1*(S<9ANDS>5ANDX>2)
3620 'PUTSPRITE0,(X*16,-1),cs,0:PUTSPRITE1,(X*16+7,8),cs,2
3640 'IFG=-1ANDH=0THENVPOKE(VN+213),X+160:COPY(X*16,160)-(15+X*16,175),2TO(0,16),0:H=1
3650 'OUT&HAA,5:U=INP(&HA9):IFU=251THENA=1:GOTO2320
3690 'GOTO 800
5000 GOTO 5000
49000 '
49010 '	Choix de fichier: blocs decor
49020 '
50000 SCREEN1,2:WIDTH32:KEYOFF:VPOKE&H3800,255:VPOKE&H3810,255
50005 ON ERROR GOTO 55000
50010 GOSUB51000
50020 PRINT"blocs decor"
50030 LOCATE0,4:FILES"*.sc5"
50035 B$="":D=0
50040 GOSUB52000
50043 '
50045 '	Choix de fichier: tableaux
50047 '
50050 B$=A$
50070 A$=""
50100 CLS
50110 GOSUB51000
50120 PRINT"tableaux"
50160 LOCATE0,4:FILES"*.MAP"
50170 GOSUB52000
50190 C$=LEFT$(A$,8):RETURN
50195 RETURN
50196 '
50197 '	Choix de fichier: creation nouveau fichier 
50198 '
50200 LOCATE0,19:PRINT"                          ";
50210 LOCATE0,19:INPUT"Nom du fichier";A$
50220 IFA$=""THEN50210
50230 C$=LEFT$(A$,8):D=1
50500 RETURN
50900 '
50910 '	Choix de fichier: mise en place tableaux choix de fichier
50920 '
51000 PRINT:PRINT"Fichiers":PRINT"-----------------------------"
51010 LOCATE0,18:PRINT"-----------------------------":PRINT"Choix du fichier ";
51030 RETURN
51900 '
51910 '	Choix de fichier: selection fichier 
51920 '
52000 X=0:Y=32
52010 S=STICK(0)
52020 X=X-13*(S=3ANDX=0ANDH=0)+13*(S=7ANDX=13ANDH=0)
52030 Y=Y-8*(S=5ANDY<120ANDH=0)+8*(S=1ANDY>32ANDH=0)
52040 H=-1*(S>0)
52080 PUT SPRITE0,(X*8,Y+6),8,0
52090 IFSTRIG(O)=-1THEN53000
52100 GOTO52010
53000 A=VPEEK(&H1800+X+Y*4):IFA=32THEN IFB$=""THENBEEP:GOTO52010ELSE50200
53004 '
53006 '	Choix de fichier: reconnaissance selection fichier 
53008 '
53010 A$=""
53020 FORI=0TO7:A=VPEEK(&H1800+X+Y*4+I)
53030 A$=A$+CHR$(A)
53040 NEXTI
53050 A$=A$+".SC5"
53500 LOCATE10,20:PRINTA$
54000 RETURN
55000 IFERR=53THENRESUME50200
55050 ONERRORGOTO0:RESUME10
59900 '
59910 '	DATAS Sprites
59920 '
60000 DATA255,128,128,128,128,128,128,128
60010 DATA128,128,128,128,128,128,128,255
60020 DATA1,1,1,1,1,1,1,255
60030 DATA255,1,1,1,1,1,1,1
60040 DATA0,24,60,126,126,60,24,0
