ZED40:			;ED 40        IN B,(C)

	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	EXX
	LD B,A
	EXX
	JP NEXTOP

ZED41:			;ED 41        OUT (C),B

	EXX
	LD A,C
	EXX
	LD C,A
	EXX
	LD A,B
	EXX
	CALL OUTPUT
	JP NEXTOP

ZED42:			;ED 42        SBC HL,BC

	EX AF,AF'
	EXX
	SBC HL,BC
	EXX
	EX AF,AF'
	JP NEXTOP


ZED43:			;ED 43 nn nn  LD (nn),BC

	CALL GETA
	LD E,A
	INC HL; MI
	CALL GETA
	LD D,A
	INC HL; MI
	EX DE,HL
	EXX
	LD A,C
	EXX
	CALL PUTA
	INC HL
	EXX
	LD A,B
	EXX
	CALL PUTA
	EX DE,HL
	JP NEXTOP
ZED44:			;ED 44        NEG

	EX AF,AF'
	NEG
	EX AF,AF'
	JP NEXTOP
ZED45:			;ED 45        RETN

	LD HL,(PILKSP)
	CALL GETA
	INC HL
	LD E,A
	CALL GETA
	INC HL
	LD D,A
	LD (PILKSP),HL
	EX DE,HL
	JP NEXTOP

ZED46:			;ED 46        IM 0
	XOR A
	LD (IMODE),A
	JP NEXTOP
ZED47:			;ED 47        LD I,A

	EX AF,AF'
	LD (PILKI),A
	EX AF,AF'
	JP NEXTOP
ZED48:			;ED 48        IN C,(C)


	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	EXX
	LD C,A
	EXX
	JP NEXTOP
ZED49:			;ED 49        OUT (C),C

	EXX
	LD A,C
	EXX
	LD C,A
	CALL OUTPUT
	JP NEXTOP
ZED4A:			;ED 4A        ADC HL,BC

	EX AF,AF'
	EXX
	ADC HL,BC
	EXX
	EX AF,AF'
	JP NEXTOP
ZED4B:			;ED 4B nn nn  LD BC,(nn)

	CALL GETA
	LD E,A
	INC HL ;MI
	CALL GETA
	LD D,A
	INC HL ;MI
	EX DE,HL
	CALL GETA
	EXX
	LD C,A
	EXX
	INC HL
	CALL GETA
	EXX
	LD B,A
	EXX
	EX DE,HL
	JP NEXTOP

ZED4D:			;ED 4D        RETI

	LD HL,(PILKSP)
	CALL GETA
	INC HL
	LD E,A
	CALL GETA
	INC HL
	LD D,A
	LD (PILKSP),HL
	EX DE,HL
	JP NEXTOP

ZED4F:			;ED 4F        LD R,A

	EX AF,AF'
	LD R,A
	EX AF,AF'
	JP NEXTOP
ZED50:			;ED 50        IN D,(C)


	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	EXX
	LD D,A
	EXX
	JP NEXTOP
ZED51:			;ED 51        OUT (C),D

	EXX
	LD A,C
	EXX
	LD C,A
	EXX
	LD A,D
	EXX
	CALL OUTPUT
	JP NEXTOP
ZED52:			;ED 52        SBC HL,DE

	EX AF,AF'
	EXX
	SBC HL,DE
	EXX
	EX AF,AF'
	JP NEXTOP


ZED53:			;ED 53 nn nn  LD (nn),DE

	CALL GETA
	LD E,A
	INC HL; MI
	CALL GETA
	LD D,A
	INC HL; MI
	EX DE,HL
	EXX
	LD A,E
	EXX
	CALL PUTA
	INC HL
	EXX
	LD A,D
	EXX
	CALL PUTA
	EX DE,HL
	JP NEXTOP

ZED56:			;ED 56        IM 1
	LD A,1
	LD (IMODE),A

	JP NEXTOP
ZED57:			;ED 57        LD A,I

	EX AF,AF'
	LD A,(PILKI)
	EX AF,AF'
	JP NEXTOP
ZED58:			;ED 58        IN E,(C)


	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	EXX
	LD E,A
	EXX
	JP NEXTOP
ZED59:			;ED 59        OUT (C),E

	EXX
	LD A,C
	EXX
	LD C,A
	EXX
	LD A,E
	EXX
	CALL OUTPUT
	JP NEXTOP
ZED5A:			;ED 5A        ADC HL,DE

	EX AF,AF'
	EXX
	ADC HL,DE
	EXX
	EX AF,AF'
	JP NEXTOP
ZED5B:			;ED 5B nn nn  LD DE,(nn)
	CALL GETA
	LD E,A
	INC HL ;MI
	CALL GETA
	LD D,A
	INC HL ;MI
	EX DE,HL
	CALL GETA
	EXX
	LD E,A
	EXX
	INC HL
	CALL GETA
	EXX
	LD D,A
	EXX
	EX DE,HL
	JP NEXTOP

ZED5E:			;ED 5E        IM 2
	LD A,2
	LD (IMODE),A

	JP NEXTOP
ZED5F:			;ED 5F        LD A,R

	EX AF,AF'
	LD A,R
	EX AF,AF'
	JP NEXTOP
ZED60:			;ED 60        IN H,(C)


	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	EXX
	LD H,A
	EXX
	JP NEXTOP

ZED61:			;ED 61        OUT (C),H

	EXX
	LD A,C
	EXX
	LD C,A
	EXX
	LD A,H
	EXX
	CALL OUTPUT
	JP NEXTOP
ZED62:			;ED 62        SBC HL,HL

	EX AF,AF'
	EXX
	SBC HL,HL
	EXX
	EX AF,AF'
	JP NEXTOP
;ZED63:			;ED 63 nn nn  LD (nn),HL



ZED67:			;ED 67        RRD

	EX AF,AF'
	EXX
	RRD
	EXX
	EX AF,AF'
	JP NEXTOP
ZED68:			;ED 68        IN L,(C)

	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	EXX
	LD L,A
	EXX
	JP NEXTOP
ZED69:			;ED 69        OUT (C),L

	EXX
	LD A,C
	EXX
	LD C,A
	EXX
	LD A,L
	EXX
	CALL OUTPUT
	JP NEXTOP
ZED6A:			;ED 6A        ADC HL,HL

	EX AF,AF'
	EXX
	ADC HL,HL
	EXX
	EX AF,AF'
	JP NEXTOP

;ZED6B:			;ED 6B nn nn  LD HL,(nn)

ZED6F:			;ED 6F        RLD

	EX AF,AF'
	EXX
	RLD
	EXX
	EX AF,AF'
	JP NEXTOP
ZED70:			;ED 70        IN F,(C)

	EXX
	LD A,C
	EXX
	LD C,A
	JP INPUT
	LD E,A
	EX AF,AF'
	LD D,A

	PUSH DE
	POP AF
	EX AF,AF'
	JP NEXTOP

ZED71:			;ED 71        OUT (C),0 *
	EXX
	LD A,C
	EXX
	LD C,A
	XOR A
	CALL OUTPUT
	JP NEXTOP
ZED72:			;ED 72        SBC HL,SP

	EXX
	PUSH DE
	LD DE,(PILKSP)
	EX AF,AF'
	SBC HL,DE
	EX AF,AF'
	POP DE
	EXX
	JP NEXTOP

ZED73:			;ED 73 nn nn  LD (nn),SP

	CALL GETA
	LD E,A
	INC HL ;MI
	CALL GETA
	LD D,A
	INC HL ;MI
	PUSH HL
	LD HL,(PILKSP)
	EX DE,HL
	LD A,E
	CALL PUTA
	INC HL
	LD A,D
	CALL PUTA
	POP HL
	JP NEXTOP

ZED78:			;ED 78        IN A,(C)


	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	LD B,A
	EX AF,AF'
	LD A,B
	EX AF,AF'
	JP NEXTOP
ZED79:			;ED 79        OUT (C),A

	EXX
	LD A,C
	EXX
	LD C,A
	EX AF,AF'
	LD B,A
	EX AF,AF'
	LD A,B
	CALL OUTPUT
	JP NEXTOP
ZED7A:			;ED 7A        ADC HL,SP

	EXX
	PUSH DE
	LD DE,(PILKSP)
	EX AF,AF'
	ADC HL,DE
	EX AF,AF'
	POP DE
	EXX
	JP NEXTOP
ZED7B:			;ED 7B nn nn  LD SP,(nn)

	CALL GETA
	LD E,A
	INC HL ;MI
	CALL GETA
	LD D,A
	INC HL ;MI
	EX DE,HL
	CALL GETA
	LD (PILKSP),A
	INC HL
	CALL GETA
	LD (PILKSP+1),A
	EX DE,HL
	JP NEXTOP

ZEDA0:			;ED A0        LDI
	EXX
	CALL ZEDA0J
	EXX
	JP NEXTOP
ZEDA0J:			; LDIR USES THIS ENTRY!!!
	CALL GETA
	LD (ZEDA0D),A
	EX DE,HL
	CALL PUTA
	EX DE,HL
	PUSH HL
	PUSH DE
	LD HL,ZEDA0D
	LD DE,ZEDA0D
	EX AF,AF'
	LDI
	EX AF,AF'
	POP DE
	POP HL
	INC DE
	INC HL
	RET

ZEDA0D:	DEFB 0

ZEDA2:			;ED A2        INI
	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	EXX
	CALL PUTA
	INC HL
	EX AF,AF'
	DEC B
	EX AF,AF'
	EXX
	JP NEXTOP
ZEDA3:			;ED A3        OUTI

	EXX
	LD A,C
	EXX
	LD C,A

	EXX
	CALL GETA
	EXX
	CALL OUTPUT
	EX AF,AF'
	EXX
	INC HL
	DEC B
	EXX
	EX AF,AF'
	JP NEXTOP
ZEDA8:			;ED A8        LDD

	EXX
	CALL ZEDA8J
	EXX
	JP NEXTOP
ZEDA8J:			; LDDR USES THIS ENTRY!!!
	CALL GETA
	LD (ZEDA0D),A
	EX DE,HL
	CALL PUTA
	EX DE,HL
	PUSH HL
	PUSH DE
	LD HL,ZEDA8D
	LD DE,ZEDA8D
	EX AF,AF'
	LDD
	EX AF,AF'
	POP DE
	POP HL
	DEC DE
	DEC HL
	RET

ZEDA8D:	DEFB 0

ZEDA9:			;ED A9        CPD

	EXX
	CALL GETA
	DEC HL
	DEC BC
	EXX
	LD B,A
	EX AF,AF'
	CP B
	EX AF,AF'
	JP NEXTOP

ZEDAA:			;ED AA        IND

	EXX
	LD A,C
	EXX
	LD C,A
	CALL INPUT
	EXX
	CALL PUTA
	DEC HL
	EX AF,AF'
	DEC B
	EX AF,AF'
	EXX
	JP NEXTOP

ZEDAB:			;ED AB        OUTD

	EXX
	LD A,C
	EXX
	LD C,A

	EXX
	CALL GETA
	EXX
	CALL OUTPUT
	EXX
	EX AF,AF'
	DEC HL
	DEC B
	EX AF,AF'
	EXX
	JP NEXTOP

ZEDB0:			;ED B0        LDIR

	EXX
	CALL ZEDA0J	;LDI entrypoint
	LD A,B
	OR C
	EXX
	JP Z,NEXTOP
	DEC HL
	DEC HL
	JP NEXTOP

ZEDB1:			;ED B1        CPIR

	EX AF,AF'
	LD C,A
	EX AF,AF'

	EXX
ZEDB1J:	CALL GETA
	INC HL
	DEC BC
	EXX
	LD B,A
	LD A,C
	CP B
	JR Z,ZEDB1J2
	EXX
	LD A,B
	OR C
	JP NZ,ZEDB1J
	EXX
ZEDB1J2:
	EX AF,AF'
	CP B
	EX AF,AF'
	JP NEXTOP

ZEDB2:			;ED B2        INIR

	EXX
	LD A,C
	EXX
	LD C,A
ZEDB2J1:
	CALL INPUT
	EXX
	CALL PUTA
	INC HL
	LD A,B
	EX AF,AF'
	DEC B
	EX AF,AF'
	EXX
	DEC A
	JR NZ,ZEDB2J1
	JP NEXTOP

ZEDB3:			;ED B3        OTIR

	EXX
	LD A,C
	EXX
	LD C,A

ZEDB3J2:

	EXX
	CALL GETA
	EXX
	CALL OUTPUT
	EXX
	LD A,B
	EX AF,AF'
	INC HL
	DEC B
	EX AF,AF'
	EXX
	DEC A
	JR NZ,ZEDB3J2

	JP NEXTOP

ZEDB8:			;ED B8        LDDR

	EXX
	CALL ZEDA8J	;LDD entrypoint
	LD A,B
	OR C
	EXX
	JP Z,NEXTOP
	DEC HL
	DEC HL
	JP NEXTOP

ZEDB9:			;ED B9        CPDR
	EX AF,AF'
	LD C,A
	EX AF,AF'

	EXX
ZEDB9J:	CALL GETA
	DEC HL
	DEC BC
	EXX
	LD B,A
	LD A,C
	CP B
	JR Z,ZEDB9J2
	EXX
	LD A,B
	OR C
	JP NZ,ZEDB9J
	EXX
ZEDB9J2:
	EX AF,AF'
	CP B
	EX AF,AF'
	JP NEXTOP

ZEDBA:			;ED BA        INDR

	EXX
	LD A,C
	EXX
	LD C,A
ZEDBAJ1:
	CALL INPUT
	EXX
	CALL PUTA
	DEC HL
	LD A,B
	EX AF,AF'
	DEC B
	EX AF,AF'
	EXX
	DEC A
	JR NZ,ZEDBAJ1

	JP NEXTOP

ZEDBB:			;ED BB        OTDR

	EXX
	LD A,C
	EXX
	LD C,A
ZEDBBJ2:

	EXX
	CALL GETA
	EXX
	CALL OUTPUT
	EXX
	LD A,B
	EX AF,AF'
	DEC HL
	DEC B
	EX AF,AF'
	EXX
	DEC A
	JR NZ,ZEDBBJ2

	JP NEXTOP
