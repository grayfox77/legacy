;
;                      Hallowen.SMM
;
;          "Halloween" Source Game Music Module
;
; Version Amalthea-1-0 (1st Ver., 1st Release, No Revis.)
;
;   This  MSX  Assembly  source file is part of "Yawara:
;   General Game  Music Player". Please, read the notice
;   and documentation file "Yawara.html". If you use the
;   program  in any way  you are  automatically agreeing
;   with  everything stated in  that  file, even if  you
;   haven't read it, so  I  recommend you  do  it. Thank
;   you for your cooperation, good night.
;
;    This file was created, edited and programmed from
;    1994  to 1998 by  "Cyberknight" Masao Kawata  for
;    "Unicorn Dreams Artwork Programs".  For  contact,
;    send an E-Mail to:
;
;                cyber_unicorn@hotmail.com
;
;    Check also the Home Page for new releases:
;
;            http://unicorndreams.home.ml.org
;

;
;   Compile using M80 and L80:
;
;   M80 =Hallowen.SMM
;   L80 Hallowen,Hallowen.GMM /n/e
;

;
;   To execute, run the program loader ("Yawara.Bin" or
;   "Yawara.Saf"). Please, read the  documentation file
;   ("Yawara.html").
;

;
;   "Halloween" game mapping:
;
;   0000                   99FF
;      |?|M|M|?|?|?|?|?|?|?|?|?|?|?|
;   0000                           D9FF
;
;   M = Game Music Routines and Data.
;


; MSX standard addresses and constants.

HOLD8  equ  0F806H
FNKSTR equ  0F87FH
LINTTB equ  0FBB2H
HKEYI  equ  0FD9AH
HTIMI  equ  0FD9FH
SysDB  equ  LINTTB
EBDBnk equ  FNKSTR
SavHok equ  SysDB+010H
EBTb0  equ  HOLD8
Where  equ  EBTb0+039H


       .Z80
       aseg

       org  00100H
       .Phase 00100H

GameDt:db   '           Halloween            '
GmBlPt:dw   GmBlNm-GameDt
StupRP:dw   SetupR-GameDt
SilnRP:dw   SilnRt-GameDt
StMsRP:dw   StMsRt-GameDt
PlayRP:dw   PlayRt-GameDt
GMPBAd:dw   0C600H
GMPNam:db   'Yawara  C60'
OpenMN:db   000H
StrtSE:db   000H
ChATDD:dw   0E011H
ChBTDD:dw   0E01CH
ChCTDD:dw   0E027H
BGMSPt:dw   MusicT-GameDt
SFXSPt:dw   EffctT-GameDt
OpnGFN:db   'Hallown0Sc2'
OpnGFS:dw   04000H
MnuGFN:db   'Hallown1Sc2'
MnuGFS:dw   04000H
OpnStC:dw   21*32+10
Resorc:db   00000010B

SetupR:jr   z,Start
       ld   de,00001H
       ld   a,(de)
       ld   (SavHok),a
       inc  de
       ld   a,(de)
       ld   (HTIMI),a
       ld   hl,0B000H        ; Restore BIOS.
       ld   de,00000H
       ld   bc,01600H
       ldir
       ret

Start: ld   a,(00000H)
       or   a
       jr   z,SilnRt
       ld   hl,00000H        ; Save BIOS.
       ld   de,0B000H
       ld   bc,01600H
       ldir
       ld   hl,01600H        ; Move game block.
       ld   de,00000H
       ld   bc,09A00H
       ldir
       ld   de,00001H
       ld   c,0C9H
       ld   (015D6H),a
       ld   hl,SavHok
       ld   a,(hl)
       ld   (de),a
       ld   (hl),c
       inc  de
       ld   hl,HTIMI
       ld   a,(hl)
       ld   (de),a
       ld   (hl),c
       call Where
Here:  ld   de,There-Here
       add  hl,de
       ld   de,00038H
       ld   bc,GmBlNm-There
       ldir
SilnRt:xor  a
       ld   (020F9H),a
       jp   01D4BH

StMsRt:cp   0FFH
       jr   z,SilnRt
       jp   01BCAH

PlayRt:call 01C7FH
       jp   015C5H

There: push hl
       push de
       push bc
       push af
       exx
       ex   af,af'
       push hl
       push de
       push bc
       push af
       push iy
       push ix
       call HKEYI

GmBlNm:db   'Hallowen000'
GmBlSz:dw   09A00H
LoadAd:dw   01600H
       dw   0FFFFH
MusicT:db   000H,'   All Hallows'' Eve!   '
       db   001H,'      Halloween!!      '
       db   0FFH,0FFH
EffctT:db   0FFH,'       Silence...      '
       db   0FFH,0FFH

       end
