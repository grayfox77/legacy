;VSYS2.MAC

PUBLIC STATUS,INIALL

MACLIB B:\mark\MAC\VSYS.INC
MACLIB B:\mark\MAC\VBASE.INC

;----------------------------- STATUS CHECK!

STATUS:
 CALL SET_BUFSIZ
 CALL CHECK_MSX
 RET NZ
 CALL CHECK_MEM
 RET NZ
 CALL CHECK_DISK
 RET NZ
 CALL S_PARAM
 CALL OPTION
 RET NZ
 JP RTRUE

;----------------------------- SUPER INIALL!

INIALL:;CANNOT USE DTA!
; LD IX,0041H;VDP=OFF!
; CALL GOBIOS
 CALL VFIRST
 CALL SCRTXT
 CALL OFFKDV
 CALL B_TROF
 CALL SCRTXT
 CALL SAVEPR
 CALL FVDP
 CALL SAVE_DOS_COLOR
 CALL COLOR
 CALL PALETTE
 CALL BASEWIDTH
 CALL KTBLSET
 CALL INIDSK
 CALL S_TABCLM
 CALL S_PUTEOF
 CALL FONTSET
; CALL SVDP
; LD IX,0044H;VDP=ON!
; CALL GOBIOS
 CALL RESUME
 JP Z,RTRUE
 JP RFALSE

;----------------------- START SUB
S_PARAM:
 LD A,(CFG100)
 LD (MAXTXT),A
 LD A,(CFG100+1)
 LD (MTXTMD),A
 LD A,(CFG100+2)
 LD (CLICKF),A
 LD A,(CFG100+3)
 AND A
 JP Z,SPRMJ0
 LD (TMPDRV),A
SPRMJ0:
 LD HL,CFG101
 LD DE,INSMD
 LD BC,CFG102 -CFG101
 LDIR
 LD HL,CFG102
 LD DE,REPTIM
 LD BC,CFG103 - CFG102
 LDIR

 RET

OPTION:
 LD A,(CMDTA)
 AND A
 JP Z,RTRUE
 LD HL,CMDTA+1
 CALL TXBIG
 LD A,(CMDTA)
 LD C,A
 LD HL,CMDTA+1
 CALL ACTPRM
 LD (MSDTA),HL
 LD (MSDTA+2),BC
 JP Z,RTRUE
 JP RFALSE

ACTPRM:
 LD A,(HL)
 CP " "
 JP NZ,ACPJ0
 INC HL
 DEC C
 JP Z,RTRUE
 JP ACTPRM
ACPJ0:
 LD A,(HL)
 LD IX,OPTHD
 CALL CHECKM
 AND A
 JP Z,RTRUE
 INC HL
 DEC C
 JP Z,RTRUE
 LD IX,OPTD
 CALL GETBT
 AND A
 LD DE,ERS003
 JP Z,RFALSE
 CALL OPTK
 JP NZ,RFALSE
 LD A,C
 AND A
 JP Z,RTRUE
 JP ACTPRM

OPTK:
 CP 9
 JP C,OPTKJ0;SET MAXTXT
 JP Z,OPTKJ1;SET TMPDRV
 CP 10
 JP Z,OPTKJ2;SET BINARY
 CP 11
 JP Z,OPTKJ3;SET BACK UP
 CP 12
 JP Z,OPTKJ4;SET NORMAL MODE
; CP 13
; JP Z,OPTKJ5;SET HANKAKU REVERSE
; CP 14
; JP Z,OPTKJ6;SET JE SYSTEM OR ECHO
 CP 15
 JP Z,OPTKJ7;SET CSRWK2 -> CSRWKS
 CP 16
 JP Z,OPTKJ8;PRINT BUFFER SIZ
 CP 17
 JP Z,OPTKJ9;PRINT OPTION HELP
 CP 18
 JP Z,OPTKJ10;PRINT MARIO-NET
 CP 19
 JP Z,OPTKJ11;TAB CULUM
 LD DE,ERS003
 JP RFALSE

OPTKJ0:
 LD (MAXTXT),A
 JP RTRUE
OPTKJ1:
 LD IX,DRVD
 CALL GETBT
 LD DE,ERS004
 JP Z,RFALSE
 LD (TMPDRV),A
 JP RTRUE
OPTKJ2:
 LD A,0FFH
 LD (MTXTMD),A
 LD (TXTMD),A
 JP RTRUE
OPTKJ3:
 LD A,0FFH
 LD (BKUMD),A
 JP RTRUE
OPTKJ4:
; LD B,11011101B
; LD A,(PTFLG2)
; AND B
; LD (PTFLG2),A
; LD A,(PTFLG3)
; AND B
; LD (PTFLG3),A
; LD A,(PTFLG3+1)
; AND B
; LD (PTFLG3+1),A
; JP RTRUE
OPTKJ5:
; LD A,(PTFLG3+1)
; AND 11101110B
; OR 00000001B
; LD (PTFLG3+1),A
; JP RTRUE
OPTKJ6:
; LD A,0FFH
; LD (JESMD),A
; JP RTRUE
OPTKJ7:
 LD A,0FFH
 LD (XCMD),A
 JP RTRUE
OPTKJ8:;=
 LD DE,DOSMS0
 CALL PTMS
 CALL PUT_BUFSIZ
 LD DE,MMS003
 JP RFALSE
OPTKJ9:;?
 LD DE,MMS004
 JP RFALSE
OPTKJ10:;$
 LD DE,MMS005
 JP RFALSE
OPTKJ11:;A
 LD IX,TABD
 CALL GETBT
 LD DE,ERS004
 JP Z,RFALSE
 LD (TBCLM),A
 JP RTRUE
 

GETBT:
 LD A,(HL)
 DEC C
 INC HL
 CALL CHECKM
 AND A
 RET

CHECKM:
 PUSH HL
 PUSH IX
 POP HL
 CALL TXMACH
 POP HL
 RET

RESUME:
 LD HL,6000H
 XOR A
 CALL NWV1;KILL ZERO
 LD HL,1C00H
 LD DE,SUBDTA
 LD BC,64
 XOR A
 CALL VR2M
 LD A,64
 LD HL,SUBDTA
 LD DE,100H
 CALL TXCOMP
 AND A
 JP NZ,RFALSE
 JP RTRUE

SCRTXT:
 LD A,(SCRMOD)
 AND A
 RET Z
 LD IX,00D5H;INISCREEN
 CALL GOBIO2
 RET

B_TROF:
 LD D,0
 LD E,2
 CALL EXTBIO
 RET

OFFKDV:
 LD A,(HOKVLD)
 AND A
 RET Z
 LD D,11H
 LD E,0
 XOR A
 CALL EXTBIO
 LD (DEFKDV),A
 AND A
 RET Z

 LD D,11H
 LD E,1
 XOR A
 CALL EXTBIO
 RET

INIDSK:
 LD HL,EXCD
 CALL INIDRV
 RET

KJUNK0:DW 0000H
KJUNK1:DW 0000H
KTBLSET:
 LD HL,01800H
 CALL NSTW1
 LD (KJUNK0),HL
 LD HL,DHEAD2
 LD (KJUNK1),HL

 LD DE,16
 CALL NVM2V
 LD DE,6
 LD B,2
KTBJ0: 
 CALL DDTRS
 CALL NVM2V
 CALL DDTRX
 INC B
 DEC DE
 LD A,D
 OR E
 JP NZ,KTBJ0
 CALL DDTRX
 CALL DDTRX
 LD DE,7
 LD B,1
KTBJ1:
 CALL NVM2X
 CALL DDTRS
 CALL NVM2V
 INC B
 DEC DE
 LD A,D
 OR E
 JP NZ,KTBJ1
 CALL NVM2X
 CALL DDTRS

 LD HL,(KJUNK1)
 LD DE,512
 CALL VM2V
 RET

NVM2V:
 PUSH BC
 PUSH DE
 PUSH DE
 LD HL,(KJUNK1)
 CALL VM2V
 POP DE
 LD HL,(KJUNK1)
 ADD HL,DE
 LD (KJUNK1),HL
 POP DE
 POP BC
 RET

DDTRS:
 PUSH BC
 LD A,(WPT)
 LD C,A
 LD A,0FFH
DDTRJ0:
 OUT(C),A
 DJNZ DDTRJ0
 POP BC
 RET

NVM2X:
 PUSH DE
 LD DE,8
 CALL NVM2V
 POP DE
 RET

DDTRX:
 PUSH BC
 LD B,8
 CALL DDTRS
 POP BC
 RET

SAVEPR:
 LD HL,(0F3DBH)
 LD (PSAVE),HL
 LD HL,(0F3DDH)
 LD (PSAVE+2),HL
 LD HL,(0F3B0H)
 LD (PSAVE+4),HL
 LD HL,(0FBCEH)
 LD (PSAVE+6),HL
 LD A,(0F3DBH)
 LD (PSAVE+8),A
; LD HL,0F87FH;FUNCTIONKEY SAVE F1-F5
; LD DE,6640H
; LD BC,80
; XOR A
; CALL M2VR
 LD HL,(BASE1)
 LD DE,2800H
 LD BC,800H
 XOR A
 CALL VRMS2
 XOR A
 LD (0F3DBH),A
 XOR A
 LD (0FBCEH),A;FNKKEY=OFF
 RET

S_TABCLM:
 LD A,(TBCLM)
 LD B,128
 LD HL,TABAD
STBCJ0:
 LD (HL),A
 DEC A
 CALL Z,STBCJ1
 INC HL
 DJNZ STBCJ0
 RET
STBCJ1:
 LD A,(TBCLM)
 RET

S_PUTEOF:
 LD HL,0FF60H
 CALL NSTW2
 LD A,(WPT)
 LD C,A
 LD A,26
 LD B,160
STXJ0:
 NOP
 NOP
 NOP
 OUT(C),A
 DJNZ STXJ0 

 LD HL,0FF80H
 CALL NSTW2
 LD DE,(DEOF1)
 LD D,0
 LD HL,DEOF1+1
 CALL VM2V

 LD HL,0FFA0H
 CALL NSTW2
 LD DE,(DEOF2)
 LD D,0
 LD HL,DEOF2+1
 CALL VM2V

 LD HL,0FF60H
 LD DE,0FF60H
 LD BC,144
 LD A,1
 CALL VRMS2

 RET

;------------------------- FVDP
FVDP:
 LD HL,(LINL40)
 LD H,0
 LD DE,(WIDTH2)
 CALL ICOMP2
 AND A
 JP NZ,FVW80J
 JP FVW40J

FVW80J:
 LD HL,(WIDTH2)
 LD (WIDTH0),HL
 LD (WIDTH),HL
 LD HL,0
 LD (BASE1),HL
 LD (BASE),HL
 LD HL,1000H
 LD (BASE3),HL  
 LD A,0FFH
 LD (BASE7),A
 RET

FVW40J:
 LD HL,(WIDTH1)
 LD (WIDTH0),HL
 LD (WIDTH),HL
 LD HL,0
 LD (BASE1),HL
 LD (BASE),HL
 LD HL,800H
 LD (BASE3),HL
 XOR A
 LD (BASE7),A
 RET

FONTSET:
 LD HL,(BASE3)
 CALL NSTW1
 LD DE,2048
 LD HL,DHEAD
 CALL VM2V
 RET

SAVE_DOS_COLOR:
 LD HL,DHEAD2 -48 -4 -48 -4
 LD A,(HL)
 INC HL
 LD (DSCLMD),A
 LD DE,6701H
 LD BC,48 + 3
 XOR A
 CALL M2VR
 RET

COLOR:
 LD A,(DHEAD2 -48 -4)
 LD (EDCLMD),A
 AND 00000100B
 RET Z
 LD HL,DHEAD2 -48 -3
 LD DE,0F3E9H
 LD BC,3
 LDIR
 LD IX,00D5H
 CALL GOBIO2
 RET

PALETTE:
 LD A,(DHEAD2 -48 -11)
 AND 00000011B
 RET Z;NO SET
 LD HL,DHEAD2 - 48
 CP  00000010B
 CALL Z,PLTJ0
 CALL SVPLT
 RET
PLTJ0:
 LD HL,DHEAD2 -48 -11 -48
 RET

BASEWIDTH:
 LD HL,(BASE4)
 LD DE,(WIDTH)
 DEC HL
 CALL IMULT
 LD DE,(BASE1)
 ADD HL,DE
 LD (BASE5),HL
 LD DE,(WIDTH)
 ADD HL,DE
 LD (BASE6),HL
 RET

VRMS2:
 PUSH DE
 PUSH BC
 PUSH AF
 AND 00000001B
 LD DE,DTA+2000H
 CALL VR2M
 POP AF
 AND 00000010B
 POP BC
 POP DE
 LD HL,DTA+2000H
 CALL M2VR
 RET

;---------------------------------- CHECK SUB

SET_BUFSIZ:
 LD HL,(0006H)
 LD DE,DTA
 AND A
 SBC HL,DE
 LD DE,400H
 AND A
 SBC HL,DE
 LD (BUFSIZ),HL
 RET

PUT_SYS_INFO:
 LD A,(CMDTA)
 AND A
 RET Z
 LD A,(CMDTA+1)
 CP "="
 JP NZ,RTRUE
 LD DE,DOSMS0
 CALL PTMS
 CALL PUT_BUFSIZ
 LD DE,MMS003
 JP RFALSE

CHECK_MSX:
 LD A,(EXPTBL)
 LD HL,002DH
 CALL RDSLT
 AND A
 LD DE,ERS001
 JP Z,RFALSE
 JP RTRUE

CHECK_VRAM:
 LD A,(0FAFCH);MODE
 AND 00000110B;VRAM?
 CP  00000100B;VRAM 128K
 LD DE,ERS002
 JP NZ,RFALSE
 JP RTRUE

CHECK_MEM:
 LD HL,(BUFSIZ)
 LD DE,2800H
 AND A
 SBC HL,DE
 JP NC,RTRUE

 LD DE,ERS000
 CALL PTMS
 CALL PUT_BUFSIZ
 LD DE,MMS003
 JP RFALSE

CHECK_DISK:
 LD DE,DTA+2000H
 CALL SDMA
 LD C,19H
 CALL SYSENT
 INC A
 LD (DEFDRV),A
 LD (TMPDRV),A
 CALL FXDRV
 LD DE,DTA
 CALL SDMA
 JP RTRUE

PUT_BUFSIZ:
 LD DE,MMS000
 CALL PTMS
 LD HL,(BUFSIZ)
 LD DE,DTA+2000H;WORKAREA
 CALL PTHL
 LD DE,MMS002
 CALL PTMS
 JP RTRUE



MMS000:DB "buffer size = $"
MMS001:DB 13,10,"$"
MMS002:DB " byte(s)",13,10,"$"
MMS003:DB "break.",13,10,"$"
MMS004:DB "/(1-8)  Max open files",13,10
       DB "/T(A-H) Temporary drive",13,10
       DB "/N      Binary edit",13,10
       DB "/B      Make back up file",13,10
       DB "/=      Put buffer size",13,10
       DB "/?      Option help",13,10
       DB "$"
MMS005:DB 13,10
       DB "           MARIO-NET",13,10,13,10
       DB "         (0427-95-3421)",13,10
       DB 13,10
       DB "Thanks for...",13,10,13,10
       DB " MARIO,AYUMU,KENICHI,ϯè-,LARK",13,10
       DB " MAHOUJIN,SNOWWIND,NIYA,TYOKOBO,RAM",13,10
       DB " TAKAWO,BLASTER,S76,ITA",13,10
       DB "$"


ERS000:DB "Not enough memory",13,10,"$"
ERS001:DB "Wrong version of MSX",13,10,"$"
ERS002:DB "Not enough VRAM",13,10,"$"
ERS003:DB "Wrong parameter error",13,10,"$"
ERS004:DB "Illegal operand error",13,10,"$"

OPTHD:DB "/-",0
OPTD:DB "12345678TNBMHS#=?$A",0
DRVD:DB "ABCDEFGH",0
TABD:DB "#2#4###8",0

END
