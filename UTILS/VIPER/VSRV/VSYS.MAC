;VSYS.MAC
;.Z80
;CSEG

	PUBLIC	STARTUP,KANEL,KANELF,KANELM,S_TXMD,GODOS,RSTART
	PUBLIC	GODOSR,EXCSAV,EXCTXT,EXCRST,EXCRS2,LOADSC,SAVESC
	PUBLIC	INPYN,KSCRST,MEDSTL,STKCLS,EDSWON
	PUBLIC	JRET,EXRET,EXIT,EXCD,EXTBI2

;INCLUDE VBASE.INC
	INCLUDE	VWORK.INC
	INCLUDE	RABELS.INC
	INCLUDE	SYSDRV.INC
	INCLUDE	MATHDRV.INC
	INCLUDE	VDPDRV.INC
	INCLUDE	FILEDRV.INC
	INCLUDE	VKEY.INC
;INCLUDE VMEMORY.INC
	INCLUDE	VSTACK.INC
	INCLUDE	VTEXT.INC
	INCLUDE	VLH.INC
;INCLUDE VSCREEN.INC
	INCLUDE	VCSR.INC
	INCLUDE	VVIEW.INC
	INCLUDE	VPUT.INC
	INCLUDE	VEDITOR.INC
	INCLUDE	VFILER.INC
	INCLUDE	VINPBOX.INC
	INCLUDE	VMENUBOX.INC
	INCLUDE	VSYS2.INC

KMAP:	DW	0000H

STARTUP:
	CALL	STATUS;SYS2 CHECK STATUS!
	JP	NZ,PTMS
	LD	SP,(0006H)

	CALL	INIALL;SYS2 INIALL!
	JP	Z,RESUME_WAKEUP
	CALL	SYSRST
	CALL	S_TXCB
	CALL	S_TRAP
	CALL	TIMISV
	CALL	ONTRP
	CALL	SCNCUT
	CALL	EXCRS2

	CALL	MFOPTION
GOEDIT:
	CALL	FWIN
	JP	NZ,GODOS
STJ0:
	LD	HL,KMAP1
	CALL	KANEL
	AND	A
	JP	Z,GODOSR
	JP	GODOS

RESUME_WAKEUP:
	CALL	LDRSM
	XOR	A
	LD	HL,1C00H
	CALL	NWV1
	CALL	S_TRAP
	CALL	TIMISV
	CALL	ONTRP
	CALL	SCNCUT
; CALL SYSRST
	CALL	LOADSC
	CALL	SCRRST
	CALL	OPTION2
	CALL	Z,F_LOAD
	JP	STJ0

EXCD:
	POP	HL
	RET

KANEL:
	LD	DE,(KMAP)
	PUSH	DE
	LD	(KMAP),HL
	CALL	CSRWKS
	CALL	MVCSR
EKJ0:
	CALL	FRONT
	LD	A,(SYS12)
	AND	A
	CALL	NZ,KSCRST
EKJ1:
	LD	A,(COM)
	LD	IY,(KMAP)
	LD	B,(IY)
	CP	B
	JP	NC,EKJ0
	INC	IY
	LD	IX,EKJ3
	CALL	MPCALL;CALL FUNCTION
EKJ3:
	CALL	CSRWKS
	CALL	MVCSR
	JP	EKJ0

KANELF:
	LD	DE,(KMAP)
	PUSH	DE
	LD	(KMAP),HL
	CALL	CSRWKS
	CALL	MVCSR
EKFJ0:
	CALL	FRONT
	LD	A,(SYS12)
	AND	A
	CALL	NZ,KSCRST
EKFJ1:
	LD	A,(COM)
	LD	IY,(KMAP)
	LD	B,(IY)
	CP	B
	JP	NC,EKFJ0
	INC	IY
	LD	IX,EKFJ3
	CALL	MPCALL;CALL FUNCTION
EKFJ3:
	LD	HL,16
	LD	(CSP9),HL
	CALL	CSRWKS
	CALL	MVCSR
	JP	EKFJ0

KANELM:
	LD	DE,(KMAP)
	PUSH	DE
	LD	(KMAP),HL
	CALL	MVMCSR
EKMJ0:
	CALL	FRONT
	LD	A,(SYS12)
	AND	A
	CALL	NZ,KSCRST
EKMJ1:
	CALL	TIMIOF
	LD	A,(COM)
	LD	IY,(KMAP)
	LD	B,(IY)
	CP	B
	JP	NC,EKMJ0
	INC	IY
	LD	IX,EKMJ3
	CALL	MPCALL;CALL FUNCTION
EKMJ3:
	CALL	MVMCSR
	JP	EKMJ0

EXRET:
	POP	DE
	POP	HL
	POP	HL
	LD	(KMAP),HL
	PUSH	DE
	RET

EXIT:
	CALL	EXRET
	LD	A,0FFH
	AND	A
	RET

JRET:
	RET

KSCRST:
	PUSH	AF
	LD	HL,SVDTA1
	CALL	LODSCT
	XOR	A
	LD	(SYS12),A
	POP	AF
	RET

INPYN:
	PUSH	AF
	CALL	RSTMS2
	POP	AF
	CALL	EPUTMS
	CALL	WAIT2
	PUSH	AF
	LD	HL,SVDTA2
	CALL	LODSCT
	POP	AF
	RET

S_TXMD:
	CALL	S_TEXTMODE
	CALL	S_SYSP
	CALL	S_EOFMODE
	CALL	S_LHEAD
	CALL	S_MAX
	RET

S_TEXTMODE:
	LD	HL,(BASE)
	CALL	INEG
	LD	(PCTRL2),HL
	LD	A,(TXTMD)
	AND	A
	LD	HL,(SCB2+8)
	JP	Z,STMJ0
	INC	HL
STMJ0:
	LD	(MEM10),HL
	CALL	INEG
	LD	(PCTRL0),HL
	RET

S_EOFMODE:
	LD	A,(EOFMD)
	LD	HL,0FF60H
	LD	DE,32
	AND	A
	JP	Z,SEMJ0
	ADD	HL,DE
	DEC	A
	JP	Z,SEMJ0
	ADD	HL,DE
SEMJ0:
	LD	(PCTRL1),HL
	RET

S_SYSP:
	LD	HL,(SYS0)
	LD	DE,(WIDTH)
	CALL	IMULT
	LD	DE,(BASE)
	ADD	HL,DE
	LD	(SYS2),HL

	LD	HL,(SYS1)
	LD	DE,(WIDTH)
	CALL	IMULT
	LD	DE,(SYS2)
	ADD	HL,DE
	LD	(SYS3),HL

	LD	DE,(WIDTH)
	AND	A
	SBC	HL,DE
	LD	(SYS4),HL

	LD	DE,(SYS2)
	AND	A
	SBC	HL,DE
	LD	(SYS5),HL

	LD	HL,(SYS3)
	LD	DE,(WIDTH)
	ADD	HL,DE
	LD	(SYS6),HL

	LD	HL,(SYS2)
	LD	DE,(WIDTH)
	PUSH	HL
	AND	A
	SBC	HL,DE
	LD	(SYS7),HL

	POP	HL
	ADD	HL,DE
	LD	(SYS8),HL

	LD	HL,(SYS3)
	LD	DE,(SYS2)
	AND	A
	SBC	HL,DE
	LD	(SYS9),HL

	LD	HL,(SYS4)
	LD	DE,(WIDTH)
	AND	A
	SBC	HL,DE
	LD	(SYS10),HL

	LD	HL,(SYS5)
	LD	DE,(WIDTH)
	AND	A
	SBC	HL,DE
	LD	(SYS11),HL

	RET

EDSWON:
	LD	A,(EDFLG)
	OR	00010000B
	LD	(EDFLG),A
	RET

MEDSTL:
	LD	HL,FCB5
	LD	DE,EDSTL
	LD	BC,12
	LDIR

	LD	HL,0
	LD	(EDSTL+12),HL
	LD	(EDSTL+14),HL

	LD	HL,(SCB1+4)
	LD	A,(TMPRST)
	AND	2
	JP	NZ,MEDSJ0
	LD	HL,0FFFFH
	LD	(EDSTL+12),HL
	LD	(EDSTL+14),HL
MEDSJ0:
	LD	HL,EDSTL+12
	LD	DE,SCB2+4
	LD	BC,EDSTL+16
	CALL	DBLADD

	LD	HL,EDSTL+16
	LD	DE,SCB1+4
	LD	BC,EDSTL+12
	CALL	DBLADD

	LD	A,(TXTNUM)
	LD	(EDSTL+16),A

	LD	A,(EDFLG)
	LD	(EDSTL+17),A
	LD	HL,(CSP0)
	LD	(EDSTL+18),HL
	LD	HL,(CSP1)
	LD	(EDSTL+20),HL
	LD	HL,(CSP3)
	LD	(EDSTL+22),HL
	LD	HL,(CSP10)
	LD	(EDSTL+24),HL
	RET

EXCSAV:
	CALL	MEDSTL
	LD	HL,(CSRP)
	LD	DE,(SYS2)
	AND	A
	SBC	HL,DE
	LD	(CSP14),HL
	LD	A,(TXTNUM)
	CALL	TXCB2V
	RET

EXCTXT:
	PUSH	AF
	CALL	EXCSAV
	POP	AF
	LD	(TXTNUM),A
	CALL	V2TXCB
	CALL	RSTFNK
	RET

EXCRST:
	CALL	S_TXMD
	LD	HL,(CSP14)
	LD	DE,(SYS2)
	ADD	HL,DE
	LD	(CSRP),HL
	LD	(OCSRP),HL
	LD	A,(EDFLG)
	AND	00000001B
	JP	NZ,EXJ0
	CALL	NEWTXT
EXJ0:
	CALL	S_TXMD
	CALL	S_MAX
	CALL	PUTTX3
	CALL	CSRSET
	CALL	PUTBAR
	RET

EXCRS2:
	CALL	S_TXMD
	LD	HL,(CSP14)
	LD	DE,(SYS2)
	ADD	HL,DE
	LD	(CSRP),HL
	LD	(OCSRP),HL
	LD	A,(EDFLG)
	AND	00000001B
	JP	NZ,EX2J0
	CALL	NEWTXT
EX2J0:
	CALL	S_TXMD
	CALL	S_MAX
	CALL	INCTX3
	CALL	CSRSET
	RET

TXCB2V:
	CP	4
	JP	NC,TX2VJ0
	ADD	A,A
	ADD	A,20H
	LD	E,0
	LD	D,A
	LD	HL,MEMSIZ
	LD	BC,SPCSIZ-MEMSIZ
	XOR	A
	CALL	M2VR
	RET
TX2VJ0:
	SUB	4
	ADD	A,A
	ADD	A,30H
	LD	E,0
	LD	D,A
	LD	HL,MEMSIZ
	LD	BC,SPCSIZ-MEMSIZ
	XOR	A
	CALL	M2VR
	RET

V2TXCB:
	CP	4
	JP	NC,V2TXJ0
	ADD	A,A
	ADD	A,20H
	LD	L,0
	LD	H,A
	LD	DE,MEMSIZ
	LD	BC,SPCSIZ-MEMSIZ
	XOR	A
	CALL	VR2M
	RET
V2TXJ0:
	SUB	4
	ADD	A,A
	ADD	A,30H
	LD	L,0
	LD	H,A
	LD	DE,MEMSIZ
	LD	BC,SPCSIZ-MEMSIZ
	XOR	A
	CALL	VR2M
	RET

OPTION2:
	LD	A,(CMDTA)
	AND	A
	JP	Z,RFALSE
	LD	HL,CMDTA+1
	LD	DE,FCB9
	CALL	TX2FCB
	JP	NZ,RFALSE
	JP	RTRUE

MFOPTION:
	XOR	A
	LD	(FEPBUF),A
	LD	A,(CMDTA)
	AND	A
	RET	Z
	LD	HL,(MSDTA)
	LD	BC,(MSDTA+2)
	LD	B,0
	LD	DE,FEPBUF
	LD	A,(MAXTXT)
	CALL	TX2MFCB
	RET

SYSRST:
	LD	A,(DEFDRV)
	LD	(FCB5),A
	LD	(FCB6),A
	LD	A,(TMPDRV)
	LD	(FCB1),A
	LD	(FCB2),A
	LD	(FCB4),A
	LD	(FCB7),A
	LD	(FCB8),A
	RET

S_TXCB:
;------------------ USR TEXTSPACE
	LD	HL,0FF00H;TXTMEMSIZ
	LD	(SUBDTA+4),HL
	LD	HL,02000H;NUMMEMSIZ
	LD	(SUBDTA+6),HL
	LD	HL,0;TXTMEMSTART
	LD	(SUBDTA+8),HL
	LD	HL,8000H;NUMMEMSTART
	LD	(SUBDTA+10),HL
	LD	A,0FFH;TXTMEMSWITCH
	LD	(SUBDTA+12),A
	XOR	A;NUMMEMSWITCH
	LD	(SUBDTA+13),A
	LD	HL,(MAXTXT)
	LD	(SUBDTA+14),HL
	LD	HL,4
	LD	(SUBDTA+16),HL
	CALL	PSTXCB
	LD	DE,3000H
	LD	A,(MAXTXT)
	LD	B,A
	CALL	LDTXCB
;------------------ SYSTEM WORKSPACE
	LD	HL,1500H;TXTMEMSIZ
	LD	(SUBDTA+4),HL
	LD	HL,0300H;NUMMEMSIZ
	LD	(SUBDTA+6),HL
	LD	HL,4800H;TXTMEMSTART
	LD	(SUBDTA+8),HL
	LD	HL,5D00H;NUMMEMSTART
	LD	(SUBDTA+10),HL
	XOR	A;TXTMEMSWITCH
	LD	(SUBDTA+12),A
	XOR	A;NUMMEMSWITCH
	LD	(SUBDTA+13),A
	LD	HL,3
	LD	(SUBDTA+14),HL
	LD	HL,0
	LD	(SUBDTA+16),HL
	CALL	PSTXCB
	LD	DE,2000H
	LD	B,3
	CALL	LDTXCB

;------------------- FILER SPACE
	LD	HL,1C00H;TXTMEMSIZ
	LD	(SUBDTA+4),HL
	LD	HL,0400H;NUMMEMSIZ
	LD	(SUBDTA+6),HL
	LD	HL,0C000H;TXTMEMSTART
	LD	(SUBDTA+8),HL
	LD	HL,0DC00H;NUMMEMSTART
	LD	(SUBDTA+10),HL
	XOR	A;TXTMEMSWITCH
	LD	(SUBDTA+12),A
	XOR	A;NUMMEMSWITCH
	LD	(SUBDTA+13),A
	LD	HL,1
	LD	(SUBDTA+14),HL
	LD	HL,0
	LD	(SUBDTA+16),HL
	CALL	PSTXCB
	LD	DE,2600H
	LD	B,1
	CALL	LDTXCB
;------------------ FIRST SET
	LD	HL,3000H
	LD	DE,MEMSIZ
	LD	BC,SPCSIZ-MEMSIZ
	XOR	A
	CALL	VR2M
	LD	A,4
	LD	(TXTNUM),A
	RET

PSTXCB:;PRESET TEXT CONTOROL BLOCK
;SET MEMSIZ/MAXMEM <-------- SCB1/2
	XOR	A
	LD	(EDFLG),A
	XOR	A
	LD	(TMPRST),A

	LD	DE,(SUBDTA+4)
	LD	HL,(SUBDTA+14)
	CALL	IMOD
	LD	(MEMSIZ),DE
	LD	(SCB1+10),DE
	LD	(SCB2+10),DE
	LD	(SUBDTA),DE

	LD	DE,(SUBDTA+6);<---------- SCB3
	LD	HL,(SUBDTA+14)
	CALL	IMOD
	LD	(SCB3+10),DE
	LD	(SUBDTA+2),DE

;SET POINTER/START
	LD	HL,(SUBDTA+8)
	LD	(SCB1),HL
	LD	(SCB1+8),HL
	LD	DE,(SCB1+10)
	ADD	HL,DE
	DEC	HL
	LD	(SCB2),HL
	LD	(SCB2+8),HL
	LD	HL,(SUBDTA+10)
	LD	(SCB3),HL
	LD	(SCB3+8),HL

;SET MINMEM/WMEM/RMEM
	LD	HL,800H
	LD	(SCB1+12),HL;MINMEM <---- SCB1/2
	LD	(SCB2+12),HL
	LD	HL,100H
	LD	(SCB3+12),HL

	LD	HL,(SCB1+10)
	LD	DE,(EDTSIZ);STANDARD SPCSIZ
	AND	A
	SBC	HL,DE
	SRL	H
	RR	L
	LD	DE,800H;MINMEM
	AND	A
	SBC	HL,DE
	LD	(SCB1+14),HL
	LD	(SCB1+16),HL
	LD	(SCB2+14),HL
	LD	(SCB2+16),HL
	LD	HL,(SCB3+10);<----------- SCB3
	SRL	H
	RR	L
	LD	(SCB3+14),HL
	LD	(SCB3+16),HL

;SET MEMSWITCH
	LD	A,(SUBDTA+12)
	LD	(SCB1+24),A
	LD	(SCB2+24),A
	LD	A,(SUBDTA+13)
	LD	(SCB3+24),A
	RET

LDTXCB:
	PUSH	BC
	PUSH	DE
	PUSH	DE

	LD	HL,(SUBDTA+16)
	LD	DE,EXCDTA
	CALL	HL2TX
	LD	B,A
	LD	A,4
	LD	HL,EXCDTA
	CALL	RSFT2

	LD	BC,4
	LD	HL,EXCDTA
	LD	DE,FCB1+5
	LDIR
	LD	BC,4
	LD	HL,EXCDTA
	LD	DE,FCB2+5
	LDIR
	LD	BC,4
	LD	HL,EXCDTA
	LD	DE,FCB3+5
	LDIR
	LD	BC,4
	LD	HL,EXCDTA
	LD	DE,FCB4+5
	LDIR
	LD	BC,4
	LD	HL,EXCDTA
	LD	DE,FCB5+5
	LDIR
	LD	BC,4
	LD	HL,EXCDTA
	LD	DE,FCB6+5
	LDIR

	LD	HL,MEMSIZ
	POP	DE
	LD	BC,SPCSIZ-MEMSIZ
	XOR	A
	CALL	M2VR

	LD	BC,(SUBDTA)
	LD	DE,SCB1;POINTER
	CALL	IMADD
	LD	DE,SCB1+8;START
	CALL	IMADD
	LD	DE,SCB2
	CALL	IMADD
	LD	DE,SCB2+8
	CALL	IMADD
	LD	BC,(SUBDTA+2)
	LD	DE,SCB3
	CALL	IMADD
	LD	DE,SCB3+8
	CALL	IMADD
	LD	DE,SUBDTA+16
	LD	BC,1
	CALL	IMADD

	POP	DE
	INC	D
	INC	D
	POP	BC
	DEC	B
	JP	NZ,LDTXCB
	RET

;------------------------ 
LOADSC:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL,4000H
	LD	DE,(BASE1)
	LD	BC,800H
	XOR	A
	CALL	VRMW
	LD	HL,(BASE3)
	LD	DE,255*8
	ADD	HL,DE
	EX	DE,HL
	LD	HL,CHFONTS
	LD	BC,8
	XOR	A
	CALL	M2VR
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

SAVESC:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	ERS_CS
	LD	HL,(BASE1)
	LD	DE,4000H
	LD	BC,800H
	XOR	A
	CALL	VRMW
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

LOADPR:
	LD	HL,(PSAVE)
	LD	(0F3DBH),HL
	LD	HL,(PSAVE+2)
	LD	(0F3DDH),HL
	LD	HL,(PSAVE+4)
	LD	(0F3B0H),HL
	LD	HL,(PSAVE+6)
	LD	(0FBCEH),HL
	LD	A,(PSAVE+8)
	LD	(0F3DBH),A
	LD	A,(PSAVE+9)
	LD	(0F418H),A
; LD DE,0F87FH;FUNCTIONKEY LOAD F1-F5
; LD HL,6640H
; LD BC,80
; XOR A
; CALL VR2M
	LD	HL,2800H
	LD	DE,(BASE1)
	LD	BC,800H
	XOR	A
	CALL	VRMW
	RET

SVRSM:
	LD	HL,100H
	LD	DE,1C00H
	LD	BC,400H
	XOR	A
	CALL	M2VR
	RET

LDRSM:
	LD	HL,1C00H
	LD	DE,100H
	LD	BC,400H
	XOR	A
	CALL	VR2M
	RET

FWIN:
	LD	A,(FEPBUF)
	AND	A
	JP	NZ,F_MLOAD
FWINJ0:
	LD	HL,(WIDTH)
	LD	DE,(0F3DCH)
	LD	D,0
	DEC	E
	CALL	IMULT
	LD	DE,(0F3DDH)
	DEC	E
	LD	D,0
	ADD	HL,DE
	LD	DE,(BASE)
	ADD	HL,DE
	LD	A," "
	CALL	NWV1
	EX	DE,HL
	CALL	S_CSP

	LD	DE,WCB1
	LD	HL,FEPBUF
	CALL	INPUT4
	AND	A
	JP	Z,F_MLOAD
	AND	10000000B
	JP	NZ,RFALSE
	CALL	VFILER
	JP	NZ,FWINJ0
	JP	RTRUE

RSTART:
	CALL	LOADPR
	LD	SP,(0006H)
	XOR	A
	LD	(SPLMD),A
	LD	A,1
	LD	(SYS0),A
	LD	A,22
	LD	(SYS1),A
	CALL	S_TXMD
	XOR	A
	LD	(FEPBUF),A
	JP	GOEDIT

STKCLS:
	LD	DE,SCB4
	CALL	RRST
	LD	DE,SCB5
	CALL	RRST
	LD	DE,SCB6
	CALL	RRST
	RET
;-------------------- START & END KANEL
EXTBI2:
	CALL	EXTBIO
	RET

ENDK:
	CALL	SETCOL
	CALL	SETPLT
	CALL	SETPL2
	CALL	LOADPR
	CALL	INIDR2
	CALL	ONKDV
	CALL	B_TRON
	CALL	SCNCUT
	CALL	TIMION
	RET
;---------------------------- END SUB

SETCOL:
	LD	A,(DSCLMD)
	AND	00000100B
	RET	Z
	LD	HL,6701H
	LD	DE,0F3E9H
	LD	BC,3
	XOR	A
	CALL	VR2M
	RET

SETPLT:
	LD	A,(DSCLMD)
	AND	00000011B
	CP	00000010B
	RET	NZ
	LD	HL,6704H
	LD	DE,DTA
	LD	BC,48
	XOR	A
	CALL	VR2M
	LD	HL,DTA
	CALL	SVPLT
	RET

SETPL2:
	LD	A,(DSCLMD)
	CALL	NRV1
	AND	00000001B
	RET	Z
	LD	IX,0141H
	CALL	GOBIO2
	RET

ONKDV:
	LD	A,(HOKVLD)
	AND	A
	RET	Z
	LD	D,11H
	LD	E,1
	LD	A,(DEFKDV)
	AND	A
	RET	Z
	LD	D,11H
	LD	E,1
	CALL	EXTBIO
	RET

B_TRON:
	LD	D,0
	LD	E,3
	CALL	EXTBIO
	RET

;---------------------- GO DOS!

GODOSE:;OPTION ERROR BREAK!!
	CALL	ENDK
	JP	0000H

GODOS:;QUIT EDITOR!!
	CALL	STKCLS
	JP	GODOSZ

GODOSR:;RESUME END!!
	CALL	SAVESC
	CALL	SVRSM
	JP	GODOSZ

GODOSZ:
	CALL	WAIT1
	CALL	OFFTRP
	CALL	ENDK
	JP	0000H

	END
