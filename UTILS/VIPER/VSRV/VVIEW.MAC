;VVIEW.MAC
	CSEG
	;.Z80

	PUBLIC	CLS1
	PUBLIC	RSTMS1,RSTMS2,PUTMS,EPUTMS,FPUTMS,GPUTMS
	PUBLIC	RSTMSC,SRCMSD,PUTMSD,SAVSCT,LODSCT
;PUBLIC PUTST,PUTWIN,PUTWN2,AJWIN
	PUBLIC	PUTWIN,PUTWN2,AJWIN
	PUBLIC	PUTUP,MCSTB,UPSTB,UPSTB2,PUTBAR,CSRSET,CSRRST
	PUBLIC	LINUP1,LINUP2,LINDW1,LINDW2,LINUP3,LINDW3
	PUBLIC	SCRDW1
	PUBLIC	FCPS10,BCPS10,BCPS11

	INCLUDE	VWORK.INC
	INCLUDE	RABELS.INC
	INCLUDE	SYSDRV.INC
	INCLUDE	MATHDRV.INC
	INCLUDE	VDPDRV.INC
	INCLUDE	FILEDRV.INC
	INCLUDE	VSTACK.INC
	INCLUDE	VTEXT.INC
	INCLUDE	VLH.INC
	INCLUDE	VPUT.INC
	INCLUDE	VCSR.INC
	INCLUDE	VKEY.INC
	INCLUDE	VSYS.INC
	INCLUDE	VINPBOX.INC
	INCLUDE	VEDITOR.INC
	INCLUDE	VFILER.INC

WJUNK0:	DW	0000H
WJUNK1:	DW	0000H

MSCSR:	DW	0000H

JOD:	DW	0000H
JD:	DW	0000H
JBD:	DW	0000H
JCD:	DW	0000H
JOL:	DW	0000H

CLS1:
	LD	HL,(BASE1)
	CALL	NSTW1
	LD	HL,(BASE6)
	LD	DE,(BASE1)
	AND	A
	SBC	HL,DE
	LD	A,(WPT)
	LD	C,A
	LD	B," "
CLS1J0:
	OUT(C),B
	DEC	HL
	LD	A,H
	OR	L
	JP	NZ,CLS1J0
	RET

PUTUP:
	CALL	PSTART
	LD	A,(JD)
	AND	A
	CALL	Z,PJ2
	LD	A,(JD)
	INC	A
	LD	(JD),A
	LD	HL,(JD)
	CALL	GETLH
	LD	(JOL),HL

	LD	DE,(JBD)
	LD	HL,(JCD)
	CALL	PUTTX1
	CALL	PINC
PJ3:
	CALL	PCPS
	RET	C
	LD	DE,(JBD)
	LD	HL,(JCD)
	CALL	PUTTX1
	LD	(JBD),DE
	PUSH	BC
	POP	DE
	LD	HL,(JOL)
	CALL	ICOMP2
	AND	A
	JP	NZ,PJ4
	LD	HL,(JD)
	INC	HL
	CALL	GETLH
	CALL	ICOMP2
	AND	A
	JP	NZ,PJ5
	LD	HL,(JD)
	CALL	GETLH
	CALL	ICOMP2
	AND	A
	RET	NZ
	LD	(JOL),HL
	LD	HL,(JD)
	CALL	SETLH
	LD	HL,(JCD)
	LD	DE,(WIDTH)
	ADD	HL,DE
	LD	(JCD),HL
	JP	PJ3

PSTART:
	LD	(JBD),DE
	LD	(JOD),HL
	EX	DE,HL
	CALL	GETLN
	LD	(JD),DE
	EX	DE,HL
	LD	HL,(JBD)
	AND	A
	SBC	HL,DE
	LD	DE,(WIDTH)
	ADD	HL,DE
	LD	(JCD),HL
	RET

PINC:
	LD	(JBD),DE
	LD	DE,(WIDTH)
	ADD	HL,DE
	LD	(JCD),HL
	LD	HL,(JD)
	CALL	SETLH
	RET

PCPS:
	LD	A,(JD)
	INC	A
	LD	(JD),A
	LD	D,A
	LD	A,(SYS1)
	INC	A
	CP	D
	RET

PJ2:
	LD	A,(JD)
	INC	A
	LD	(JD),A
	LD	HL,(JCD)
	LD	DE,(JBD)
	CALL	INCTX1
	CALL	PINC
	RET

PJ4:
	LD	A,(JD)
	LD	L,A
	LD	A,(SYS1)
	INC	A
	LD	H,A
	CALL	DWLH
	LD	BC,(JOL)
	LD	HL,(JD)
	CALL	SETLH
	LD	HL,(JBD)
	LD	DE,(WIDTH)
	ADD	HL,DE
	LD	(JCD),HL
	EX	DE,HL
	LD	HL,(SYS3)
	AND	A
	SBC	HL,DE
	PUSH	HL
	POP	BC
	LD	HL,(JBD)
	JP	NC,PJ6
	LD	A,(JD)
	LD	B,A
	LD	A,(SYS1)
	CP	B
	RET	C
PJ7:
	LD	BC,(JOL)
	LD	DE,(JBD)
	LD	HL,(JCD)
	CALL	PUTTX1
	RET
PJ6:
	XOR	A
	CALL	VRMW
	JP	PJ7

PJ5:
	LD	HL,(JBD)
	LD	DE,(WIDTH)
	ADD	HL,DE
	EX	DE,HL
	LD	HL,(SYS3)
	AND	A
	SBC	HL,DE
	PUSH	HL
	POP	BC
	EX	DE,HL
	LD	DE,(JBD)
	JP	C,PJ8
	XOR	A
	CALL	VRMW
PJ8:
	LD	A,(JD)
	LD	L,A
	LD	A,(SYS1)
	LD	H,A
	CALL	UPLH
	LD	HL,(SYS1)
	CALL	GETLH
	PUSH	HL
	POP	BC
	LD	DE,(SYS4)
	LD	HL,(SYS3)
	CALL	PUTTX1
	LD	HL,(SYS1)
	INC	HL
	CALL	SETLH
	RET

PUTBAR:
	LD	A,(SCHMD)
	CP	1
	JP	Z,FSTB
	CP	2
	RET	Z
	JP	EDSTB

UPSTB:
	LD	HL,(SYS7)
	CALL	NSTW1
	LD	DE,(WIDTH)
	LD	HL,STBAR
	CALL	VM2V
	RET

UPSTB2:
	LD	HL,(SYS7)
	CALL	NSTW1
	LD	DE,25
	LD	HL,STBAR
	CALL	VM2V
	RET

;EDIT TEXT HEAD
EDSTB:
	LD	A,16
	LD	(STBAR),A
	LD	HL,STBAR
	LD	DE,STBAR+1
	LD	BC,79
	LDIR
	LD	HL,FCB5
	LD	DE,SUBDTA
	CALL	FCB2TX
	LD	C,A
	LD	B,0
	PUSH	BC
	LD	HL,(WIDTH)
	AND	A
	SBC	HL,BC
	DEC	HL
	DEC	HL
	LD	DE,STBAR
	ADD	HL,DE
	EX	DE,HL
;---------------
	LD	A,"["
	LD	(DE),A
	INC	DE
	LD	HL,SUBDTA
	POP	BC
	LD	A,C
	AND	A
	JP	Z,P3J3
	LDIR
P3J3:
	LD	A,"]"
	LD	(DE),A
	CALL	MCSTB
	CALL	UPSTB
	LD	A,(SCTMD)
	AND	A
	RET	NZ

	LD	B,93
	LD	A,(BASE7)
	AND	A
	JP	Z,P3J4
	LD	B,94
P3J4:
	LD	HL,(BASE5)
	CALL	RSTMSC
	LD	A,B
	CALL	PUTMS
	RET

MCSTB:
	LD	HL,(CSP1)
	LD	DE,(CSP10)
	ADD	HL,DE
	LD	DE,STBAR+1
	LD	A,5
	CALL	HL2TXS
	LD	A,11
	LD	(STBAR+6),A
	LD	A,32
	LD	(STBAR+8),A
	LD	HL,(CSP0)
	INC	HL
	LD	DE,STBAR+7
	CALL	HL2TX
	CALL	MEDSTL
	LD	HL,EDSTL+12
	LD	DE,STBAR+11
	LD	A,7
	CALL	DH2TXS
	LD	A,20
	LD	HL,STBAR
	CALL	XCHSTB
	RET

XCHSTB:
	LD	B,A
XCSJ2:
	LD	A,(HL)
	CP	32
	JP	Z,XCSJ0
	CP	"0"
	JP	C,XCSJ1
	CP	"9"+1
	JP	NC,XCSJ1
	SUB	"0"-1
	LD	(HL),A
XCSJ1:
	INC	HL
	DJNZ	XCSJ2
	RET
XCSJ0:
	LD	A,16
	LD	(HL),A
	JP	XCSJ1

;FILER HEAD
FSTB:
	LD	B,91
	LD	A,(BASE7)
	AND	A
	JP	Z,FSTBJ0
	LD	B,92
FSTBJ0:
	LD	HL,(SYS7)
	CALL	RSTMSC
	LD	A,B
	CALL	PUTMS
	LD	HL,(SYS3)
	CALL	RSTMSC
	LD	B,5
	LD	A,(BASE7)
	AND	A
	JP	Z,FSTBJ1
	LD	B,6
FSTBJ1:
	LD	A,B
	CALL	EPUTMS
	RET

CSRRST:
	LD	DE,(SYS2)
	LD	(OCSRP),DE
	CALL	S_CSP
	CALL	S_CSP9
	LD	A,(EDFLG)
	OR	00000100B
	LD	(EDFLG),A
	RET

CSRSET:
	LD	A,(EDFLG)
	AND	00000100B
	JP	Z,CSRRST
	LD	HL,(CSRP)
	LD	DE,(SYS2)
	CALL	ICOMP0
	JP	NZ,CSRRST
	LD	DE,(CSRP)
	LD	HL,(SYS3)
	CALL	ICOMP1
	JP	NZ,CSRRST

	LD	(OCSRP),DE
	CALL	S_CSP
	RET

FCPS10:
	LD	DE,(LHEAD)
	LD	HL,(LHEAD+2)
	AND	A
	SBC	HL,DE
	PUSH	HL
	CALL	M2T
	POP	DE
	CALL	WHD
	LD	HL,(CSP10)
	INC	HL
	LD	(CSP10),HL
	LD	L,0
	LD	A,(SYS1)
	LD	H,A
	CALL	S_LHEAD
	CALL	UPLH
	RET

BCPS10:
	CALL	BCJ0
	LD	L,1
	LD	A,(SYS1)
	INC	A
	LD	H,A
	CALL	DWLH
	CALL	S_LHEAD
	RET

BCPS11:
	CALL	BCJ0
	LD	HL,(LHEAD)
	LD	(LHEAD+2),HL
	CALL	S_LHEAD
	RET

BCJ0:
	LD	HL,(CSP10)
	DEC	HL
	LD	(CSP10),HL
	CALL	RHD
	EX	DE,HL
	CALL	T2M
	RET

LINUP1:
	CALL	BCPS10
	CALL	SCRDW1
	LD	HL,(CSRP)
	LD	DE,(WIDTH)
	ADD	HL,DE
	EX	DE,HL
	CALL	S_CSP
	CALL	LINPUT
	RET

LINDW1:
	CALL	FCPS10
	CALL	SCRUP1
	LD	HL,(CSRP)
	LD	DE,(WIDTH)
	AND	A
	SBC	HL,DE
	EX	DE,HL
	CALL	S_CSP
	CALL	LINPUT
	RET

LINUP3:
	CALL	BCPS10
	LD	HL,(CSRP)
	LD	DE,(WIDTH)
	ADD	HL,DE
	EX	DE,HL
	CALL	S_CSP
	CALL	LINPU2
	RET

LINDW3:
	CALL	FCPS10
	LD	HL,(CSRP)
	LD	DE,(WIDTH)
	AND	A
	SBC	HL,DE
	EX	DE,HL
	CALL	S_CSP
	CALL	LINPU2
	RET

LINUP2:
	LD	A,(EDFLG)
	AND	00001000B
	JP	Z,LU2J0
	LD	DE,(SYS2)
	CALL	S_CSP
	CALL	S_CSP9
	RET
LU2J0:
	CALL	BCPS11
	LD	DE,(CSRP)
	LD	HL,(WIDTH)
	ADD	HL,DE
	EX	DE,HL
	CALL	S_CSP
	RET

LINDW2:
	LD	A,(EDFLG)
	AND	00001000B
	JP	Z,LD2J0
	LD	DE,(SYS3)
	DEC	DE
	DEC	DE
	CALL	S_CSP
	CALL	S_CSP9
	RET
LD2J0:
	LD	HL,(JUNK3)
	LD	DE,(WIDTH)
	AND	A
	SBC	HL,DE
	LD	(JUNK3),HL
	LD	HL,(CSRP)
	AND	A
	SBC	HL,DE
	EX	DE,HL
	CALL	S_CSP
	CALL	FCPS10
	LD	HL,(JUNK3)
	CALL	SCRUP2
	LD	DE,(CSRP)
	CALL	S_CSP
	RET

LINPUT:
	LD	BC,(CSP4)
	LD	DE,(CSP2)
	LD	A,(FCSMD)
	AND	A
	JP	NZ,LPTJ1
	LD	HL,(CSP9)
	ADD	HL,DE
	CALL	PUTTX1
	PUSH	BC
	EXX
	POP	DE
	LD	HL,(MEM10)
	CALL	ICOMP0
	EXX
	AND	A
	JP	NZ,LPTJ3
	CALL	ICOMP0
	AND	A
	JP	NZ,LPTJ0
	CALL	S_CSP
LPTJ1:
	LD	HL,(CSP6)
	CALL	ICOMP1
	AND	A
	JP	NZ,LPTJ2
	CALL	PUTTX1
LPTJ2:
	LD	HL,(CSP1)
	INC	HL
	CALL	SETLH
	RET
LPTJ0:
	PUSH	DE
	LD	DE,(COLDDE)
	CALL	S_CSP
	POP	DE
	JP	LPTJ1

LPTJ3:
	LD	BC,(CSP4)
	LD	DE,(CSP2)
	LD	HL,(MEM10)
	CALL	INCTX2
	CALL	S_CSP
	JP	LPTJ1

LINPU2:
	LD	BC,(CSP4)
	LD	DE,(CSP2)
	LD	HL,(CSP9)
	ADD	HL,DE
	CALL	INCTX1
	PUSH	BC
	EXX
	POP	DE
	LD	HL,(MEM10)
	CALL	ICOMP0
	EXX
	AND	A
	JP	NZ,LPT2J3
	CALL	ICOMP0
	AND	A
	JP	NZ,LPT2J0
	CALL	S_CSP
LPT2J1:
	LD	HL,(CSP6)
	CALL	ICOMP1
	AND	A
	JP	NZ,LPT2J2
	CALL	INCTX1
LPT2J2:
	LD	HL,(CSP1)
	INC	HL
	CALL	SETLH
	RET
LPT2J0:
	PUSH	DE
	LD	DE,(COLDDE)
	CALL	S_CSP
	POP	DE
	JP	LPT2J1
LPT2J3:
	LD	BC,(CSP4)
	LD	DE,(CSP2)
	LD	HL,(MEM10)
	CALL	INCTX2
	CALL	S_CSP
	JP	LPT2J1

SCRDW1:
	CALL	SCJ0
	EX	DE,HL
	CALL	VRMW
	RET

SCRUP1:
	CALL	SCJ0
	CALL	VRMW
	RET

SCRUP2:
	LD	DE,(SYS2)
	AND	A
	SBC	HL,DE
	PUSH	HL
	CALL	SCJ0
	POP	BC
	CALL	VRMW
	RET

SCJ0:
	CALL	ERS_CS
	LD	DE,(SYS2)
	LD	HL,(SYS8)
	XOR	A
	LD	BC,(SYS5)
	RET

LODSCT:
	LD	DE,(BASE5)
	LD	BC,(WIDTH0)
	XOR	A
	CALL	M2VR
	RET

SAVSCT:
	LD	HL,(BASE5)
	LD	BC,(WIDTH0)
	XOR	A
	CALL	VR2M
	RET

RSTMS1:
	LD	A,(SYS12)
	AND	A
	LD	DE,SVDTA1
	CALL	Z,SAVSCT
	LD	A,0FFH
	LD	(SYS12),A
	LD	HL,(BASE5)
	CALL	RSTMSC
	RET

RSTMS2:
	LD	DE,SVDTA2
	CALL	SAVSCT
	LD	HL,(BASE5)
	CALL	RSTMSC
	RET

PUTMS:
	PUSH	DE
	LD	DE,MSD
	CALL	SRCMSD
	CALL	PUTMSD
	POP	DE
	RET

GPUTMS:
	PUSH	AF
	CALL	RSTMS1
	POP	AF
FPUTMS:
	PUSH	AF
	LD	HL,(BASE5)
	CALL	RSTMSC
	POP	AF
EPUTMS:
	LD	DE,CND
	CALL	SRCMSD
EPMSJ0:
	LD	A,(DE)
	AND	A
	RET	Z
	CALL	PUTMS
	INC	DE
	JP	EPMSJ0

RSTMSC:
	LD	(MSCSR),HL
	RET

PUTMSD:
	LD	HL,(MSCSR)
	CALL	NSTW1
	LD	A,(WPT)
	LD	C,A
PMSDJ0:
	LD	A,(DE)
	CP	0
	RET	Z
	CP	13
	JP	Z,PMSRET
	CP	14
	JP	Z,PMSLIN
	CP	26
	JP	Z,PMSEOF
	OUT(C),A
	INC	DE
	LD	HL,(MSCSR)
	INC	HL
	LD	(MSCSR),HL
	JP	PMSDJ0

PMSRET:
	LD	A," "
	CALL	PMSEND
	JP	PMSDJ0

PMSLIN:
	LD	A,"-"
	CALL	PMSEND
	JP	PMSDJ0

PMSEOF:
	LD	A,(FKLCH)
	CALL	PMSEND
	JP	PMSDJ0

PMSEND:
	PUSH	DE
	PUSH	AF
	LD	DE,(MSCSR)
	LD	HL,(WIDTH0)
	CALL	IMOD
	EX	DE,HL
	LD	HL,(WIDTH0)
	AND	A
	SBC	HL,DE
	LD	B,L
	LD	DE,(MSCSR)
	ADD	HL,DE
	LD	(MSCSR),HL
	LD	A,(WPT)
	LD	C,A
	POP	AF
PSEDJ0:
	OUT	(C),A
	DJNZ	PSEDJ0
	POP	DE
	INC	DE
	RET

SRCMSD:
	AND	A
	RET	Z
PMDJ1:
	LD	B,A
PMDJ0:
	LD	A,(DE)
	INC	DE
	AND	A
	JP	Z,PMDJ2
	JP	PMDJ0
PMDJ2:
	DJNZ	PMDJ0
	RET

AJWIN:
	PUSH	DE
	PUSH	BC
	PUSH	BC
	LD	A,H
	AND	10000000B
	JP	NZ,PWJ5
	LD	DE,(SYS7)
	CALL	ICOMP0
	JP	Z,PWJ4
PWJ5:
	LD	HL,(SYS2)
PWJ4:
	LD	DE,(BASE)
	AND	A
	SBC	HL,DE
	EX	DE,HL
	LD	HL,(WIDTH)
	CALL	IMOD
	LD	(WJUNK0),HL
	LD	(WJUNK1),DE

	LD	DE,(WIDTH)
	DEC	DE
	AND	A
	SBC	HL,DE
	LD	A,L
	OR	H
	JP	NZ,PWJ0

;AJX<-
	LD	DE,0
	LD	(WJUNK0),DE
	LD	HL,(WJUNK1)
	INC	HL
	LD	(WJUNK1),HL

PWJ0:
	LD	DE,(WJUNK1)
	LD	A,E
	OR	D
	POP	BC
	JP	NZ,PWJ1

;AJY DOWN
	LD	DE,1
	LD	(WJUNK1),DE

PWJ1:
	LD	HL,(WJUNK0)
	LD	E,B
	LD	D,0
	ADD	HL,DE
	INC	HL
	INC	HL
	LD	DE,(WIDTH)
	AND	A
	SBC	HL,DE
	JP	C,PWJ2

;AJX<-
	LD	HL,(WIDTH)
	LD	E,B
	LD	D,0
	AND	A
	SBC	HL,DE
	DEC	HL
	DEC	HL
	LD	(WJUNK0),HL
PWJ2:
	LD	HL,(WJUNK1)
	LD	E,C
	LD	D,0
	ADD	HL,DE
	INC	HL
	LD	DE,(BASE4)
	DEC	DE
	AND	A
	SBC	HL,DE
	JP	C,PWJ3

	LD	HL,(BASE4)
	DEC	HL
	DEC	HL
	LD	E,C
	LD	D,0
	AND	A
	SBC	HL,DE
	DEC	HL
	LD	(WJUNK1),HL
PWJ3:
	LD	HL,(WJUNK1)
	LD	DE,(WIDTH)
	CALL	IMULT
	LD	DE,(WJUNK0)
	ADD	HL,DE
	LD	DE,(BASE)
	ADD	HL,DE
	POP	BC
	POP	DE
	RET

PUTWIN:
	CALL	MWDTA
	CALL	PWDTA
	RET

PUTWN2:
	CALL	MWDTA
	CALL	MBOARD
	CALL	PWDTA
	RET

MBOARD:
	PUSH	HL
	PUSH	DE
	PUSH	BC
	DEC	C
	DEC	C
	LD	HL,DTA
	LD	E,B
	LD	D,0
	ADD	HL,DE
	INC	HL
MBDJ0:
	PUSH	HL
MBDJ2:
	LD	A,(IX)
	INC	IX
	AND	A
	JP	Z,MBDJ1
	LD	(HL),A
	INC	HL
	JP	MBDJ2
MBDJ1:
	POP	HL
	ADD	HL,DE
	DEC	C
	JP	NZ,MBDJ0
	POP	BC
	POP	DE
	POP	HL
	RET

MWDTA:
	PUSH	HL
	PUSH	BC
	PUSH	DE
	LD	D,B
	LD	HL,DTA
	LD	A,18H
	LD	(HL),A
	INC	HL
	LD	A,17H
PTWJ0:
	LD	(HL),A
	INC	HL
	DJNZ	PTWJ0
	LD	A,19H
	LD	(HL),A
	INC	HL

PTWJ1:
	LD	A,16H
	LD	(HL),A
	INC	HL
	LD	B,D
	LD	A," "
PTWJ2:
	LD	(HL),A
	INC	HL
	DJNZ	PTWJ2
	LD	A,16H
	LD	(HL),A
	INC	HL
	DEC	C
	JP	NZ,PTWJ1

	LD	A,1AH
	LD	(HL),A
	INC	HL
	LD	B,D
	LD	A,17H
PTWJ3:
	LD	(HL),A
	INC	HL
	DJNZ	PTWJ3
	LD	A,1BH
	LD	(HL),A

	POP	DE
	LD	A,E
	LD	DE,MSD
	CALL	SRCMSD
	LD	HL,DTA+3
PTWJ4:
	LD	A,(DE)
	AND	A
	JP	Z,PTWJ5
	LD	(HL),A
	INC	HL
	INC	DE
	JP	PTWJ4

PTWJ5:
	POP	BC
	INC	B
	INC	B
	INC	C
	INC	C
	POP	HL
	RET

PWDTA:
	LD	DE,DTA
PTWJ6:
	PUSH	BC
	CALL	NSTW1
	EX	DE,HL
	LD	A,(WPT)
	LD	C,A
	OTIR
	EX	DE,HL
	LD	BC,(WIDTH)
	ADD	HL,BC
	POP	BC
	DEC	C
	JP	NZ,PTWJ6
	RET

	END
