;VCSR.MAC
	CSEG
	;.Z80

	PUBLIC	ERS_CS,MVCSR,S_CSP9,S_CSP,FW_CS,BK_CS
	PUBLIC	CSRWKS,CSRWK2,PUTSL,RSTFNK

	INCLUDE	VWORK.INC
	INCLUDE	RABELS.INC
	INCLUDE	SYSDRV.INC
	INCLUDE	MATHDRV.INC
	INCLUDE	VDPDRV.INC
	INCLUDE	FILEDRV.INC
	INCLUDE	VKEY.INC
	INCLUDE	VSTACK.INC
	INCLUDE	VTEXT.INC
	INCLUDE	VLH.INC
	INCLUDE	VPUT.INC
	INCLUDE	VVIEW.INC
	INCLUDE	VEDITOR.INC

PUT_CS:
	PUSH	IX
	LD	IX,CSRCB3
	CALL	SETPOS
	AND	A
	JP	NZ,PCJ0
	CALL	PUTC
PCJ0:
	LD	IX,CSRCB2
	CALL	SETPOS
	AND	A
	JP	NZ,PCJ1
	CALL	PUTC
PCJ1:
	LD	IX,CSRCB1
	CALL	PUTC
	POP	IX
	RET

ERS_CS:
	PUSH	IX
	LD	IX,CSRCB1
	CALL	ERSC
	LD	IX,CSRCB2
	CALL	ERSC
	LD	IX,CSRCB3
	CALL	ERSC
	POP	IX
	RET

ERSC:
	LD	A,(IX+10)
	AND	A
	RET	Z
	LD	L,(IX+2)
	LD	H,(IX+3)
	CALL	NRV1
	XOR	(IX+8)
	RET	NZ
	LD	A,(IX+7)
	CALL	NWV1
	RET

PUTC:
	LD	A,(IX+9)
	AND	A
	RET	Z
	LD	L,(IX)
	LD	H,(IX+1)
	CALL	NRV1
	LD	C,(IX+8)
	CP	C
	JP	Z,A1J
	LD	(IX+7),A
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,(BASE3)
	ADD	HL,BC
	CALL	NSTR1
	LD	A,(RPT)
	LD	C,A
	LD	HL,CHDTA
	LD	B,8
	INIR
	LD	HL,(BASE3)
	LD	C,(IX+4)
	LD	B,(IX+5)
	ADD	HL,BC
	CALL	NSTW1
	LD	A,(WPT)
	LD	C,A
	LD	A,(IX+6)
	DEC	A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	L,A
	LD	H,0
	LD	DE,CHFONTS
	ADD	HL,DE
	LD	DE,CHDTA
	LD	B,8
A1:
	LD	A,(DE)
	XOR	(HL)
	OUT(C),A
	INC	HL
	INC	DE
	DJNZ	A1
	LD	A,(IX+8)
	LD	L,(IX)
	LD	H,(IX+1)
	CALL	NWV1
A1J:
	LD	(IX+2),L
	LD	(IX+3),H
	RET

SETPOS:
	LD	A,(IX+9)
	AND	A
	RET	Z
	LD	L,(IX+12)
	LD	H,(IX+13)
	LD	DE,(CSP10)
	AND	A
	SBC	HL,DE
	LD	A,L
	OR	H
	JP	Z,SPJ1
	LD	DE,(SYS1)
	INC	DE
	PUSH	HL
	AND	A
	SBC	HL,DE
	POP	HL
	JP	NC,SPJ1
	LD	DE,(WIDTH)
	CALL	IMULT
	LD	DE,(SYS7)
	ADD	HL,DE
	LD	E,(IX+11)
	LD	D,0
	ADD	HL,DE
	LD	(IX),L
	LD	(IX+1),H
	XOR	A
	RET
SPJ1:
	LD	A,0FFH
	RET

MVCSR:
	LD	HL,(CSRP)
	LD	(CSRCB1),HL
	LD	A,(INSMD)
	INC	A
	LD	(CSRCB1+6),A
	CALL	PUTFNK
	CALL	ERS_CS
	CALL	PUT_CS
	CALL	PUTSL
	RET

RSTFNK:
	LD	HL,0FFFFH
	LD	(LHEAD+62),HL
	RET


PUTFNK:
	LD	A,(SCTMD)
	AND	A
	RET	Z
	LD	HL,(SYS3)
	LD	DE,(BASE5)
	CALL	ICOMP2
	AND	A
	RET	Z
	LD	HL,(SYS1)
	INC	HL
	CALL	GETLH
	LD	DE,(LHEAD+62)
	CALL	ICOMP2
	AND	A
	RET	NZ
	LD	(LHEAD+62),HL
	LD	B,H
	LD	C,L
	LD	DE,(BASE5)
	LD	HL,(BASE6)
	CALL	PUTTX1
	RET

PUTSL:
	LD	A,(SCHMD)
	AND	A
	RET	NZ
	CALL	MCSTB
	CALL	UPSTB2
	RET

S_CSP9:
	LD	HL,(CSP0)
	LD	(CSP9),HL
	RET

S_CSP:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	(CSRP),DE
	EX	DE,HL
	CALL	GETLN
	LD	(CSP0),HL
	LD	(CSP1),DE

	LD	HL,(CSP10)
	ADD	HL,DE
	LD	(CSP3),HL

	LD	HL,(CSRP)
	LD	DE,(CSP0)
	AND	A
	SBC	HL,DE
	LD	(CSP2),HL

	PUSH	HL
	LD	DE,(WIDTH)
	ADD	HL,DE
	LD	(CSP6),HL
	POP	HL
	AND	A
	SBC	HL,DE
	LD	(CSP11),HL

	LD	HL,(CSP1)
	PUSH	HL
	PUSH	HL
	CALL	GETLH
	LD	(CSP4),HL
	POP	HL
	DEC	HL
	CALL	GETLH
	LD	(CSP12),HL
	POP	HL
	INC	HL
	CALL	GETLH
	LD	(CSP13),HL

	LD	B,0FFH
	LD	DE,(CSP4)
	LD	HL,(MEM10)
	CALL	ICOMP0
	NEG
	ADD	A,B
	LD	B,A
	LD	HL,(CSP1)
	INC	HL
	CALL	GETLH
	EX	DE,HL
	LD	HL,(MEM10)
	CALL	ICOMP0
	NEG
	ADD	A,B
	LD	(CSP5),A

	LD	HL,(CSRP)
	LD	DE,(SYS2)
	CALL	ICOMP0
	LD	(CSP7),A
	LD	DE,(CSRP)
	LD	HL,(SYS3)
	LD	A,(EDFLG)
	AND	00001000B
	JP	Z,SCSPJ0
	DEC	HL
SCSPJ0:
	CALL	ICOMP1
	LD	(CSP8),A
	POP	HL
	POP	DE
	POP	BC
	RET

FW_CS:
	CALL	GTXTAD
	CALL	S_CSP
	CALL	S_CSP9
	RET

BK_CS:
	CALL	GTXTAD
	PUSH	BC
	POP	DE
	LD	A,(TXTF2)
	AND	A
	CALL	NZ,BJ0
	LD	HL,(MEM10)
	CALL	ICOMP0
	AND	A
	JP	NZ,BJ1
	PUSH	DE
	POP	BC
	RET

BJ0:
	EX	DE,HL
	LD	DE,(COLDDE)
	CALL	S_CSP
	LD	DE,(TXTF0)
	AND	A
	SBC	HL,DE
	EX	DE,HL
	RET

BJ1:
	LD	A,(CSP5)
	CP	1
	JP	Z,BJ2
	LD	BC,(CSP4)
	LD	DE,(CSP2)
	CALL	INCTX2
	CALL	S_CSP
	LD	BC,(MEM10)
	RET
BJ2:
	LD	BC,(LHEAD)
	LD	DE,(SYS7)
	LD	HL,(MEM10)
	CALL	INCTX2
	CALL	S_CSP
	CALL	S_CSP9
	LD	BC,(MEM10)
	RET

CSRWK2:
	LD	A,(XCMD)
	AND	A
	JP	NZ,CSW2J0
	LD	A,(CSP7)
	AND	A
	JP	NZ,CSJ11
	LD	A,(CSP8)
	AND	A
	JP	NZ,CSJ12
	JP	CSJ3
CSW2J0:
CSRWKS:
	LD	A,(CSP7)
	AND	A
	JP	NZ,CSJ1
	LD	A,(CSP8)
	AND	A
	JP	NZ,CSJ2

	LD	A,(FCSMD)
	AND	A
	JP	Z,CSJ3

CSJ4:
	LD	HL,(CSRP)
	LD	(OCSRP),HL
	LD	DE,(CSRP)
	CALL	S_CSP
	RET

CSJ1:;LINUP!
	LD	HL,(CSP10)
	LD	DE,0000H
	AND	A
	SBC	HL,DE
	JP	Z,CSJ5
	CALL	LINUP1
	JP	CSJ4

CSJ2:;LINDW!
	LD	HL,(SYS1)
	INC	HL
	CALL	GETLH
	EX	DE,HL
	LD	HL,(MEM10)
	CALL	ICOMP0
	AND	A
	JP	NZ,CSJ5
	CALL	LINDW1
	JP	CSJ4

CSJ3:;NONFREE!
	LD	HL,(CSP2)
	LD	DE,(CSP9)
	ADD	HL,DE
	EX	DE,HL
	CALL	S_CSP
	CALL	BK_CS
	JP	CSJ4

CSJ5:
	LD	DE,(OCSRP)
	CALL	S_CSP
	JP	CSJ4

CSJ11:;LINUP!
	LD	HL,(CSP10)
	LD	DE,0000H
	AND	A
	SBC	HL,DE
	JP	Z,CSJ5
	CALL	LINUP3
	JP	CSJ4

CSJ12:;LINDW!
	LD	HL,(SYS1)
	INC	HL
	CALL	GETLH
	EX	DE,HL
	LD	HL,(MEM10)
	CALL	ICOMP0
	AND	A
	JP	NZ,CSJ5
	CALL	LINDW3
	JP	CSJ4

	END
