;VFILER
	;.z80
	CSEG

	PUBLIC	VFILER,FRCOM,FRRET,FRDEL,FRSORT,FRREN,FRLDRV

	INCLUDE	VWORK.INC
	INCLUDE	RABELS.INC
	INCLUDE	SYSDRV.INC
	INCLUDE	MATHDRV.INC
	INCLUDE	VDPDRV.INC
	INCLUDE	FILEDRV.INC
	INCLUDE	VKEY.INC
	INCLUDE	VSTACK.INC
	INCLUDE	VTEXT.INC
	INCLUDE	VLH.INC
	INCLUDE	VCSR.INC
	INCLUDE	VVIEW.INC
	INCLUDE	VPUT.INC
	INCLUDE	VSYS.INC
	INCLUDE	VEDITOR.INC
	INCLUDE	VINPBOX.INC
	INCLUDE	VJUMP.INC

JUNK0:	DW	0000H
IJUNK:	DW	0000H
JJUNK:	DW	0000H
KJUNK:	DW	0000H
MCONT:	DB	00H
AJUNK:	DB	00H

FRCOMD:	DB	"  dDrRsSlL",13,13,0
FRMAP:	DW	FRMARK,FRDEL,FRREN,FRSORT,FRLDRV,FRMARK

FRCOM:
	LD	A,(FEPBUF+1)
	LD	HL,FRCOMD
	CALL	TXMACH
	AND	A
	RET	Z
	DEC	A
	SRL	A
	LD	IY,FRMAP
	CALL	MPJUMP

FRREAD:
	CALL	LREAD
	LD	DE,(CSP4)
	LD	HL,(MEM10)
	CALL	ICOMP1
	RET

FRJUNK:	DW	0000H
SJUNK0:	DW	0000H
SJUNK1:	DW	0000H

FRRET:
	XOR	A
	LD	(FEPBUF),A
	LD	HL,FEPBUF+1
	LD	(FRJUNK),HL

	CALL	AMKSRC
	JP	NZ,FRRTJ2
FRRTJ5:
	CALL	FRRTJ0
	JP	NZ,IBXRET
	CALL	MKSRCH
	JP	Z,FRRTJ5
	JP	IBXRET

FRRTJ2:
	CALL	FRRTJ0
	JP	IBXRET

FRRTJ0:
	CALL	FRREAD
	JP	NZ,RFALSE
	LD	A,12
	LD	HL,EXCDTA+4
	LD	DE,(FRJUNK)
	CALL	TX2FCB

	LD	A,(FEPBUF)
	INC	A
	LD	(FEPBUF),A
	LD	BC,(MAXTXT)
	CP	C
	JP	NC,RFALSE
	LD	HL,(FRJUNK)
	LD	DE,12
	ADD	HL,DE
	LD	(FRJUNK),HL
	JP	RTRUE

FRSPUT:
; LD A,0FFH
; LD (SLTMD),A
	CALL	FRRPUT
; XOR A
; LD (SLTMD),A


; CALL JPOINT
	RET

FRLDRV:
	LD	A,40
	CALL	GPUTMS
	CALL	WAIT3
	LD	A,50
	JP	NZ,GPUTMS
	PUSH	BC
	LD	A,B
	CALL	CLGDRV
	POP	BC
	LD	A,49
	JP	NZ,GPUTMS
	LD	A,B
	LD	(DEFDRV),A
	JP	FRSPUT

FRAFE:
	LD	A,42
	CALL	GPUTMS
	CALL	WAIT2
	RET	NZ
	CALL	FXDRV
	CALL	FATERS
	CALL	DIRERS
	CALL	SFAT
	LD	DE,DTA
	CALL	SDMA
	CALL	S_DIR
	CALL	FRSPUT
	RET

FATERS:
	LD	HL,2
	LD	BC,(CLSNUM)
	DEC	BC
	DEC	BC
FRAFEJ0:
	PUSH	HL
	PUSH	BC
	LD	DE,0
	CALL	WFAT
	POP	BC
	POP	HL
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JP	NZ,FRAFEJ0
	RET

DIRERS:
	LD	HL,DTA
	LD	DE,DTA+1
	LD	BC,1000H
	XOR	A
	LD	(HL),A
	LDIR
	RET

FRDEL:
	XOR	A
	LD	(AJUNK),A
	CALL	AMKSRC
	JP	NZ,FRDLJ1
FRDLJ5:
	CALL	SNSSTP
	JP	Z,FRSPUT
	CALL	FRDLJ0
	CALL	MKSRCH
	JP	Z,FRDLJ5

	CALL	FRSPUT
	RET
FRDLJ1:
	CALL	FRDLJ0
	JP	Z,FRSPUT
	RET

FRUNDL:
	XOR	A
	LD	(AJUNK),A
	LD	A,53
	CALL	FRYNA
	LD	A,55
	JP	NZ,GPUTMS
	LD	A,54
	CALL	GPUTMS
	CALL	MARKJ
	CALL	L_DIR
	LD	HL,DTA
	LD	A,(DENTNM)
	LD	B,A
FRUDJ0:
	LD	A,(HL)
	CP	0E5H
	CALL	Z,FRUDJ15
	LD	DE,32
	ADD	HL,DE
	DJNZ	FRUDJ0

	CALL	S_DIR
	CALL	SFAT
	LD	DE,DTA
	CALL	SDMA
	LD	A,57
	CALL	GPUTMS
	JP	FRSPUT

FRUDJ15:
	PUSH	HL
	CALL	Z,FRUDJ1;FAT CHECK
	POP	HL
	RET	NZ
	LD	A,"$"
	LD	(HL),A
	RET

FRUDJ1:
	PUSH	HL
	POP	IX
	LD	L,(IX+26)
	LD	H,(IX+27)
	LD	(UJUNK0),HL
	LD	E,(IX+29)
	LD	D,(IX+30)
	LD	(UJUNK2),DE

	LD	HL,(UJUNK0)
	CALL	RFAT
	LD	A,L
	OR	H
	JP	NZ,RFALSE
	CALL	FRUDJ2
	JP	NZ,RFALSE
	JP	RTRUE

UJUNK0:	DW	0000H
UJUNK1:	DW	0000H
UJUNK2:	DW	0000H

FRUDJ2:
	LD	HL,(UJUNK0)
	INC	HL
	LD	(UJUNK1),HL
	LD	DE,(UJUNK2)
	LD	HL,4
	CALL	IMOD
	LD	A,E
	OR	D
	JP	Z,FRUDJ4X
	LD	A,L
	OR	H
	JP	Z,FRUDJ4
FRUDJ4X:
	INC	DE
FRUDJ4:
	LD	(UJUNK2),DE

FRUDJ3:
	LD	HL,(UJUNK2)
	DEC	HL
	LD	A,L
	OR	H
	JP	Z,FRUDJ5

	LD	DE,(UJUNK1)
	LD	HL,713
	CALL	ICOMP1
	JP	NZ,FRUDJ5

	LD	HL,(UJUNK1)
	CALL	RFAT
	LD	A,L
	OR	H
	CALL	Z,FRUDJ10
	LD	DE,(UJUNK1)
	INC	DE
	LD	(UJUNK1),DE
	JP	FRUDJ3

FRUDJ10:
	LD	HL,(UJUNK0)
	LD	DE,(UJUNK1)
	CALL	WFAT
	LD	HL,(UJUNK1)
	LD	(UJUNK0),HL
	LD	HL,(UJUNK2)
	DEC	HL
	LD	(UJUNK2),HL
	RET

FRUDJ5:
	LD	DE,0FFFH
	LD	HL,(UJUNK0)
	CALL	WFAT
	JP	RTRUE

FRREN:
	CALL	RSTMS1
	XOR	A
	LD	(AJUNK),A
	CALL	AMKSRC
	JP	NZ,FRRNJ1
FRRNJ5:
	CALL	SNSSTP
	JP	Z,FRSPUT
	CALL	FRRNJ0
	CALL	MKSRCH
	JP	Z,FRRNJ5

	CALL	FRSPUT
	RET
FRRNJ1:
	CALL	FRRNJ0
	JP	Z,FRSPUT
	RET

FSD0:	DW	0000H
FSD1:	DW	0000H
FSD2:	DW	0000H

FRSORT:
	LD	A,(MCONT)
	AND	A
	LD	A,52
	JP	Z,GPUTMS
	LD	A,51
	CALL	GPUTMS
	CALL	MARKJ
;--------------- FORWARD
	LD	HL,(CSP3)
	LD	(FSD0),HL
	LD	HL,FEPBUF
	LD	(FSD1),HL

	LD	HL,EXCDTA+100H
	LD	DE,EXCDTA+101H
	XOR	A
	LD	(HL),A
	LD	(FSD2),HL
	LD	BC,256
	LDIR

	LD	A,0FFH
	LD	(SLTMD),A
	CALL	J_HEAD
	XOR	A
	LD	(SLTMD),A

	CALL	FRSJ0
;--------------- MARK DATA 
	CALL	AMKSRC
	JP	NZ,FRSPUT
FRSJ3:
	CALL	FRSJ2
	CALL	MKSRCH
	JP	Z,FRSJ3
;--------------- BEHIND
	LD	HL,(FSD2)
	LD	A,(DENTNM)
	LD	B,A
	LD	C,0
FRSJ10:
	PUSH	BC
	PUSH	HL
	LD	A,(HL)
	AND	A
	CALL	Z,FRSJ11
	POP	HL
	INC	HL
	POP	BC
	INC	C
	DJNZ	FRSJ10
;--------------- MAKE!
	CALL	L_DIR
	CALL	FRSJ5
	CALL	S_DIR
	LD	A,56
	CALL	GPUTMS
	JP	FRSPUT

FRSJ11:
	LD	A,C
	LD	HL,(FSD1)
	LD	(HL),A
	INC	HL
	LD	(FSD1),HL
	RET

FRSJ5:
	LD	HL,FEPBUF
	LD	DE,ASTDTA+32
	LD	BC,224
	LDIR

	XOR	A
	LD	(DTA+2000H),A
	LD	HL,DTA+2000H
	LD	DE,DTA+2001H
	LD	BC,1800H
	LDIR

	LD	A,(DENTNM)
	LD	B,A
	LD	HL,ASTDTA+32
	LD	DE,DTA+2000H
FRSJ6:
	PUSH	BC
	PUSH	HL
	PUSH	DE
	PUSH	DE
	LD	A,(HL)
	CALL	GETDRP
	POP	DE
	LD	BC,32
	LDIR
	POP	DE
	LD	HL,32
	ADD	HL,DE
	EX	DE,HL
	POP	HL
	INC	HL
	POP	BC
	DJNZ	FRSJ6

	LD	HL,DTA+2000H
	LD	DE,DTA
	LD	BC,1800H
	LDIR
	RET

FRSJ0:
	LD	HL,(CSP3)
	LD	DE,(FSD0)
	CALL	ICOMP3
	JP	Z,RTRUE
	CALL	FRREAD
	JP	NZ,RFALSE
	LD	A,(EXCDTA+16)
	CP	"*"
	CALL	NZ,FRSJ2
FRSJ1:
	CALL	CSR_D
	CALL	CSRWK2
	LD	HL,(CSP3)
	LD	DE,(FSD0)
	CALL	ICOMP3
	JP	Z,RTRUE

	CALL	FRREAD
	JP	NZ,RFALSE
	LD	A,(EXCDTA+16)
	CP	"*"
	CALL	NZ,FRSJ2
	JP	FRSJ1

FRSJ2:
	CALL	GETAST
	LD	A,(HL)
	PUSH	AF
	LD	HL,(FSD1)
	LD	(HL),A
	INC	HL
	LD	(FSD1),HL
	LD	HL,(FSD2)
	POP	AF
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	A,0FFH
	LD	(HL),A
	RET

EXCDIR:
	PUSH	BC
	CALL	GETDRP
	POP	BC
	PUSH	HL
	PUSH	BC
	LD	DE,SUBDTA
	LD	BC,32
	LDIR
	POP	AF
	CALL	GETDRP
	POP	DE
	PUSH	HL
	LD	BC,32
	LDIR
	POP	DE
	LD	HL,SUBDTA
	LD	BC,32
	LDIR
	RET

GETAST:
	LD	HL,(CSP3)
	DEC	HL
	LD	DE,ASTDTA+32
	ADD	HL,DE
	RET

GETDRP:
	LD	L,A
	LD	H,0
	LD	DE,32
	CALL	IMULT
	LD	DE,DTA
	ADD	HL,DE
	RET

FRRPUT:
	XOR	A
	LD	(MCONT),A
	XOR	A
	LD	(SYS12),A
	CALL	FXDRV
	CALL	M_DIR
	CALL	AUTTX3
	CALL	MSCT
	CALL	CSRRST
	CALL	PUTBAR
	LD	HL,16
	LD	(CSP9),HL
	LD	DE,(SYS2)
	ADD	HL,DE
	EX	DE,HL
	CALL	S_CSP
	CALL	CSRWKS
	LD	A,(EDFLG)
	OR	00001000B
	LD	(EDFLG),A
	RET

MSCT:
;------------------- FILE NUMBER
	LD	HL,(ASTDTA)
	LD	H,0
	LD	DE,FRSCT+9
	LD	A,3
	CALL	HL2TXS

	LD	HL,(ASTDTA+1)
	LD	H,0
	LD	DE,FRSCT+13
	LD	A,3
	CALL	HL2TXS
;------------------- DRIVE
	LD	HL,(DEFDRV)
	LD	H,0
	DEC	HL
	LD	DE,DRVSTR
	ADD	HL,DE
	LD	A,(HL)
	LD	(FRSCT+7),A

;------------------- DISK FREE AREA SIZ
	LD	HL,(FRESIZ)
	LD	DE,FRSCT+26
	LD	A,4
	CALL	HL2TXS

	RET

DRVSTR:	DB	"ABCDEFGH"


FRDLJ0:
	CALL	FRREAD
	JP	NZ,RFALSE
	CALL	MFIL
	LD	A,26
	CALL	FRYNA
	JP	NZ,FRDLJ3
	LD	A,30
	CALL	GPUTMS
	CALL	FRREAD
	LD	DE,FCB9
	CALL	MFCB
	JP	NZ,FRDLJ3

	LD	DE,FCB9
	CALL	FDEL
	AND	A
	JP	NZ,FRDLJ4
	LD	A,27;COMPLETE
	CALL	GPUTMS
	JP	RTRUE
FRDLJ3:;CANCELED
	LD	A,28
	CALL	GPUTMS
	JP	RFALSE
FRDLJ4:;ERROR!
	LD	A,29
	CALL	GPUTMS
	JP	RFALSE

FRRNJ0:
	CALL	FRREAD
	JP	NZ,RFALSE
	CALL	MFIL
	LD	A,31
	CALL	FPUTMS
	CALL	FRREAD
	LD	DE,FCB9
	CALL	MFCB
	JP	NZ,FRRNJ3

	LD	DE,WCB6
	LD	HL,FCB9+16
	CALL	INPUT3
	AND	A
	JP	NZ,FRRNJ3
	LD	A,35
	CALL	FPUTMS
	LD	DE,FCB9
	CALL	FNAME
	AND	A
	JP	NZ,FRRNJ4
	LD	A,32;COMPLETE
	CALL	FPUTMS
	JP	RTRUE

FRRNJ3:;CANCELED
	LD	A,33
	CALL	FPUTMS
	JP	RFALSE
FRRNJ4:;ERROR!
	LD	A,34
	CALL	FPUTMS
	JP	RFALSE

MFCB:
	PUSH	DE
	CALL	FRREAD
	LD	A,12
	LD	HL,EXCDTA+4
	POP	DE
	CALL	TX2FCB
	RET

MFIL:
	LD	HL,EXCDTA+4
	LD	DE,MSDTA
	LD	BC,12
	LDIR
	XOR	A
	LD	(MSDTA+16),A
	RET

FRYNA:
	PUSH	AF
	LD	A,(AJUNK)
	AND	A
	JP	NZ,FRYNAJ0
	CALL	RSTMS1
	POP	AF
	CALL	EPUTMS
	CALL	WAIT2
	AND	A
	JP	Z,RTRUE
	AND	10000000B
	JP	NZ,RFALSE
	LD	A,0FFH
	LD	(AJUNK),A
	JP	RTRUE

FRYNAJ0:
	POP	AF
	JP	RTRUE


AMKSRC:
	CALL	MARKJ
	CALL	ERS_CS
	LD	A,(MCONT)
	AND	A
	JP	Z,RFALSE
	LD	A,0FFH
	LD	(SLTMD),A
	CALL	J_HEAD
	XOR	A
	LD	(SLTMD),A
	CALL	FRREAD
	JP	NZ,RFALSE
	LD	A,(EXCDTA+16)
	CP	"*"
	JP	Z,RTRUE
	CALL	MKSRCH
	JP	Z,RTRUE
	CALL	JPOINT
	JP	RFALSE

MKSRCH:
	CALL	CSR_D
	CALL	CSRWK2
	CALL	FRREAD
	JP	NZ,RFALSE
	LD	A,(EXCDTA+16)
	CP	"*"
	JP	Z,RTRUE
	JP	MKSRCH

FRMARK:
	LD	A,(CSP5)
	CP	0FFH
	RET	NZ
	CALL	GTXTAD
	PUSH	BC
	POP	HL
	CALL	NRV1
	CP	" "
	LD	B,1
	LD	A,"*"
	JP	Z,FRMKJ0
	LD	B,0FFH
	LD	A," "
FRMKJ0:
	LD	(FEPBUF+1),A
	LD	A,(MCONT)
	ADD	A,B
	LD	(MCONT),A
	LD	A,1
	LD	(FEPBUF),A
	CALL	ANKW
	CALL	CSR_LD
	RET

;----------------------------------------- VFILER
VFILER:
	CALL	SAVSC2
	LD	DE,SCB5
	CALL	RRST
	LD	(JJUNK),DE
	LD	A,(TXTNUM)
	LD	(IJUNK),A
	LD	A,3
	CALL	EXCTXT
	CALL	SVVM1
	CALL	FRSETP

;----------------------- SCREEN MODE

	XOR	A
	LD	(EDFLG),A
	CALL	EXCRS2
	CALL	CLS1
	CALL	PUTBAR
	CALL	FRRPUT
	LD	HL,KMAP3
	CALL	KANELF
	LD	(KJUNK),A
	CALL	LDVM1
	LD	A,(IJUNK)
	CALL	EXCTXT
	CALL	EXCRS2
	CALL	LODSC2
	LD	A,(KJUNK)
	AND	A
	JP	Z,F_MLOAD
	JP	RFALSE

SVVM1:
	LD	HL,SYS0
	LD	DE,VMSAV1
	LD	BC,MEMSIZ-SYS0
	LDIR
	RET

LDVM1:
	LD	HL,VMSAV1
	LD	DE,SYS0
	LD	BC,MEMSIZ-SYS0
	LDIR
	RET

LODSC2:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL,0E000H
	LD	DE,(BASE1)
	LD	BC,800H
	XOR	A
	CALL	VRMW
	LD	HL,(BASE3)
	LD	DE,255*8
	ADD	HL,DE
	EX	DE,HL
	LD	HL,CHFONTS
	LD	BC,8
	XOR	A
	CALL	M2VR
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

SAVSC2:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	CALL	ERS_CS
	LD	HL,(BASE1)
	LD	DE,0E000H
	LD	BC,800H
	XOR	A
	CALL	VRMW
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET


FRSETP:
	LD	HL,19
	LD	A,(BASE7)
	AND	A
	JP	Z,FRSPJ0
	LD	HL,20
FRSPJ0:
	LD	(SYS1),HL
	LD	HL,1
	LD	(SYS0),HL
	LD	HL,1
	LD	(SCHMD),HL
	LD	A," "
	LD	(RETCH),A
	LD	A,127
	LD	(TABCH),A
	LD	A,2
	LD	(EOFMD),A
	LD	A,0
	LD	(FCSMD),A
	LD	A,0
	LD	(INSMD),A
	RET

	END

