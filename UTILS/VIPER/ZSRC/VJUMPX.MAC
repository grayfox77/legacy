;VJUMP.MAC
CSEG
.Z80

PUBLIC HILIN,LOWLIN,AJLIN
PUBLIC PAGE_U,PAGE_D,ROLL2U,ROLL2D
PUBLIC J_HEAD,J_TAIL,J_LINE,JUMPCSP
PUBLIC G_SSTR,SRCH_U,SRCH_D,SRCHIU,SRCHID
PUBLIC RPLC_U,RPLC_D,RPLC_A,RPLCIU,RPLCID,RPLCIA
PUBLIC TWIN_U,TWIN_D,TWIN_A,JPOINT,MARKJ
PUBLIC AINDNT,LREAD

MACLIB B:\ZERO\MAC\VBASE.INC
MACLIB B:\ZERO\MAC\VMEMORY.INC
MACLIB B:\ZERO\MAC\VSCREEN.INC
MACLIB B:\ZERO\MAC\VEDITOR.INC
MACLIB B:\ZERO\MAC\VINPBOX.INC

MJUNK7: DW 0000H
JJUNK0: DW 0000H
SJUNK0: DW 0000H
SJUNK1: DW 0000H
SJUNK2: DW 0000H

RJUNK0: DW 0000H
RJUNK1: DW 0000H

MARKJ:
 LD HL,(CSP3)
 LD (SJUNK0),HL
 LD HL,(CSP10)
 LD (SJUNK1),HL
 LD HL,(CSP0)
 LD (SJUNK2),HL
 RET

JPOINT:
 LD HL,(SJUNK1)
 CALL AJLIN
 LD A,(SCHMD)
 PUSH AF
 LD A,2
 LD (SCHMD),A
 LD HL,(SJUNK0)
 CALL JUMPCSP
 POP AF
 LD (SCHMD),A
 LD DE,(SJUNK2)
 LD (CSP9),DE
 CALL CSRWK2
 JP RTRUE

TWIN_U:
 LD A,(RPLMD)
 AND A
 JP NZ,RPLC_U
 JP SRCH_U

TWIN_D:
 LD A,(RPLMD)
 AND A
 JP NZ,RPLC_D
 JP SRCH_D
 
TWIN_A:
 CALL RPLC_A
 RET

SRCHIU:
 CALL S_SSTR
 RET NZ
 JP SRCH_U

SRCHID:
 CALL S_SSTR
 RET NZ
 JP SRCH_D

RPLCIA:
 CALL S_RSTR
 RET NZ
 JP RPLC_A

RPLCIU:
 CALL S_RSTR
 RET NZ
 JP RPLC_U

RPLCID:
 CALL S_RSTR
 RET NZ
 JP RPLC_D

AINDNT:
 CALL GTXTAD
 LD H,B
 LD L,C
 LD BC,(CSP4)
 AND A
 SBC HL,BC
 LD A,L
 AND A
 JP Z,INSCR
 PUSH HL
 CALL LREAD
 POP HL
 JP NZ,INSCR
 LD B,L
 LD A,2
 LD (FEPBUF),A
 LD HL,0A0DH
 LD (FEPBUF+1),HL
 LD IY,FEPBUF+3
 LD IX,FEPBUF
 LD DE,EXCDTA
 LD HL,AIDTJ1
AIDTJ0:
 LD A,(DE)
 LD C,A
 CALL TXMACH
 JP Z,INSW
 LD (IY),C
 INC (IX)
 INC IY
 INC DE
 DJNZ AIDTJ0
 JP INSCR
AIDTJ1:
 DB 9," ",0

G_SSTR:
 LD DE,(CSRP)
 CALL S_CSP
 CALL LREAD
 JP NZ,RFALSE

 CALL GTXTAD
 LD A,(SCB2+24)
 LD L,C
 LD H,B
 CALL NSTR3
 LD A,(RPT)
 LD C,A
 LD B,32
 LD IX,SDATA+1
 LD E,0
GSTRJ0:
 IN A,(C)
 LD D,A
 CALL TXTYPE
 CP 3
 JP NC,GSTRJ1
 LD A,D
 LD (IX),A
 INC IX
 INC E
 DJNZ GSTRJ0
GSTRJ1:
 LD A,E
 LD (SDATA),A
 CALL P_SSTR
 LD A,25
 CALL EPUTMS
 XOR A
 LD (RPLMD),A
 JP RTRUE

SRCH_U:
 CALL SRCDJ3
 RET NZ
SRCUJ0:
 CALL SRCH_UB
 JP NZ,SRCDJ1
 CALL LSTSRC
 JP NZ,SRCUJ0
 JP SRCDJ2

SRCH_D:
 CALL SRCDJ3
 RET NZ
SRCDJ0:
 CALL SRCH_DB
 JP NZ,SRCDJ1
 CALL LSTSRC
 JP NZ,SRCDJ0
 JP SRCDJ2

RPLC_A:
 CALL RPLDJ3
 RET NZ
 CALL LSTSRC
 JP NZ,RPLAJ0
 CALL RPLUJ2
 JP NZ,RPLAJ3
RPLAJ0: 
 CALL SRCH_UB
 JP NZ,RPLAJ1
 CALL LSTSRC
 JP NZ,RPLAJ0
 CALL RPLUJ2
 JP Z,RPLAJ0
 JP RPLAJ3
RPLAJ1:
 CALL JPOINT
RPLAJ2:
 CALL SRCH_DB
 JP NZ,RPLAJ3
 CALL LSTSRC
 JP NZ,RPLAJ2
 CALL RPLDJ2
 JP Z,RPLAJ2
RPLAJ3:
 CALL JPOINT
 CALL P_NUM
 LD A,10
 CALL FPUTMS
 JP RTRUE

RPLC_U:
 CALL RPLDJ3
 RET NZ
RPLUJ0:
 CALL SRCH_UB
 JP NZ,RPLDJ1
 CALL LSTSRC
 JP NZ,RPLUJ0
 CALL RPLUJ2
 JP Z,RPLUJ0
 JP RPLDJ1

RPLC_D:
 CALL RPLDJ3
RPLDJ0:
 CALL SRCH_DB
 JP NZ,RPLDJ1
 CALL LSTSRC
 JP NZ,RPLDJ0
 CALL RPLDJ2
 JP Z,RPLDJ0
 JP RPLDJ1

SRCDJ3:
 LD A,(SDATA)
 AND A
 JP Z,RFALSE
 CALL P_SSTR
 LD A,12
 CALL EPUTMS
 CALL MARKJ
 CALL ERS_CS
 CALL CHKBSD
 JP RTRUE

RPLDJ3:
 LD A,(SDATA)
 AND A
 JP Z,RFALSE
 CALL P_SSTR
 CALL P_RSTR
 LD A,8
 CALL FPUTMS
 CALL MARKJ
 CALL ERS_CS
 CALL CHKBSD
 LD HL,0
 LD (RJUNK0),HL
 LD A,(RPAMD)
 LD (RJUNK1),A
 JP RTRUE

RPLDJ2:
 LD DE,(CSRP)
 CALL S_CSP
 CALL S_CSP9
 CALL SNSSTP
 JP Z,RFALSE
 LD A,(RJUNK1)
 AND A
 CALL NZ,RPLDJ5
 JP NZ,RTRUE
 CALL GTXTAD
 LD (LINK0),BC
 LD HL,(SDATA)
 LD H,0
 LD (LINK1),HL
 LD HL,RDATA+1
 LD (LINK2),HL
 LD HL,(RDATA)
 LD H,0
 LD (LINK3),HL
 CALL MKANEL
 LD HL,(LINK0)
 LD DE,(LINK3)
 ADD HL,DE
 LD BC,(CSP12)
 LD DE,(CSP11)
 CALL INCTX2
 CALL S_CSP
 CALL INCTX4
 LD DE,(CSRP)
 CALL S_CSP
 CALL S_CSP9
 LD HL,(RJUNK0)
 INC HL
 LD (RJUNK0),HL
 JP RTRUE

RPLUJ2:
 LD DE,(CSRP)
 CALL S_CSP
 CALL S_CSP9
 CALL SNSSTP
 JP Z,RFALSE
 LD A,(RJUNK1)
 AND A
 CALL NZ,RPLDJ5
 JP NZ,RTRUE
 CALL GTXTAD
 LD (LINK0),BC
 LD HL,(SDATA)
 LD H,0
 LD (LINK1),HL
 LD HL,RDATA+1
 LD (LINK2),HL
 LD HL,(RDATA)
 LD H,0
 LD (LINK3),HL
 CALL MKANEL
 CALL INCTX4
 LD DE,(CSRP)
 CALL S_CSP
 CALL S_CSP9
 LD HL,(RJUNK0)
 INC HL
 LD (RJUNK0),HL
 JP RTRUE

SRCDJ2:
 CALL PUTTX3
 LD DE,(CSRP)
 CALL S_CSP
 CALL S_CSP9
 LD A,24
 CALL FPUTMS
 JP RTRUE

RPLDJ1:
 CALL JPOINT
 CALL P_NUM
 LD A,10
 CALL FPUTMS
 JP RFALSE

RPLDJ5:
 CALL PUTTX3
 CALL PUTBAR
 CALL MVCSR
 LD A,9
 CALL FPUTMS
 CALL WAIT2
 PUSH AF
 CALL ERS_CS
 LD A,8
 CALL FPUTMS
 POP AF
 JP Z,RTRUE
 AND 10000000B
 JP Z,RPDJ50
 JP RFALSE
RPDJ50:
 XOR A
 LD (RJUNK1),A
 JP RTRUE

SRCDJ1:
 CALL JPOINT
 LD A,13
 CALL FPUTMS
 JP RFALSE

CHKBSD:
 LD A,(FZSMD)
 AND A
 RET Z
 LD A,(SDATA)
 LD HL,SDATA+1
 CALL TXBIG
 RET

P_NUM:
 LD HL,(RJUNK0)
 LD DE,MSDTA
 CALL HL2TX
 LD E,A
 LD D,0
 LD HL,MSDTA
 ADD HL,DE
 LD A," "
 LD (HL),A
 INC HL
 XOR A
 LD (HL),A
 RET

P_SSTR:
 LD A,(SDATA)
 AND A
 RET Z
 LD HL,SDATA+1
 LD DE,MSDTA
 LD A,(SDATA)
 LD C,A
 LD B,0
 PUSH BC
 LDIR
 POP BC
 LD HL,MSDTA
 ADD HL,BC
 XOR A
 LD (HL),A

 CALL RSTMS1
 RET

P_RSTR:
 LD A,(RDATA)
 AND A
 RET Z
 LD HL,MSDTA
 LD BC,(SDATA)
 LD B,0
 INC BC
 ADD HL,BC
 EX DE,HL
 LD HL,RDATA+1
 LD A,(RDATA)
 LD C,A
 LD B,0
 PUSH DE
 PUSH BC
 LDIR
 POP BC
 POP HL
 ADD HL,BC
 XOR A
 LD (HL),A

 CALL RSTMS1
 RET

SRCH_UB:
 CALL CSRWK2
 CALL SRCH_L
 JP Z,RETCSR
SRUJ0:
 CALL SNSSTP
 JP Z,RFALSE

 LD HL,(CSP3)
 PUSH HL
 CALL CSR_U
 CALL CSRWK2
 POP HL
 LD DE,(CSP3)
 CALL ICOMP2
 JP NZ,RFALSE

 CALL RLSRCH
 JP NZ,SRUJ0
 JP RETCSR


SRCH_DB:
 CALL CSRWK2
 CALL SRCH_R
 JP Z,RETCSR
SRDJ0:
 CALL SNSSTP
 JP Z,RFALSE
 LD HL,(CSP13)
 LD DE,(MEM10)
 CALL ICOMP1
 JP Z,RFALSE

 CALL CSR_D
 CALL CSRWK2
 CALL LRSRCH
 JP NZ,SRDJ0
 JP RETCSR
;!!!!!!!!!!!!! POINT
SRCH_R:
 LD DE,(CSRP)
 CALL S_CSP
 CALL LREAD
 JP NZ,RFALSE

 CALL BK_CS
 PUSH BC
 CALL CCRF1
 LD HL,(TXTF0)
 LD H,0
 DEC HL
 POP BC
 ADD HL,BC
 PUSH BC

 LD HL,(CSP13)
 DEC HL
 LD DE,(MEM10)
 CALL ICOMP1
 JP NZ,SRCRJ0
 LD HL,(MEM10)
SRCRJ0:
 AND A
 SBC HL,BC
 LD B,L;<-LOOP NUMBER

 POP HL
 LD DE,(CSP4)
 AND A
 SBC HL,DE
 LD DE,EXCDTA+1
 ADD HL,DE;HL;<-START
 LD A,(SDATA+1)
 LD C,A
 LD A,B
 OR A
 JP Z,RFALSE
SRRJ0:
 LD A,(HL)
 CP C
 JP Z,RTRUE
 INC HL
 DJNZ SRRJ0
 JP RFALSE


SRCH_L:
 LD DE,(CSRP)
 CALL S_CSP
 CALL LREAD
 JP NZ,RFALSE

 CALL GTXTAD
 LD L,C
 LD H,B
 LD BC,(CSP4)
 AND A
 SBC HL,BC

 LD B,L;<-LOOP NUMBER
 LD DE,EXCDTA-1
 ADD HL,DE;HL;<-START
 LD A,(SDATA+1)
 LD C,A
 LD A,B
 OR A
 JP Z,RFALSE
SRLJ0:
 LD A,(HL)
 CP C
 JP Z,RTRUE
 DEC HL
 DJNZ SRLJ0
 JP RFALSE

RETCSR:
 LD DE,EXCDTA
 AND A
 SBC HL,DE
 LD DE,(CSP4)
 ADD HL,DE
 LD BC,(CSP4)
 LD DE,(CSP2)
 CALL INCTX2
 CALL S_CSP

 JP RTRUE

LSTSRC:
 CALL GTXTAD
 LD H,B
 LD L,C
 LD DE,EXCDTA
 LD BC,32
 LD A,(SCB2+24)
 CALL VR2M
 LD A,(FZSMD)
 AND A
 CALL NZ,LSSJ0
 LD HL,EXCDTA
 LD DE,SDATA+1
 LD A,(SDATA)
 CALL TXCOMP
 RET
LSSJ0:
 LD A,32
 LD HL,EXCDTA
 CALL TXBIG
 RET

LREAD:
 LD HL,(CSP4)
 LD DE,(MEM10)
 CALL ICOMP1
 JP Z,RFALSE
 LD HL,(CSP13)
 LD DE,(MEM10)
 CALL ICOMP1
 CALL Z,LRJ0
 LD DE,(CSP4)
 AND A
 SBC HL,DE
 LD B,H
 LD C,L
 PUSH BC
 PUSH BC
 LD HL,(CSP4)
 LD DE,EXCDTA
 LD A,(SCB2+24)
 CALL VR2M
 POP BC
 LD A,(FZSMD)
 AND A
 CALL NZ,LRJ1
 POP DE
 LD B,E
 JP RTRUE
LRJ0:
 LD HL,(MEM10)
 RET
LRJ1:
 LD A,C
 LD HL,EXCDTA
 CALL TXBIG
 RET

LRSRCH:
 CALL LREAD
 JP NZ,RFALSE
 LD HL,EXCDTA
 LD A,(SDATA+1)
 LD C,A
LRSJ0:
 LD A,(HL)
 CP C
 JP Z,RTRUE
 INC HL
 DJNZ LRSJ0
 JP RFALSE

RLSRCH:
 CALL LREAD
 JP NZ,RFALSE
 LD HL,EXCDTA
 DEC DE
 ADD HL,DE
 LD A,(SDATA+1)
 LD C,A
RLSJ0:
 LD A,(HL)
 CP C
 JP Z,RTRUE
 DEC HL
 DJNZ RLSJ0
 JP RFALSE

HILIN:
 LD DE,(SYS2)
 CALL S_CSP
 CALL S_CSP9
 RET

LOWLIN:
 LD DE,(SYS3)
 DEC DE
 CALL S_CSP
 CALL BK_CS
 CALL S_CSP9
 RET

ROLL2U:
 LD HL,(CSP10)
 LD A,H
 OR L
 JP Z,CSR_U
 DEC HL
 LD A,H
 OR L
 JP Z,LU3J0
 CALL BCPS20
 CALL ERS_CS
 LD HL,(SYS5)
 LD BC,(WIDTH)
 AND A
 SBC HL,BC
 PUSH HL
 LD HL,(SYS8)
 ADD HL,BC
 POP BC
 LD DE,(SYS2)
 EX DE,HL
 XOR A
 CALL VRMX
 LD BC,(LHEAD+2)
 LD HL,(SYS8)
 LD DE,(WIDTH)
 ADD HL,DE
 LD DE,(SYS2)
 CALL PUTTX1
 LD DE,(CSRP)
 CALL S_CSP
 RET
LU3J0:
 CALL BCPS10
 CALL SCRDW1
 LD BC,(LHEAD+2)
 LD DE,(SYS2)
 LD HL,(SYS8)
 CALL PUTTX1
 LD DE,(CSRP)
 CALL S_CSP
 RET

ROLL2D:
 LD HL,(SYS1)
 INC HL
 CALL GETLH
 EX DE,HL 
 LD HL,(MEM10)
 CALL ICOMP0
 AND A
 JP NZ,CSR_D
 CALL FCPS20
 CALL ERS_CS
 LD HL,(SYS5)
 LD BC,(WIDTH)
 AND A
 SBC HL,BC
 PUSH HL
 LD HL,(SYS8)
 ADD HL,BC
 POP BC
 LD DE,(SYS2)
 XOR A
 CALL VRMX
 LD HL,(SYS1)
 DEC HL
 CALL GETLH
 PUSH HL
 POP BC
 LD HL,(SYS4)
 LD DE,(WIDTH)
 AND A
 SBC HL,DE
 EX DE,HL
 LD HL,(SYS4)
 CALL PUTTX1
 LD HL,(SYS1)
 CALL SETLH
 LD HL,(SYS3)
 CALL PUTTX1
 LD HL,(SYS1)
 INC HL
 CALL SETLH
 LD DE,(CSRP)
 CALL S_CSP
 RET

FCPS20:
 LD DE,(LHEAD)
 LD HL,(LHEAD+2)
 AND A
 SBC HL,DE
 LD (SUBDTA),HL
 PUSH HL
 LD DE,(LHEAD+2)
 LD HL,(LHEAD+4)
 AND A
 SBC HL,DE
 LD (SUBDTA+1),HL
 PUSH HL
 LD BC,2
 LD DE,SCB3
 LD HL,SUBDTA
 CALL WDST
 POP HL
 POP DE
 ADD HL,DE
 CALL M2T
 LD HL,(CSP10)
 INC HL
 INC HL
 LD (CSP10),HL
 LD L,0
 LD A,(SYS1)
 DEC A
 LD H,A
 CALL S_LHEAD
 CALL UPLH2
 RET

BCPS20:
 LD HL,(CSP10)
 DEC HL
 DEC HL
 LD (CSP10),HL
 LD BC,2
 LD DE,SCB3
 LD HL,SUBDTA
 CALL RDST
 LD HL,(SUBDTA)
 LD H,0
 PUSH HL
 LD DE,(SUBDTA+1)
 LD D,0
 ADD HL,DE
 CALL T2M
 LD L,2
 LD A,(SYS1)
 INC A
 LD H,A
 CALL DWLH2
 CALL S_LHEAD
 POP DE
 LD HL,(LHEAD)
 ADD HL,DE
 LD (LHEAD+2),HL
 RET

PAGE_D:
 CALL FCPS40
 AND A
 JP NZ,LOWLIN
 CALL INCTX4
 CALL PUTTX4
 LD DE,(CSRP)
 CALL S_CSP
 RET

PAGE_U:
 CALL BCPS40
 AND A
 JP NZ,HILIN
 CALL PUTTX3
 LD DE,(CSRP)
 CALL S_CSP
 RET
  
J_HEAD:
 LD HL,0
 CALL JUMPCSP
 RET

J_TAIL:
 LD HL,0FFFFH
 CALL JUMPCSP
 RET

J_LINE:
 LD DE,WCB8
 CALL INPUT2
 RET NZ
 CALL JUMPCSP
 RET

JUMPCSP:
 LD (JJUNK0),HL
 LD DE,(CSP10)
 INC DE
 AND A
 SBC HL,DE
 JP C,JUMPUP
 JP JUMPDW

JUMPUP:
 CALL PUTSL
 LD HL,(CSP10)
 LD DE,(JJUNK0)
 AND A
 SBC HL,DE
 LD HL,(CSP10)
 JP C,JUJ0
 PUSH HL
 CALL J1SCUP
 CALL SNSSTP;BREAK?
 POP HL
 JP Z,JUJ0X;BREAK JUMP
 LD DE,(CSP10)
 AND A
 SBC HL,DE
 LD A,H 
 OR L
 JP NZ,JUMPUP
JUJ0X:
 CALL AUTTX3
 CALL HILIN
 CALL KEYRST
 RET
JUJ0:
 LD HL,(JJUNK0)
 LD DE,(CSP10)
 AND A
 SBC HL,DE
 LD DE,(WIDTH)
 CALL IMULT
 LD DE,(SYS7)
 ADD HL,DE
 LD DE,(CSP0)
 ADD HL,DE
 EX DE,HL
 PUSH DE
 CALL AUTTX3
 POP DE
 CALL S_CSP
 CALL S_CSP9
 CALL KEYRST
 RET 

JUMPDW:
 CALL PUTSL
 LD HL,(CSP10)
 PUSH HL
 LD DE,(SYS1)
 ADD HL,DE
 LD DE,(JJUNK0)
 AND A
 SBC HL,DE
 POP HL
 JP NC,JUJ0
 PUSH HL
 CALL J1SCDW
 CALL SNSSTP
 POP HL
 JP Z,JDJ0X
 LD DE,(CSP10)
 AND A
 SBC HL,DE
 LD A,H 
 OR L
 JP NZ,JUMPDW
JDJ0X:
 CALL INCTX4
 CALL CHKAJE
 CALL NZ,AJEOF1
 CALL PUTTX4
 CALL LOWLIN
 CALL KEYRST
 RET

J1SCDW:
 CALL FCPS40 
 AND A
 JP NZ,LOWLIN
 CALL INCTX3
 LD DE,(CSRP)
 CALL S_CSP
 RET

J1SCUP:
 CALL BCPS40
 AND A
 JP NZ,HILIN
 LD DE,(CSRP)
 CALL S_CSP
 RET

FCPS40:
 LD HL,(SYS1)
 INC HL
 CALL GETLH
 EX DE,HL 
 LD HL,(MEM10)
 CALL ICOMP0
 AND A
 JP NZ,FCP4J2

 LD A,(SYS1)
 LD B,A
 LD DE,(LHEAD)
 LD IX,LHEAD+2
 LD IY,SUBDTA
FCP4J0:
 LD L,(IX)
 LD H,(IX+1)
 PUSH HL
 AND A
 SBC HL,DE
 LD (IY),L
 POP DE
 INC IX
 INC IX
 INC IY
 DJNZ FCP4J0

 LD A,(SYS1)
 LD C,A
 LD B,0
 LD DE,SCB3
 LD HL,SUBDTA
 CALL WDST
 LD HL,(SYS1)
 CALL GETLH
 LD DE,(LHEAD)
 AND A
 SBC HL,DE
 CALL M2T  
 CALL S_LHEAD
 LD HL,(CSP10)
 LD DE,(SYS1) 
 ADD HL,DE
 LD (CSP10),HL
 XOR A
 RET
FCP4J2:
 LD A,0FFH
 RET

BCPS40:
 LD HL,(CSP10)
 LD DE,(SYS1)
 AND A
 SBC HL,DE
 JP NC,BCP4J0
 LD DE,(CSP10)
 LD A,D
 OR E
 JP Z,BCP4J2
 LD HL,0
BCP4J0:
 LD (CSP10),HL
 LD (MJUNK7),DE

 LD A,(MJUNK7)
 LD C,A
 LD B,0
 LD DE,SCB3
 LD HL,SUBDTA
 CALL RDST

 LD A,(MJUNK7)
 LD B,A
 LD HL,0
 LD IY,SUBDTA
BCP4J1:
 LD E,(IY)
 LD D,0
 ADD HL,DE
 INC IY
 DJNZ BCP4J1
 CALL T2M  
 CALL S_LHEAD
 XOR A
 RET
BCP4J2:
 LD A,0FFH
 RET

AJLIN:
 LD DE,(CSP10)
 AND A
 SBC HL,DE
 JP C,AJLJ1
 LD A,L
 OR H
 RET Z
 EX DE,HL
 LD HL,(SYS1)
 CALL IMOD
 LD A,L
 AND A
 RET Z
 LD B,L
AJLJ0:
 PUSH BC
 CALL FCPS10
 POP BC
 DJNZ AJLJ0
 CALL INCTX3
 RET
AJLJ1:
 LD A,L
 OR H
 RET Z
 CALL INEG
 EX DE,HL
 LD HL,(SYS1)
 CALL IMOD
 LD A,L
 AND A
 RET Z
 LD B,L
AJLJ2:
 PUSH BC
 CALL BCPS10
 POP BC
 DJNZ AJLJ2
 RET

CHKAJE:
 LD HL,(SYS1)
 CALL GETLH
 EX DE,HL
 LD HL,(MEM10)
 CALL ICOMP0
 AND A
 RET Z
 LD HL,(CSP10)
 LD A,L
 OR H
 RET Z
 LD A,0FFH
 AND A
 RET

AJCHK:
 LD HL,0
AJCJ0:
 PUSH HL
 LD HL,(SYS1)
 CALL GETLH
 EX DE,HL
 LD HL,(MEM10)
 CALL ICOMP0
 AND A
 JP Z,AJCJ1
 CALL BCPS10
 POP HL
 LD BC,(WIDTH)
 ADD HL,BC
 JP AJCJ0
AJCJ1:
 POP HL
 RET

AJEOF1:
 CALL AJCHK
 CALL INCTX4
 RET

END